
public import __primitives__.(Static);


//
// constructors, move, destroy
//

[A]
overload Static[A]() returned:Static[A] {
}

[A]
overload Static[A](src:Static[A]) returned:Static[A] {
}

[A]
overload move(src:Static[A]) returned:Static[A] {
}

[A]
overload destroy(x:Static[A]) {
}



//
// equals?, lesser?
//

[A, B]
overload equals?(a:Static[A], b:Static[B]) = false;

[A]
overload equals?(a:Static[A], b:Static[A]) = true;

[A]
overload lesser?(a:Static[A], b:Static[A]) = false;



//
// wrapStatic, unwrapStatic
//

macro wrapStatic(x) {
    return Static[x]();
}

[A]
unwrapStatic(static A) = A;



//
// call
//

[F]
overload call(static F, forward ...args) =
    forward ...F(...args);



//
// fieldRef
//

[Module, Ident]
overload fieldRef(static Module, static Ident) =
    forward staticFieldRef(Module, Ident);

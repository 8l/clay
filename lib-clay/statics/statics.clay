
public import __primitives__.(Static);


//
// constructors, moveUnsafe, resetUnsafe, destroy
//

[A]
overload Static[A]() returned:Static[A] {
}

[A]
overload Static[A](src:Static[A]) returned:Static[A] {
}

[A]
overload moveUnsafe(src:Static[A]) returned:Static[A] {
}

[A]
overload resetUnsafe(x:Static[A]) {
}

[A]
overload destroy(x:Static[A]) {
}



//
// equals?, lesser?
//

[A, B]
overload equals?(a:Static[A], b:Static[B]) = false;

[A]
overload equals?(a:Static[A], b:Static[A]) = true;

[A]
overload lesser?(a:Static[A], b:Static[A]) = false;



//
// wrapStatic, unwrapStatic
//

callbyname wrapStatic(x) {
    return Static[x]();
}

[A]
unwrapStatic(static A) = A;



//
// call
//

[F]
overload call(static F, forward ...args) =
    forward ...F(...args);



//
// fieldRef
//

[Module, Ident]
overload fieldRef(static Module, static Ident) =
    forward staticFieldRef(Module, Ident);



record HashMap[K,V] (
    buckets : Vector[NodePtr[K,V]],
    size : SizeT,
);


[K,V]
overload RegularRecord?(static HashMap[K,V]) = false;

alias NodePtr[K,V] = SharedPointer[Node[K,V]];

private record Node[K,V] (
    key : K,
    value : V,
    next : NodePtr[K,V],
);



//
// default constructor
//

[K,V]
overload HashMap[K,V]() {
    var buckets = Vector[NodePtr[K,V]]();
    reserve(buckets, 32);
    for (i in range(1024))
        push(buckets, nullShared(Node[K,V]));
    return HashMap[K,V](move(buckets), SizeT(0));
}



//
// copy constructor
//

[K,V]
overload HashMap[K,V](src:HashMap[K,V]) {
    var buckets = Vector[NodePtr[K,V]]();
    reserve(buckets, size(src.buckets));
    for (b in src.buckets)
        push(buckets, cloneList(b));
    return HashMap[K,V](move(buckets), src.size);
}

[K,V]
private cloneList(_b:NodePtr[K,V]) {
    var b = _b;
    var list = nullShared(Node[K,V]);
    while (not null?(b)) {
        list = new Node[K,V](b^.key, b^.value, list);
        b = b^.next;
    }
    var result = nullShared(Node[K,V]);
    while (not null?(list)) {
        var temp = list^.next;
        list^.next = result;
        result = list;
        list = temp;
    }
    return result;
}



//
// moveUnsafe, resetUnsafe
//

[K,V]
overload BitwiseMovedType?(static HashMap[K,V]) = true;

[K,V]
overload resetUnsafe(a:HashMap[K,V]) {
    resetUnsafe(a.buckets);
}



//
// destroy
//

[K,V]
overload destroy(a:HashMap[K,V]) {
    destroy(a.buckets);
}



//
// size
//

[K,V]
overload size(a:HashMap[K,V]) = a.size;



//
// lookup
//

[K,V]
overload lookup(a:HashMap[K,V], key:K) {
    var i = bucketId(a, key);
    return findInBucket(a.buckets[i], key);
}



//
// put
//

[K,V]
overload put(a:HashMap[K,V], key:K, forward value:V) {
    var i = bucketId(a, key);
    var ptr = findInBucket(a.buckets[i], key);
    if (not null?(ptr)) {
        ptr^ = value;
        return;
    }
    var node = new Node(key, value, a.buckets[i]);
    a.buckets[i] = node;
    a.size += 1;
}



//
// index
//

[K,V]
overload index(a:HashMap[K,V], key:K) {
    var i = bucketId(a, key);
    var ptr = findInBucket(a.buckets[i], key);
    if (not null?(ptr))
        return ref ptr^;
    var node = new Node(key, V(), a.buckets[i]);
    a.buckets[i] = node;
    a.size += 1;
    return ref node^.value;
}



//
// remove
//

[K,V]
overload remove(a:HashMap[K,V], key:K) {
    var i = bucketId(a, key);
    var prevNode = nullShared(Node[K,V]);
    var node = a.buckets[i];
    while (not null?(node)) {
        if (node^.key == key)
            break;
        prevNode = node;
        node = node^.next;
    }
    if (null?(node))
        return false;
    if (null?(prevNode))
        a.buckets[i] = node^.next;
    else
        prevNode^.next = node^.next;
    a.size -= 1;
    return true;
}



//
// items
//

[K,V]
overload items(forward a:HashMap[K,V]) {
    var x = HashMapItems(captureValue(a), K, V);
    while (null?(x.node) and hasNext?(x.bucketsIter))
        x.node = next(x.bucketsIter);
    return move(x);
}

private record HashMapItems[CapturedMap,K,V] =
    map: CapturedMap,
    bucketsIter : SequenceIteratorType(Vector[NodePtr[K,V]]),
    node : NodePtr[K,V];

[CapturedMap,K,V]
overload HashMapItems(forward captured:CapturedMap, static K, static V)
    returned:HashMapItems[CapturedMap,K,V]
{
    returned.map <-- captured;
    try {
        returned.bucketsIter <-- iterator(capturedRef(returned.map).buckets);
        returned.node = nullShared(Node[K,V]);
    } catch (ex) {
        destroy(returned.map);
        throw ex;
    }
}

[C,K,V]
overload iterator(x:HashMapItems[C,K,V]) = x;

[C,K,V]
overload hasNext?(x:HashMapItems[C,K,V]) =
    not null?(x.node);

[C,K,V]
overload next(x:HashMapItems[C,K,V]) {
    assert(not null?(x.node));
    ref key, value = x.node^.key, x.node^.value;
    x.node = x.node^.next;
    while (null?(x.node) and hasNext?(x.bucketsIter))
        x.node = next(x.bucketsIter);
    return ref key, value;
}



//
// assignment
//

[K,V]
overload assign(to: HashMap[K,V], lvalue from: HashMap[K,V]) {
    var tmp = from;
    destroy(to);
    to <-- move(tmp);
}



//
// helper procs
//

[K,V]
private bucketId(a:HashMap[K,V], key:K) =
    hash(key) % size(a.buckets);

[K,V]
private findInBucket(_node:NodePtr[K,V], key:K) {
    var node = _node;
    while (not null?(node)) {
        if (node^.key == key)
            return &node^.value;
        node = node^.next;
    }
    return null(V);
}

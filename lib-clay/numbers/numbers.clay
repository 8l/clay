import base.*;


// numeric type predicates

static overloadable SignedInteger?;

[T] static overload SignedInteger?(T) = false;
static overload SignedInteger?(Int8) = true;
static overload SignedInteger?(Int16) = true;
static overload SignedInteger?(Int32) = true;
static overload SignedInteger?(Int64) = true;


static overloadable UnsignedInteger?;

[T] static overload UnsignedInteger?(T) = false;
static overload UnsignedInteger?(UInt8) = true;
static overload UnsignedInteger?(UInt16) = true;
static overload UnsignedInteger?(UInt32) = true;
static overload UnsignedInteger?(UInt64) = true;


static overloadable Integer?;

[T] static overload Integer?(T) = SignedInteger?(T) or UnsignedInteger?(T);
[A, B] static overload Integer?(A, B) = Integer?(A) and Integer?(B);


static overloadable ByteSizedInteger?;

[T] static overload ByteSizedInteger?(T) = false;
static overload ByteSizedInteger?(Int8) = true;
static overload ByteSizedInteger?(UInt8) = true;


static overloadable Float?;

[T] static overload Float?(T) = false;
static overload Float?(Float32) = true;
static overload Float?(Float64) = true;
[A, B] static overload Float?(A, B) = Float?(A) and Float?(B);


static overloadable Numeric?;

[T] static overload Numeric?(T) = Integer?(T) or Float?(T);
[A, B] static overload Numeric?(A, B) = Numeric?(A) and Numeric?(B);



// BiggerInteger?, BiggerFloat?, BiggerNumeric?

[A,B | Integer?(A,B)]
private static BiggerInteger?(A, B) =
    (TypeSize(A) > TypeSize(B)) or ((TypeSize(A) == TypeSize(B)) and
                                    UnsignedInteger?(A) and
                                    SignedInteger?(B));

[A,B | Float?(A,B)]
private static BiggerFloat?(A, B) = TypeSize(A) > TypeSize(B);


private static overloadable BiggerNumeric?;

[A,B | Float?(A) and Integer?(B)]
static overload BiggerNumeric?(A, B) = true;

[A,B | Integer?(A) and Float?(B)]
static overload BiggerNumeric?(A, B) = false;

[A,B | Integer?(A,B)]
static overload BiggerNumeric?(A, B) = BiggerInteger?(A,B);

[A,B | Float?(A,B)]
static overload BiggerNumeric?(A, B) = BiggerFloat?(A,B);


// toBiggerNumericType

private overloadable toBiggerNumericType;

[A | Numeric?(A)]
overload toBiggerNumericType(static A, static A, v) = v;

[A,B | Numeric?(A,B) and BiggerNumeric?(A,B)]
overload toBiggerNumericType(static A, static B, v) = A(v);

[A,B | Numeric?(A,B) and BiggerNumeric?(B,A)]
overload toBiggerNumericType(static A, static B, v) = B(v);


// constructors

[T1,T2 | Numeric?(T1,T2)]
overload T1(src:T2) = numericConvert(T1, src);

[T | Numeric?(T)]
overload T(src:T) : T {
    primitiveCopy(returned, src);
}


// assign

[A, B | Numeric?(A,B) and BiggerNumeric?(A,B)]
overload assign(dest:A, src:B) {
    primitiveCopy(dest, A(src));
}

[A, B | Integer?(A,B) and (TypeSize(A) == TypeSize(B))]
overload assign(dest:A, src:B) {
    primitiveCopy(dest, A(src));
}

[T | Numeric?(T)]
overload assign(dest:T, src:T) {
    primitiveCopy(dest, src);
}


// equals?, lesser?

[A,B | Numeric?(A,B)]
overload equals?(a:A, b:B) = numericEquals?(toBiggerNumericType(A, B, a),
                                            toBiggerNumericType(A, B, b));
[A | Numeric?(A)]
overload equals?(a:A, b:A) = numericEquals?(a, b);

[A,B | Numeric?(A,B)]
overload lesser?(a:A, b:B) = numericLesser?(toBiggerNumericType(A, B, a),
                                            toBiggerNumericType(A, B, b));
[A | Numeric?(A)]
overload lesser?(a:A, b:A) = numericLesser?(a, b);


// binary ops

[A,B | Numeric?(A,B)]
overload add(a:A, b:B) = numericAdd(toBiggerNumericType(A, B, a),
                                    toBiggerNumericType(A, B, b));
[A | Numeric?(A)]
overload add(a:A, b:A) = numericAdd(a, b);


[A,B | Numeric?(A,B)]
overload subtract(a:A, b:B) = numericSubtract(toBiggerNumericType(A, B, a),
                                              toBiggerNumericType(A, B, b));
[A | Numeric?(A)]
overload subtract(a:A, b:A) = numericSubtract(a, b);


[A,B | Numeric?(A,B)]
overload multiply(a:A, b:B) = numericMultiply(toBiggerNumericType(A, B, a),
                                              toBiggerNumericType(A, B, b));
[A | Numeric?(A)]
overload multiply(a:A, b:A) = numericMultiply(a, b);


[A,B | Numeric?(A,B)]
overload divide(a:A, b:B) = numericDivide(toBiggerNumericType(A, B, a),
                                          toBiggerNumericType(A, B, b));
[A | Numeric?(A)]
overload divide(a:A, b:A) = numericDivide(a, b);


[A,B | Integer?(A,B)]
overload remainder(a:A, b:B) = integerRemainder(toBiggerNumericType(A, B, a),
                                                toBiggerNumericType(A, B, b));
[A | Integer?(A)]
overload remainder(a:A, b:A) = integerRemainder(a, b);


// unary ops

[A | Numeric?(A)]
overload plus(a:A) = a;

[A | Numeric?(A)]
overload minus(a:A) = numericNegate(a);


// inc, dec

[A | Numeric?(A)]
overload inc(a:A) {
    a += A(1);
}

[A | Numeric?(A)]
overload dec(a:A) {
    a -= A(1);
}


// bitwise ops

[A,B | Integer?(A,B)]
overload shiftLeft(a:A, b:B) = integerShiftLeft(a, A(b));

[A,B | Integer?(A,B)]
overload shiftRight(a:A, b:B) = integerShiftRight(a, A(b));

[A,B | Integer?(A,B)]
overload bitwiseAnd(a:A, b:B) = integerBitwiseAnd(toBiggerNumericType(A, B, a),
                                                  toBiggerNumericType(A, B, b));
[A | Integer?(A)]
overload bitwiseAnd(a:A, b:A) = integerBitwiseAnd(a, b);

[A,B | Integer?(A,B)]
overload bitwiseOr(a:A, b:B) = integerBitwiseOr(toBiggerNumericType(A, B, a),
                                                toBiggerNumericType(A, B, b));
[A | Integer?(A)]
overload bitwiseOr(a:A, b:A) = integerBitwiseOr(a, b);

[A,B | Integer?(A,B)]
overload bitwiseXor(a:A, b:B) = integerBitwiseXor(toBiggerNumericType(A, B, a),
                                                  toBiggerNumericType(A, B, b));
[A | Integer?(A)]
overload bitwiseXor(a:A, b:A) = integerBitwiseXor(a, b);

[A | Integer?(A)]
overload bitwiseNot(a:A) = integerBitwiseNot(a);

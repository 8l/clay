
import numbers.parser.errno.*;
import printer.(error);
import printer.formatter.(repr);
import strings.*;
import libc;
import sequences.*;



/// @section  Generic parsers 

private define parseIntWith;
private define parseFloatWith;
private define parseImagWith;


[S when String?(S)]
overload parseIntWith(s:S, intType, convFunc) =
    parseIntWith(String(s), intType, convFunc);

[S when String?(S)]
overload parseFloatWith(s:S, floatType, convFunc) =
    parseIntWith(String(s), floatType, convFunc);

[S when String?(S)]
overload parseImagWith(s:S, imagType, convFunc) =
    parseImagWith(String(s), imagType, convFunc);


[S when CCompatibleString?(S)]
overload parseIntWith(s:S, intType, convFunc) {
    var p = cstring(s);
    var end = null(CChar);
    var value = convFunc(p, @end, 0);
    var typeName = StaticName(intType);
    if (end^ != 0)
        error("invalid " ++ typeName ++ ": ", repr(s));
    if ((errno() == ERANGE) or value < Least(intType) or value > Greatest(intType))
        error(typeName ++ " out of range: ", repr(s));
    return intType(value);
}

[S when CCompatibleString?(S)]
overload parseFloatWith(s:S, floatType, convFunc) {
    var p = cstring(s);
    var end = null(CChar);
    var value = convFunc(p, @end);
    var typeName = StaticName(floatType);
    if (end^ != 0)
        error("invalid " ++ typeName ++ ": ", repr(s));
    if (errno() == ERANGE)
        error(typeName ++ " out of range: ", repr(s));
    return floatType(value);
}

[S when CCompatibleString?(S)]
overload parseImagWith(s:S, imagType, convFunc) {
    var p = cstring(s);
    var end = null(CChar);
    var value = convFunc(p, @end);
    var typeName = StaticName(imagType);
    if (end^ != 0)
        error("invalid " ++ typeName ++ ": ", repr(s));
    if (errno() == ERANGE)
        error(typeName ++ " out of range: ", repr(s));
    return numericConvert(imagType,value);
}

/// @section  parseInt8, parseUInt8 

[S when String?(S)]
parseInt8(s:S) = parseIntWith(s, Int8, libc.strtol);

[S when String?(S)]
parseUInt8(s:S) = parseIntWith(s, UInt8, libc.strtoul);


/// @section  parseInt16, parseUInt16 

[S when String?(S)]
parseInt16(s:S) = parseIntWith(s, Int16, libc.strtol);

[S when String?(S)]
parseUInt16(s:S) =  parseIntWith(s, UInt16, libc.strtoul);


/// @section  parseInt32, parseUInt32 

[S when String?(S)]
parseInt32(s:S) = parseIntWith(s, Int32, libc.strtol);

[S when String?(S)]
parseUInt32(s:S) = parseIntWith(s, UInt32, libc.strtoul);


/// @section  parseInt64, parseUInt64 

[S when String?(S)]
parseInt64(s:S) = parseIntWith(s, Int64, libc.strtoll);

[S when String?(S)]
parseUInt64(s:S) = parseIntWith(s, UInt64, libc.strtoull);


/// @section  parseFloat32 

[S when String?(S)]
parseFloat32(s:S) = parseFloatWith(s, Float32, libc.strtod);


/// @section  parseFloat64 

[S when String?(S)]
parseFloat64(s:S) = parseFloatWith(s, Float64, libc.strtod);

/// @section  parseFloat80 

[S when String?(S)]
parseFloat80(s:S) = parseFloatWith(s, Float80, libc.strtold);


/// @section  parseImag32 

[S when String?(S)]
parseImag32(s:S) = parseImagWith(s, Imag32, libc.strtod);


/// @section  parseImag64 

[S when String?(S)]
parseImag64(s:S) = parseImagWith(s, Imag64, libc.strtod);

/// @section  parseComplex80 

[S when String?(S)]
parseImag80(s:S) = parseImagWith(s, Imag80, libc.strtold);





/// @section  parse numbers from strings 

[S when String?(S)]
forceinline overload Int8(s:S)  = parseInt8(s);

[S when String?(S)]
forceinline overload Int16(s:S) = parseInt16(s);

[S when String?(S)]
forceinline overload Int32(s:S) = parseInt32(s);

[S when String?(S)]
forceinline overload Int64(s:S) = parseInt64(s);

[S when String?(S)]
forceinline overload UInt8(s:S)  = parseUInt8(s);

[S when String?(S)]
forceinline overload UInt16(s:S) = parseUInt16(s);

[S when String?(S)]
forceinline overload UInt32(s:S) = parseUInt32(s);

[S when String?(S)]
forceinline overload UInt64(s:S) = parseUInt64(s);

[S when String?(S)]
forceinline overload Float32(s:S) = parseFloat32(s);

[S when String?(S)]
forceinline overload Float64(s:S) = parseFloat64(s);

[S when String?(S)]
forceinline overload Float80(s:S) = parseFloat80(s);

[S when String?(S)]
forceinline overload Imag32(s:S) = parseImag32(s);

[S when String?(S)]
forceinline overload Imag64(s:S) = parseImag64(s);

[S when String?(S)]
forceinline overload Imag80(s:S) = parseImag80(s);


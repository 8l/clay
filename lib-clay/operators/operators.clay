

//
// core operations
//

procedure destroy;
procedure move;
procedure dirtyMove;
procedure assign;

procedure equals?;
procedure lesser?;
procedure lesserEquals?;
procedure greater?;
procedure greaterEquals?;

procedure plus;
procedure minus;

procedure add;
procedure subtract;
procedure multiply;
procedure divide;
procedure remainder;

procedure addAssign;
procedure subtractAssign;
procedure multiplyAssign;
procedure divideAssign;
procedure remainderAssign;

procedure inc;
procedure dec;

procedure shiftLeft;
procedure shiftRight;
procedure bitwiseAnd;
procedure bitwiseOr;
procedure bitwiseXor;
procedure bitwiseNot;

procedure dereference;
procedure call;
procedure fieldRef;

procedure unpack;
procedure unpackRef;



//
// iteration protocol
//

procedure iterator; // create iterator

procedure hasNext?; // iterator operation
procedure next;     // iterator operation



//
// reverse iteration
//

procedure reverseIterator;



//
// sequence protocol
//

procedure size;
procedure index;

procedure begin; 
procedure end;

procedure front;
procedure back;

procedure push;
procedure pop;
procedure clear;
procedure insert;
procedure remove;
procedure reserve;



//
// hashing
//

procedure hash;



//
// mapping protocol
//

// 'index' and 'size' are available for mappings
// 'remove' is available

procedure lookup; // get pointer to element, null if not available
procedure put;
procedure items; // iterate over all items

contains?(map, key) = not null?(lookup(map, key));



//
// Type
//

[...T]
inlined Type(...x:T) = ...T;



//
// LValue?, RValue?
//

procedure LValue?;
inlined overload LValue?(lvalue a) = true;
inlined overload LValue?(rvalue a) = false;

inlined RValue?(x) = not LValue?(x);



//
// defaults
//

initialize(a) {
    a <-- Type(a)();
}

[T]
copy(a:T) = T(a);

[T]
overload move(a:T) returned:T {
    returned <-- a;
    destroy(a);
    a <-- T();
}

[T]
overload dirtyMove(src:T) returned:T {
    returned <-- move(src);
    destroy(src);
}

[T]
overload assign(dest:T, src:T) {
    if (&dest == &src) return;
    destroy(dest);
    dest <-- src;
}

[T]
overload assign(dest:T, rvalue src:T) {
    destroy(dest);
    dest <-- move(src);
}

notEquals?(a, b) = not equals?(a, b);

overload lesserEquals?(a, b) = not lesser?(b, a);

overload greater?(a, b) = lesser?(b, a);

overload greaterEquals?(a, b) = not lesser?(a, b);

overload addAssign(a, b)       { a = a + b; }
overload subtractAssign(a, b)  { a = a - b; }
overload multiplyAssign(a, b)  { a = a * b; }
overload divideAssign(a, b)    { a = a / b; }
overload remainderAssign(a, b) { a = a % b; }

overload bitwiseAnd(a, b, c, ...rest) = bitwiseAnd(bitwiseAnd(a,b), c, ...rest);
overload bitwiseOr(a, b, c, ...rest) = bitwiseOr(bitwiseOr(a,b), c, ...rest);
overload bitwiseXor(a, b, c, ...rest) = bitwiseXor(bitwiseXor(a,b), c, ...rest);



//
// swap
//

swap(a, b) {
    var temp = move(a);
    a = move(b);
    b = move(temp);
}



//
// min, max
//

min(a, b) {
    if (a <= b)
        return a;
    else
        return b;
}

overload min(lvalue a, lvalue b) {
    if (a <= b)
        return ref a;
    else
        return ref b;
}

max(a, b) {
    if (a > b)
        return a;
    else
        return b;
}

overload max(lvalue a, lvalue b) {
    if (a > b)
        return ref a;
    else
        return ref b;
}



//
// multiValueMap
//

procedure multiValueMap;

overload multiValueMap(f, forward first, forward ...rest) =
    forward ...f(first), ...multiValueMap(f, ...rest);

overload multiValueMap(f) = ;



//
// typeToValue, typesToValues
//

typeToValue(a) = ref null(a)^;
typesToValues(...a) = forward ...multiValueMap(typeToValue, ...a);



//
// ReturnType
//
// return the return type given the callable and arg types.
// return 'BadReturnType' if the call is not defined.
//

record BadReturnType {}

[T]
ReturnType(callable:T, ...argTypes) =
    ReturnType(call, T, ...argTypes);

[F, ...A]
overload ReturnType(static F, ...argTypes:A) =
    BadReturnType;

[F, ...A | CallDefined?(F, ...typesToValues(...A))]
overload ReturnType(static F, ...argTypes:A) =
    ...Type(F(...typesToValues(...argTypes)));



//
// captureValue, forwardValue, capturedRef
//

private record CapturedLValue[T] {
    ptr : Pointer[T];
}

procedure captureValue;
overload captureValue(rvalue x) = move(x);
overload captureValue(lvalue x) = CapturedLValue(&x);

procedure forwardValue;
overload forwardValue(x) = move(x);
[T] overload forwardValue(x:CapturedLValue[T]) = ref x.ptr^;

procedure capturedRef;
overload capturedRef(x) = ref x;
[T] overload capturedRef(x:CapturedLValue[T]) = ref x.ptr^;



//
// captureValues, forwardValues, capturedRefs
//

captureValues(forward ...args) = (...multiValueMap(captureValue, ...args));

[...T]
forwardValues(x:Tuple[...T]) =
    forward ...multiValueMap(forwardValue, ...unpackRef(x));

[...T]
capturedRefs(x:Tuple[...T]) =
    forward ...multiValueMap(capturedRef, ...unpackRef(x));



//
// sequence type predicates
//

Sequence?(T) = CallDefined?(iterator, T);
SequenceElementType(T) = Type(next(iterator(null(T)^)));
SizedSequence?(T) = Sequence?(T) and CallDefined?(size, T);
RandomAccessSequence?(T) = Sequence?(T) and CallDefined?(index, T, Int);
LValueSequence?(T) = Sequence?(T) and LValue?(next(iterator(null(T)^)));

// true if sequence elements are stored contiguously in memory
ContiguousSequence?(T) = false;



//
// empty?
//

[T | SizedSequence?(T)]
empty?(a:T) = (size(a) == 0);



//
// core operations
//

public import __operators__.(
    destroy,
    copy,
    move,
    assign,
    updateAssign,

    equals?,
    notEquals?,
    case?,
    compare?,
    lesser?,
    lesserEquals?,
    greater?,
    greaterEquals?,

    plus,
    minus,

    add,
    subtract,
    multiply,
    divide,
    quotient,
    remainder,
    cat,

    dereference,
    call,
    fieldRef,
    staticIndex,
    index,

    fieldRefAssign,
    fieldRefUpdateAssign,
    staticIndexAssign,
    staticIndexUpdateAssign,
    indexAssign,
    indexUpdateAssign,

    iterator,

    hasNext?,
    next,

    ifExpression,
);

define inc;
define dec;

define bitshl;
define bitshr;
define bitand;
define bitor;
define bitxor;
define bitnot;

define moveUnsafe;
define resetUnsafe;


//
// uninitialized stack allocation
//

[T] uninitializedUnsafe(#T) --> x:T {}


//
// coordinate type predicates
//

Coordinate?(T) = CallDefined?(dereference, T);
CoordinateTargetType(T) = ..Type(..dereference(typeToLValue(T)));

RandomAccessCoordinate?(T) =
    Coordinate?(T) and
    CallDefined?(add, T, Int) and
    CallDefined?(subtract, T, Int) and
    CallDefined?(subtract, T, T);

LValueCoordinate?(T) =
    Coordinate?(T) and
    LValue?(dereference(typeToLValue(T)));

// true if adjacent coordinates point to adjacent locations in memory
ContiguousCoordinate?(T) = false;



//
// iterator type predicates
//

Iterator?(T) = CallDefined?(hasNext?, T) and CallDefined?(next, T);
IteratorTargetType(T) = ..Type(..next(typeToLValue(T)));




//
// reverse iteration
//

define reverseIterator;



//
// sequence protocol
//

define size;

define begin;
define end;

define front;
define back;

define push;
define pop;
define clear;
define insert;
define remove;
define reserve;
define resize;
define resizeUnsafe;

define pushFront;
define popFront;

// defined for statically sized sequences like Array[T,n]
define StaticSize;



//
// sequence type predicates
//

Sequence?(T) = CallDefined?(iterator, T);
SequenceElementType(T) = ..Type(..next(iterator(typeToLValue(T))));
SizedSequence?(T) = Sequence?(T) and CallDefined?(size, T);
StaticSizedSequence?(T) = Sequence?(T) and CallDefined?(StaticSize, Static[T]);
RandomAccessSequence?(T) = Sequence?(T) and CallDefined?(index, T, Int);
LValueSequence?(T) = Sequence?(T) and LValue?(next(iterator(typeToLValue(T))));

CoordinateSequence?(T) = Sequence?(T) and CallDefined?(begin, T) and CallDefined?(end, T);
SequenceCoordinateType(T) = Type(begin(typeToLValue(T)));

SequenceIteratorType(T) = Type(iterator(typeToLValue(T)));

// true if sequence elements are stored contiguously in memory
ContiguousSequence?(T) = false;

// true if copying the sequence is efficient
LightweightSequence?(T) = false;

// true for sequences which have value semantics.
SequenceContainer?(T) = false;

// true if sequence is not single-valued (0 valued sequences are included).
MultiValuedSequence?(T) =
    Sequence?(T) and multiValued?(..Type(..next(iterator(typeToLValue(T)))));

private multiValued?(..x) = true;
overload multiValued?(x) = false;



//
// hashing
//

define hash;



//
// mapping protocol
//

// 'index' and 'size' are available for mappings
// 'remove' is available

define lookup; // get pointer to element, null if not available
define put;
define items; // iterate over all items

inline contains?(map, key) = not null?(lookup(map, key));



//
// LValue?, RValue?
//

define LValue?;
alias overload LValue?(ref a) = true;
alias overload LValue?(rvalue a) = false;

alias RValue?(x) = not LValue?(x);



//
// defaults
//

inline initialize(a) {
    a <-- Type(a)();
}

[T]
inline overload copy(a:T) = T(a);

[T]
inline overload move(src:T) --> returned:T {
    returned <-- moveUnsafe(src);
    resetUnsafe(src);
}

[T]
inline overload assign(ref dest:T, rvalue src:T) {
    swap(dest, src);
}

[F]
inline overload updateAssign(#F, ref dest, forward src) {
    dest = F(dest, src);
}

inline overload notEquals?(a, b) = not equals?(a, b);

inline ordered?(a, b) = (a <= b) or (b <= a);

inline overload lesserEquals?(a, b) = not lesser?(b, a);

inline overload greater?(a, b) = lesser?(b, a);

inline overload greaterEquals?(a, b) = not lesser?(a, b);

inline overload compare?(a, fn, b, ..args) : Bool
    = fn(a, b) and compare?(b, ..args);
inline overload compare?(a, fn, b) : Bool = fn(a, b);

inline overload bitand(a, b, c, ..rest) =
    bitand(bitand(a,b), c, ..rest);

inline overload bitor(a, b, c, ..rest) =
    bitor(bitor(a,b), c, ..rest);

inline overload bitxor(a, b, c, ..rest) =
    bitxor(bitxor(a,b), c, ..rest);

[FIELD]
inline overload fieldRefAssign(dest, #FIELD, forward src) {
    assign(fieldRef(dest, #FIELD), src);
}

[F, FIELD]
inline overload fieldRefUpdateAssign(#F, dest, #FIELD, forward src) {
    updateAssign(#F, fieldRef(dest, #FIELD), src);
}

[F, FIELD, T | RValue?(fieldRef(typeToLValue(T), #FIELD))]
inline overload fieldRefUpdateAssign(#F, dest:T, #FIELD, forward src) {
    fieldRefAssign(dest, #FIELD, F(fieldRef(dest, #FIELD), src));
}

[i]
inline overload staticIndexAssign(dest, #i, forward src) {
    assign(staticIndex(dest, #i), src);
}

[F, i]
inline overload staticIndexUpdateAssign(#F, dest, #i, forward src) {
    updateAssign(#F, staticIndex(dest, #i), src);
}

[F, i, T | RValue?(staticIndex(typeToLValue(T), #i))]
inline overload staticIndexUpdateAssign(#F, dest:T, #i, forward src) {
    staticIndexAssign(dest, #i, F(staticIndex(dest, #i), src));
}

[..T | countValues(..T) >= 2]
inline overload indexAssign(dest, forward ..indicesAndSrc:T) {
    assign(
        index(dest, ..withoutNthValue(
            #countValues(..T)-1,
            ..indicesAndSrc
        )),
        lastValue(..indicesAndSrc),
    );
}

[F, ..A | countValues(..A) >= 2]
inline overload indexUpdateAssign(#F, dest, forward ..indicesAndSrc:A) {
    updateAssign(
        #F,
        index(dest, ..withoutNthValue(
            #countValues(..A)-1,
            ..indicesAndSrc
        )),
        lastValue(..indicesAndSrc),
    );
}

[F, T, ..A |
    (countValues(..A) >= 2) and
    RValue?(
        index(
            typeToLValue(T),
            ..mapValues(
                typeToLValue,
                ..withoutNthValue(#countValues(..A)-1, ..A)
            )
        )
    )
]
inline overload indexUpdateAssign(#F, dest:T, forward ..indicesAndSrc:A) {
    indexAssign(
        dest,
        ..withoutNthValue(#countValues(..A)-1, ..indicesAndSrc),
        F(
            index(
                dest,
                ..withoutNthValue(#countValues(..A)-1, ..indicesAndSrc)
            ),
            lastValue(..indicesAndSrc)
        )
    );
}




//
// swap
//

inline swap(a, b) {
    var temp = moveUnsafe(a);
    a <-- moveUnsafe(b);
    b <-- move(temp);
}

[T | BitwiseMovedType?(T)]
inline overload swap(a:T, b:T) {
    alias TBytes = Array[UInt8, Int(TypeSize(T))];
    var temp = bitcast(TBytes, a);
    bitcast(TBytes, a) <-- bitcast(TBytes, b);
    bitcast(TBytes, b) <-- temp;
}


//
// min, max
//

inline min(a, b) {
    if (a <= b)
        return a;
    else
        return b;
}

inline overload min(ref a, ref b) {
    if (a <= b)
        return ref a;
    else
        return ref b;
}

inline max(a, b) {
    if (a > b)
        return a;
    else
        return b;
}

inline overload max(ref a, ref b) {
    if (a > b)
        return ref a;
    else
        return ref b;
}



//
// empty?
//

[T | SizedSequence?(T)]
inline empty?(a:T) = (size(a) == 0);



//
// ifExpression
//

[T]
alias overload ifExpression(condition, consequent:T, alternate:T) : T {
    if (condition)
        return consequent;
    else
        return alternate;
}

[T]
alias overload ifExpression(condition, ref consequent:T, ref alternate:T) : ByRef[T] {
    if (condition)
        return ref consequent;
    else
        return ref alternate;
}



//
// bitandc, bitblend
//

inline bitandc(a, b) = bitand(a, bitnot(b));
inline bitblend(mask, ones, zeros)
    = bitor(bitand(ones, mask), bitandc(zeros, mask));



//
// bitrol, bitror
//

[I, J | Integer?(I) and Integer?(J)]
bitrol(x:I, n:J) = bitor(wrapBitshl(x, n), bitshr(x, TypeSize(I)*8 - n));

[I, J | Integer?(I) and Integer?(J)]
bitror(x:I, n:J) = bitor(bitshr(x, n), wrapBitshl(x, TypeSize(I)*8 - n));



//
// PrimitiveType?
//

PrimitiveType?(X) = false;
overload PrimitiveType?(#Bool) = true;
overload PrimitiveType?(#Int8) = true;
overload PrimitiveType?(#Int16) = true;
overload PrimitiveType?(#Int32) = true;
overload PrimitiveType?(#Int64) = true;
overload PrimitiveType?(#Int128) = true;
overload PrimitiveType?(#UInt8) = true;
overload PrimitiveType?(#UInt16) = true;
overload PrimitiveType?(#UInt32) = true;
overload PrimitiveType?(#UInt64) = true;
overload PrimitiveType?(#UInt128) = true;
overload PrimitiveType?(#Float32) = true;
overload PrimitiveType?(#Float64) = true;
overload PrimitiveType?(#Float80) = true;
overload PrimitiveType?(#Imag32) = true;
overload PrimitiveType?(#Imag64) = true;
overload PrimitiveType?(#Imag80) = true;
overload PrimitiveType?(#Complex32) = true;
overload PrimitiveType?(#Complex64) = true;
overload PrimitiveType?(#Complex80) = true;
[T] overload PrimitiveType?(#Pointer[T]) = true;
[I, O] overload PrimitiveType?(#CodePointer[I, O]) = true;
[I, O] overload PrimitiveType?(#CCodePointer[I, O]) = true;
[I, O] overload PrimitiveType?(#VarArgsCCodePointer[I, O]) = true;
[I, O] overload PrimitiveType?(#StdCallCodePointer[I, O]) = true;
[I, O] overload PrimitiveType?(#FastCallCodePointer[I, O]) = true;
[T] overload PrimitiveType?(#Static[T]) = true;
[T,n] overload PrimitiveType?(#Vec[T,n]) = true;
[E | Enum?(E)] overload PrimitiveType?(#E) = true;
[..T] overload PrimitiveType?(#Union[..T]) = true;
[] overload PrimitiveType?(#Union[]) = true;
overload PrimitiveType?(#Tuple[]) = true;



//
// PrimitiveCompoundType?, PrimitiveCompoundMemberTypes
//

PrimitiveCompoundType?(X) = false;

[T,n] overload PrimitiveCompoundType?(#Array[T,n]) = true;
[..T] overload PrimitiveCompoundType?(#Tuple[..T]) = true;
[R | RegularRecord?(R)] overload PrimitiveCompoundType?(#R) = true;
[V | Variant?(V)] overload PrimitiveCompoundType?(#V) = true;

define PrimitiveCompoundMemberTypes;
[T,n] overload PrimitiveCompoundMemberTypes(#Array[T,n]) = T;
[..T] overload PrimitiveCompoundMemberTypes(#Tuple[..T]) = ..T;
[R | RegularRecord?(R)]
overload PrimitiveCompoundMemberTypes(#R) = ..RecordFieldTypes(R);
[V | Variant?(V)]
overload PrimitiveCompoundMemberTypes(#V) = ..VariantMembers(V);


//
// PODType?
//

PODType?(X) = false;

[T | PrimitiveType?(T)]
overload PODType?(#T) = true;

[T | PrimitiveCompoundType?(T)]
overload PODType?(#T) = allValues?(PODType?, ..PrimitiveCompoundMemberTypes(T));



//
// Finer-grained POD-related properties
//

BitwiseCopiedType?(T) = PODType?(T);
BitwiseMovedType?(T) = PODType?(T);
NotDestroyedType?(T) = PODType?(T);
NotResetType?(T) = PODType?(T);
CopyDoesNotThrowType?(T) = PODType?(T);
AssignDoesNotThrowType?(T) = PODType?(T);
BitwiseAssignedType?(T) = BitwiseCopiedType?(T) and NotDestroyedType?(T);
BitwiseMoveAssignedType?(T) = BitwiseMovedType?(T) and NotDestroyedType?(T);

[T | PrimitiveCompoundType?(T)]
overload BitwiseCopiedType?(#T)
    = allValues?(BitwiseCopiedType?, ..PrimitiveCompoundMemberTypes(T));
[T | PrimitiveCompoundType?(T)]
overload BitwiseMovedType?(#T)
    = allValues?(BitwiseMovedType?, ..PrimitiveCompoundMemberTypes(T));
[T | PrimitiveCompoundType?(T)]
overload NotDestroyedType?(#T)
    = allValues?(NotDestroyedType?, ..PrimitiveCompoundMemberTypes(T));
[T | PrimitiveCompoundType?(T)]
overload NotResetType?(#T)
    = allValues?(NotResetType?, ..PrimitiveCompoundMemberTypes(T));
[T | PrimitiveCompoundType?(T)]
overload CopyDoesNotThrowType?(#T)
    = allValues?(CopyDoesNotThrowType?, ..PrimitiveCompoundMemberTypes(T));
[T | PrimitiveCompoundType?(T)]
overload AssignDoesNotThrowType?(#T)
    = allValues?(AssignDoesNotThrowType?, ..PrimitiveCompoundMemberTypes(T));
[T | PrimitiveCompoundType?(T)]
overload BitwiseAssignedType?(#T)
    = allValues?(BitwiseAssignedType?, ..PrimitiveCompoundMemberTypes(T));
[T | PrimitiveCompoundType?(T)]
overload BitwiseMoveAssignedType?(#T)
    = allValues?(BitwiseMoveAssignedType?, ..PrimitiveCompoundMemberTypes(T));



//
// identity test
//

inline is?(x, y) = &x == &y;
inline isNot?(x, y) = not is?(x, y);


//
// case?
//

inline overload case?(x, forward ..ys) : Bool {
    ..for (y in ..ys)
        if (equals?(x, y))
            return true;
    return false;
}


//
// CallDefined?, CallOutputTypes
//

define CallDefined?;
[F]
alias overload CallDefined?(f:F, ..In) = StaticCallDefined?(call, F, ..In);
[f]
alias overload CallDefined?(#f, ..In) = StaticCallDefined?(f, ..In);

define CallOutputTypes;
[F]
alias overload CallOutputTypes(f:F, ..In) = ..StaticCallOutputTypes(call, F, ..In);
[f]
alias overload CallOutputTypes(#f, ..In) = ..StaticCallOutputTypes(f, ..In);


//
// Mono?, MonoInputTypes, MonoOutputTypes
//

define MonoInputTypes;
[f | StaticMono?(f)]
alias overload MonoInputTypes(#f) = ..StaticMonoInputTypes(f);
[F | LambdaRecord?(F) and LambdaMono?(F)]
alias overload MonoInputTypes(f:F) = ..LambdaMonoInputTypes(F);

[F]
alias Mono?(f:F) = CallDefined?(MonoInputTypes, F);
inline MonoType?(F) = Mono?(typeToRValue(F));

[F | MonoType?(F)]
alias MonoOutputTypes(f:F) = ..CallOutputTypes(f, ..MonoInputTypes(f));

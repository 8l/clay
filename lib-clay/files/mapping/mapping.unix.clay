import unix;


//
// FileMapping
//

private record InnerFileMapping {
    address : Pointer[Byte];
    length : SizeT;
}

record FileMapping {
    inner : SharedPointer[InnerFileMapping];
}



//
// constructors, destroy
//

enum MappingMode {
    MAP_READ,
    MAP_READ_WRITE,
    MAP_COPY_ON_WRITE
}

private modeProtection(mode) {
    if (mode == MAP_READ)
        return unix.PROT_READ;
    if (mode == MAP_READ_WRITE)
        return bitwiseOr(unix.PROT_READ, unix.PROT_WRITE);
    if (mode == MAP_COPY_ON_WRITE)
        return bitwiseOr(unix.PROT_READ, unix.PROT_WRITE);
    assert(false);
}

private modeFlags(mode) {
    if (mode == MAP_READ)
        return unix.MAP_SHARED;
    if (mode == MAP_READ_WRITE)
        return unix.MAP_SHARED;
    if (mode == MAP_COPY_ON_WRITE)
        return unix.MAP_PRIVATE;
    assert(false);
}

[A,B | Integer?(A,B)]
overload FileMapping(f:File, mode:MappingMode, offset:A, length:B) {
    var protection = modeProtection(mode);
    var flags = modeFlags(mode);
    var address = unix.syscall(unix.mmap,
                               RawPointer(0),
                               SizeT(length),
                               Int(protection),
                               Int(flags),
                               fileHandle(f),
                               Int64(offset));
    return FileMapping(new InnerFileMapping(address, SizeT(length)));
}

overload FileMapping(f:File, mode:MappingMode) =
    FileMapping(f, mode, 0, fileSize(f));

overload destroy(x:InnerFileMapping) {
    unix.syscallNoThrow(unix.munmap, x.address, x.length);
}


// disable default constructor, copy constructor, assignment
overload InnerFileMapping();
overload InnerFileMapping(x:InnerFileMapping);
overload assign(dest:InnerFileMapping, src:InnerFileMapping);



//
// mappingAddress, mappingLength
//

mappingAddress(fm:FileMapping) = fm.inner^.address;
mappingLength(fm:FileMapping) = fm.inner^.length;



//
// flushMapping
//

flushMapping(fm:FileMapping) {
    unix.syscall(unix.msync, fm.inner^.address,
                 fm.inner^.length, unix.MS_SYNC);
}


//
// fieldRef, fieldRefByIndex
//

[T, FIELD when RecordWithField?(T, FIELD)]
inline overload fieldRef(x:T, static FIELD) {
    return ref recordFieldRefByName(x, static FIELD);
}

[T, i when Record?(T)]
inline fieldRefByIndex(x:T, static i) =
    ref recordFieldRef(x, static SizeT(i));


//
// RecordFieldType, RecordFieldTypes
//

[T,i when Record?(T)]
RecordFieldType(static T, static i) = Type(fieldRefByIndex(null(T)^, static i));

[T when Record?(T)]
RecordFieldTypes(static T) = ..Type(..recordFields(null(T)^));



//
// RecordFields
//

RecordFields(R) {
    alias n = RecordFieldCount(R);
    var f = i => [RecordFieldName(R,i), RecordFieldType(R,i)];
    return ..mapValues(f, ..staticIntegers(static n));
}

RecordFieldNames(R) = ..mapValues(
    i => RecordFieldName(R, i),
    ..staticIntegers(static RecordFieldCount(R))
);



//
// RegularRecord?
//
// true if record's value semantics is defined in terms of its fields
//

[T]
RegularRecord?(static T) = false;

[T when Record?(T)]
overload RegularRecord?(static T) = true;



//
// basic constructor
//

[T, ..A when Record?(T) and (Tuple[..RecordFieldTypes(T)] == Tuple[..A])]
alias initializeRecord(static T, ..args:A) --> returned:T {
    ..recordFields(returned) <-- ..args;
}

[T, ..A when Record?(T) and (Tuple[..RecordFieldTypes(T)] == Tuple[..A])]
alias overload T(..args:A) = initializeRecord(T, ..args);



//
// default constructor
//

[T when Record?(T)]
overload initializeRecord(static T) --> returned:T {
    initializeMulti(..recordFields(returned));
}

[T when RegularRecord?(T)]
overload T() = initializeRecord(T);

[..A]
private initializeMulti(..args:A) {
    ..args <-- ..initializeTypes(..A);
}

private define initializeTypes;
inline overload initializeTypes(A, ..B) = A(), ..initializeTypes(..B);
inline overload initializeTypes() = ;



//
// copy constructor
//

[T when Record?(T)]
copyRecord(src:T) --> returned:T {
    ..recordFields(returned) <-- ..recordFields(src);
}

[T when RegularRecord?(T) and not BitwiseCopiedType?(T)]
overload T(src:T) = copyRecord(src);



//
// moveUnsafe, resetUnsafe
//

[T when Record?(T)]
moveRecordUnsafe(src:T) --> returned:T {
    alias n = RecordFieldCount(T);
    ..for (i in staticIntegers(static n))
        fieldRefByIndex(returned, i) <-- moveUnsafe(fieldRefByIndex(src, i));
}

[T when RegularRecord?(T) and not BitwiseMovedType?(T)]
overload moveUnsafe(src:T) = moveRecordUnsave(src);

[T when Record?(T)]
resetRecordUnsafe(a:T) {
    ..for (x in recordFields(a))
        resetUnsafe(x);
}

[T when RegularRecord?(T) and not NotResetType?(T)]
overload resetUnsafe(a:T) {
    resetRecordUnsafe(a);
}



//
// destroy
//

[T when Record?(T)]
destroyRecord(a:T) {
    ..for (x in recordFields(a))
        destroy(x);
}

[T when RegularRecord?(T) and not NotDestroyedType?(T)]
overload destroy(a:T) {
    destroyRecord(a);
}



//
// assign
//

[T when RegularRecord?(T) and not BitwiseAssignedType?(T)]
overload assign(ref dest:T, ref src:T) {
    alias n = RecordFieldCount(T);
    ..for (i in staticIntegers(static n))
        assign(fieldRefByIndex(dest, i), fieldRefByIndex(src, i));
}



//
// equals?
//

[T when RegularRecord?(T)]
overload equals?(a:T, b:T) {
    alias n = RecordFieldCount(T);
    ..for (i in staticIntegers(static n)) {
        if (not equals?(fieldRefByIndex(a, i), fieldRefByIndex(b, i)))
            return false;
    }
    return true;
}



//
// lesser?
//

[T when RegularRecord?(T)]
overload lesser?(a:T, b:T) {
    alias n = RecordFieldCount(T);
    ..for (i in staticIntegers(static n)) {
        ref av = fieldRefByIndex(a, i);
        ref bv = fieldRefByIndex(b, i);
        if (lesser?(av, bv))
            return true;
        if (lesser?(bv, av))
            return false;
    }
    return false;
}



//
// recordWithProperties
//
// RecordWithProperties is a special primitive type known to the compiler.
// can be used in computed records to set custom properties.
//

recordWithProperties(properties, ..fields) {
    var fieldsTuple = Tuple(..fields);

    // ensure they're all static
    static properties;
    static fieldsTuple;

    return RecordWithProperties[Type(properties), Type(fieldsTuple)]();
}

recordWithProperty(property, ..fields) =
    recordWithProperties(Tuple(property), ..fields);

recordWithPredicate(predicate, ..fields) =
    recordWithProperty([predicate, static true], ..fields);

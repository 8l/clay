

//
// RecordFieldType
//

[T,i | Record?(T)]
RecordFieldType(static T, static i) = Type(recordFieldRef(T(), static SizeT(i)));



//
// SafeRecord? - true if record doesn't contains unsafe types such as pointers
//

overloadable SafeRecord?;

[T]
overload SafeRecord?(static T) = false;

[T | Record?(T)]
overload SafeRecord?(static T) = SafeFields?(T, static 0);

private overloadable SafeFields?;

[T,i] overload SafeFields?(static T, static i) =
    SafeType?(RecordFieldType(T, static i)) and SafeFields?(T, static i+1);

[T,i | i == RecordFieldCount(T)]
overload SafeFields?(static T, static i) = true;



//
// SafeType?
//

overloadable SafeType?;

[T] overload SafeType?(static T) = true;
[T] overload SafeType?(static Pointer[T]) = false;



//
// default constructor
//

[T | Record?(T)]
overload T() returned:T {
    initRecordFields(returned, static SizeT(0));
}

private overloadable initRecordFields;

[i]
overload initRecordFields(a, static i) {
    initialize(recordFieldRef(a, static i));
    initRecordFields(a, static i+1);
}

[T,i | (i == RecordFieldCount(T))]
overload initRecordFields(a:T, static i) {
}



//
// copy constructor
//

[T | Record?(T)]
overload T(src:T) returned:T {
    copyRecordFields(returned, src, static SizeT(0));
}

private overloadable copyRecordFields;

[T,i]
overload copyRecordFields(dest:T, src:T, static i) {
    recordFieldRef(dest, static i) <-- recordFieldRef(src, static i);
    copyRecordFields(dest, src, static i+1);
}

[T,i | (i == RecordFieldCount(T))]
overload copyRecordFields(dest:T, src:T, static i) {
}



//
// move constructor
//

[T | SafeRecord?(T)]
overload move(src:T) returned:T {
    moveRecordFields(returned, src, static SizeT(0));
}

private overloadable moveRecordFields;

[T,i]
overload moveRecordFields(dest:T, src:T, static i) {
    recordFieldRef(dest, static i) <-- move(recordFieldRef(src, static i));
    moveRecordFields(dest, src, static i+1);
}

[T,i | (i == RecordFieldCount(T))]
overload moveRecordFields(dest:T, src:T, static i) {
}



//
// destroy
//

[T | Record?(T)]
overload destroy(a:T) {
    destroyRecordFields(a, static SizeT(0));
}

private overloadable destroyRecordFields;

[i]
overload destroyRecordFields(a, static i) {
    destroyRecordFields(a, static i+1);
    destroy(recordFieldRef(a, static i));
}

[T,i | (i == RecordFieldCount(T))]
overload destroyRecordFields (a:T, static i) {
}



//
// assign
//

[T | SafeRecord?(T)]
overload assign(dest:T, src:T) {
    assignRecordFields(dest, src, static SizeT(0));
}

private overloadable assignRecordFields;

[T,i]
overload assignRecordFields(dest:T, src:T, static i) {
    assign(recordFieldRef(dest, static i), recordFieldRef(src, static i));
    assignRecordFields(dest, src, static i+1);
}

[T,i | (i == RecordFieldCount(T))]
overload assignRecordFields(dest:T, src:T, static i) {
}



//
// equals?
//

[T|Record?(T)]
overload equals?(a:T, b:T) {
    return equalRecordFields?(a, b, static SizeT(0));
}

private overloadable equalRecordFields?;

[T,i]
overload equalRecordFields?(a:T, b:T, static i) {
    if (not equals?(recordFieldRef(a, static i), recordFieldRef(b, static i)))
        return false;
    return equalRecordFields?(a, b, static i+1);
}

[T,i |(i == RecordFieldCount(T))]
overload equalRecordFields?(a:T, b:T, static i) {
    return true;
}



//
// lesser?
//

[T|Record?(T)]
overload lesser?(a:T, b:T) {
    return lesserRecordFields?(a, b, static SizeT(0));
}

private overloadable lesserRecordFields?;

[T,i]
overload lesserRecordFields?(a:T, b:T, static i) {
    ref af = recordFieldRef(a, static i);
    ref bf = recordFieldRef(b, static i);
    if (lesser?(af, bf))
        return true;
    if (lesser?(bf, af))
        return false;
    return lesserRecordFields?(a, b, static i+1);
}

[T,i |(i == RecordFieldCount(T))]
overload lesserRecordFields?(a:T, b:T, static i) {
    return false;
}




//
// fieldRef, staticIndex
//

[T, FIELD | RecordWithField?(T, FIELD)]
overload fieldRef(x:T, static FIELD) {
    return ref recordFieldRefByName(x, static FIELD);
}

[T, i | Record?(T)]
overload staticIndex(x:T, static i) =
    ref recordFieldRef(x, static SizeT(i));



//
// RecordFieldType, RecordFieldTypes
//

[T,i | Record?(T)]
RecordFieldType(static T, static i) = Type(staticIndex(null(T)^, static i));

[T | Record?(T)]
RecordFieldTypes(static T) = ...Type(...recordFields(null(T)^));



//
// RegularRecord?
//
// true if record's value semantics is defined in terms of it's fields
//

[T]
RegularRecord?(static T) = false;

[T | Record?(T)]
overload RegularRecord?(static T) = true;



//
// basic constructor
//

[T, ...A | Record?(T) and (Tuple[...RecordFieldTypes(T)] == Tuple[...A])]
macro overload T(...args:A) returned:T {
    ...recordFields(returned) <-- ...args;
}



//
// default constructor
//

[T | RegularRecord?(T)]
overload T() returned:T {
    initializeMulti(...recordFields(returned));
}

[...A]
private initializeMulti(...args:A) {
    ...args <-- ...initializeTypes(...A);
}

private procedure initializeTypes;
overload initializeTypes(A, ...B) = A(), ...initializeTypes(...B);
overload initializeTypes() = ;



//
// copy constructor
//

[T | RegularRecord?(T)]
overload T(src:T) returned:T {
    ...recordFields(returned) <-- ...recordFields(src);
}



//
// moveUnsafe, resetUnsafe
//

[T | RegularRecord?(T)]
overload moveUnsafe(src:T) returned:T {
    alias n = RecordFieldCount(T);
    static for (i in ...staticIntegers(static n))
        staticIndex(returned, i) <-- moveUnsafe(staticIndex(src, i));
}

[T | RegularRecord?(T)]
overload resetUnsafe(a:T) {
    static for (x in ...recordFields(a))
        resetUnsafe(x);
}



//
// destroy
//

[T | RegularRecord?(T)]
overload destroy(a:T) {
    static for (x in ...recordFields(a))
        destroy(x);
}



//
// assign
//

[T | RegularRecord?(T)]
overload assign(dest:T, lvalue src:T) {
    alias n = RecordFieldCount(T);
    static for (i in ...staticIntegers(static n))
        assign(staticIndex(dest, i), staticIndex(src, i));
}



//
// equals?
//

[T | RegularRecord?(T)]
overload equals?(a:T, b:T) {
    alias n = RecordFieldCount(T);
    static for (i in ...staticIntegers(static n)) {
        if (not equals?(staticIndex(a, i), staticIndex(b, i)))
            return false;
    }
    return true;
}



//
// lesser?
//

[T | RegularRecord?(T)]
overload lesser?(a:T, b:T) {
    alias n = RecordFieldCount(T);
    static for (i in ...staticIntegers(static n)) {
        ref av = staticIndex(a, i);
        ref bv = staticIndex(b, i);
        if (lesser?(av, bv))
            return true;
        if (lesser?(bv, av))
            return false;
    }
    return false;
}

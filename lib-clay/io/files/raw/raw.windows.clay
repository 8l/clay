import win32;


//
// RawFile
//

record RawFile {
    handle : win32.HANDLE;
    owned : Bool;
}

overload RegularRecord?(static RawFile) = false;

private stdHandle(x) = win32.GetStdHandle(win32.DWORD(x));

stdinRawFile()  = RawFile(stdHandle(win32.STD_INPUT_HANDLE), false);
stdoutRawFile() = RawFile(stdHandle(win32.STD_OUTPUT_HANDLE), false);
stderrRawFile() = RawFile(stdHandle(win32.STD_ERROR_HANDLE), false);



//
// constructors, destroy
//

private accessFlags(mode) {
    if (mode == READ)
        return win32.GENERIC_READ;
    if (mode == READ_WRITE)
        return bitwiseOr(win32.GENERIC_READ, win32.GENERIC_WRITE);
    if (mode == CREATE)
        return bitwiseOr(win32.GENERIC_READ, win32.GENERIC_WRITE);
    if (mode == APPEND)
        return win32.GENERIC_WRITE;
    assert(false);
}

private sharingFlags(mode) {
    if (mode == READ)
        return win32.FILE_SHARE_READ;
    if (mode == READ_WRITE)
        return 0u32;
    if (mode == CREATE)
        return 0u32;
    if (mode == APPEND)
        return 0u32;
    assert(false);
}

private creationFlags(mode) {
    if (mode == READ)
        return win32.OPEN_EXISTING;
    if (mode == READ_WRITE)
        return win32.OPEN_EXISTING;
    if (mode == CREATE)
        return win32.CREATE_ALWAYS;
    if (mode == APPEND)
        return win32.OPEN_ALWAYS;
    assert(false);
}

[S | CCompatibleString?(S)]
overload RawFile(path:S, mode:OpenMode) {
    var handle = win32.CreateFileA(
        cstring(path),
        win32.DWORD(accessFlags(mode)),
        win32.DWORD(sharingFlags(mode)),
        win32.LPSECURITY_ATTRIBUTES(0),
        win32.DWORD(creationFlags(mode)),
        win32.DWORD(win32.FILE_ATTRIBUTE_NORMAL),
        win32.HANDLE(0));
    if (handle == win32.INVALID_HANDLE_VALUE)
        error(win32.errorMessage(), ": ", path);
    var f = RawFile(handle, true);
    if (mode == APPEND)
        seek(f, 0, SEEK_END);
    return move(f);
}

[S | CCompatibleString?(S)]
overload RawFile(path:S) = RawFile(path, READ);

overload destroy(x:RawFile) {
    if (x.owned) {
        var result = win32.CloseHandle(x.handle);
        if (result == 0)
            errorNoThrow(win32.errorMessage());
    }
}

// disable default constructor, copy constructor, assignment
overload RawFile();
overload RawFile(x:RawFile);
overload assign(dest:RawFile, src:RawFile);


// move constructor

overload move(src:RawFile) returned:RawFile {
    returned <-- RawFile(src.handle, src.owned);
    src <-- RawFile(win32.HANDLE(0), false);
}




//
// fileHandle
//

overload fileHandle(f:RawFile) = f.handle;



//
// read, write, flush
//

[I | Integer?(I)]
overload read(f:RawFile, buffer:Pointer[Byte], n:I) SizeT {
    var nRead = win32.DWORD();
    var result = win32.ReadFile(
        f.handle,
        win32.LPVOID(buffer),
        win32.DWORD(n),
        &nRead,
        win32.LPOVERLAPPED(0));
    if (result == 0)
        error(win32.errorMessage());
    return SizeT(nRead);
}

[I | Integer?(I)]
overload write(f:RawFile, buffer:Pointer[Byte], n:I) {
    var nWritten = win32.DWORD();
    var result = win32.WriteFile(
        f.handle,
        win32.LPVOID(buffer),
        win32.DWORD(n),
        &nWritten,
        win32.LPOVERLAPPED(0));
    if (result == 0)
        error(win32.errorMessage());
    assert(nWritten == n);
}

overload flush(f:RawFile) {
    var result = win32.FlushFileBuffers(f.handle);
    if (result == 0)
        error(win32.errorMessage());
}



//
// fileSize, seek
//

overload fileSize(f:RawFile) {
    var size = Int64();
    var result = win32.GetFileSizeEx(f.handle, &size);
    if (result == 0)
        error(win32.errorMessage());
    return size;
}

private seekMethod(x) {
    if (x == SEEK_SET) return win32.FILE_BEGIN;
    if (x == SEEK_CUR) return win32.FILE_CURRENT;
    if (x == SEEK_END) return win32.FILE_END;
    assert(false);
}

overload seek(f:RawFile, offset, whence) {
    var newPos = Int64();
    var result = win32.SetFilePointerEx(
        f.handle,
        Int64(offset),
        &newPos,
        seekMethod(whence));
    if (result == 0)
        error(win32.errorMessage());
    return newPos;
}



//
// terminal?
//

overload terminal?(f:RawFile) = true; // FIXME: implement it

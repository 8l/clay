
import simd.primitives as prims;


//
// type inferring constructor
//

[...A | (countValues(...A) > 0) and equalValues?(...A)]
overload Vec(...args:A) {
    alias n = static countValues(...A);
    alias T = firstValue(...A);
    return Vec[T, n](...args);
}



//
// basic constructor
//

[T,n,...A | Tuple[...replicateValue(T,static n)] == Tuple[...A]]
overload Vec[T,n](...args:A) {
    return insertElements(Vec[T,n](), static 0, ...args);
}

private procedure insertElements;

[T,n,i]
overload insertElements(a:Vec[T,n], static i, first, ...rest) {
    var b = prims.insertElement(a, i, first);
    return insertElements(b, static i+1, ...rest);
}

[T,n]
overload insertElements(a:Vec[T,n], static n) {
    return a;
}



//
// single element replicating constructor
//

[T,n]
overload Vec[T,n](x:T) {
    return replicateElement(Vec[T,n](), static 0, x);
}

[T,n,i]
private replicateElement(a:Vec[T,n], static i, x:T) {
    var b = prims.insertElement(a, i, x);
    return replicateElement(b, static i+1, x);
}

[T,n]
overload replicateElement(a:Vec[T,n], static n, x:T) {
    return a;
}



//
// other constructors, moveUnsafe, resetUnsafe, assign, destroy
//

[T,n]
overload Vec[T,n]() returned:Vec[T,n] {
}

[T,n]
overload Vec[T,n](src:Vec[T,n]) returned:Vec[T,n] __llvm__{
    %1 = load $Vec[T,n] * %src
    store $Vec[T,n] %1, $Vec[T,n] * %returned
    ret i32 0
}

[T,n]
overload moveUnsafe(src:Vec[T,n]) returned:Vec[T,n] {
    returned <-- src;
}

[T,n]
overload resetUnsafe(src:Vec[T,n]) {
}

[T,n]
overload assign(dest:Vec[T,n], src:Vec[T,n]) {
    dest <-- src;
}

[T,n]
overload destroy(a:Vec[T,n]) {
}



//
// unpackVec
//

[T,n]
unpackVec(a:Vec[T,n]) = ...unpackElements(a, static 0);

private procedure unpackElements;

[T,n,i]
overload unpackElements(a:Vec[T,n], static i) =
    prims.extractElement(a, i), ...unpackElements(a, static i+1);

[T,n]
overload unpackElements(a:Vec[T,n], static n) = ;



//
// get, set individual elements
//

[T,n,I | Integer?(I)]
get(a:Vec[T,n], i:I) = prims.extractElement(a, Int(i));

[T,n,I | Integer?(I)]
set(a:Vec[T,n], i:I, x:T) = prims.insertElement(a, Int(i), x);



//
// numeric ops
//

[T,n]
overload add(a:Vec[T,n], b:Vec[T,n]) = prims.add(a, b);

[T,n,X | Numeric?(X)]
overload add(a:Vec[T,n], x:X) = a + Vec[T,n](T(x));

[T,n,X | Numeric?(X)]
overload add(x:X, a:Vec[T,n]) = Vec[T,n](T(x)) + a;


[T,n]
overload subtract(a:Vec[T,n], b:Vec[T,n]) = prims.subtract(a, b);

[T,n,X | Numeric?(X)]
overload subtract(a:Vec[T,n], x:X) = a - Vec[T,n](T(x));

[T,n,X | Numeric?(X)]
overload subtract(x:X, a:Vec[T,n]) = Vec[T,n](T(x)) - a;


[T,n]
overload multiply(a:Vec[T,n], b:Vec[T,n]) = prims.multiply(a, b);

[T,n,X | Numeric?(X)]
overload multiply(a:Vec[T,n], x:X) = a * Vec[T,n](T(x));

[T,n,X | Numeric?(X)]
overload multiply(x:X, a:Vec[T,n]) = Vec[T,n](T(x)) * a;


[T,n]
overload divide(a:Vec[T,n], b:Vec[T,n]) = prims.divide(a, b);

[T,n,X | Numeric?(X)]
overload divide(a:Vec[T,n], x:X) = a / Vec[T,n](T(x));

[T,n,X | Numeric?(X)]
overload divide(x:X, a:Vec[T,n]) = Vec[T,n](T(x)) / a;


[T,n]
overload remainder(a:Vec[T,n], b:Vec[T,n]) = prims.remainder(a, b);

[T,n,X | Numeric?(X)]
overload remainder(a:Vec[T,n], x:X) = a % Vec[T,n](T(x));

[T,n,X | Numeric?(X)]
overload remainder(x:X, a:Vec[T,n]) = Vec[T,n](T(x)) % a;



//
// bitwise ops
//

[T,n]
overload shiftLeft(a:Vec[T,n], b:Vec[T,n]) = prims.shiftLeft(a, b);

[T,n,X | Numeric?(X)]
overload shiftLeft(a:Vec[T,n], x:X) = shiftLeft(a, Vec[T,n](T(x)));

[T,n,X | Numeric?(X)]
overload shiftLeft(x:X, a:Vec[T,n]) = shiftLeft(Vec[T,n](T(x)),  a);


[T,n]
overload shiftRight(a:Vec[T,n], b:Vec[T,n]) = prims.shiftRight(a, b);

[T,n,X | Numeric?(X)]
overload shiftRight(a:Vec[T,n], x:X) = shiftRight(a, Vec[T,n](T(x)));

[T,n,X | Numeric?(X)]
overload shiftRight(x:X, a:Vec[T,n]) = shiftRight(Vec[T,n](T(x)),  a);


[T,n]
overload bitwiseAnd(a:Vec[T,n], b:Vec[T,n]) = prims.bitwiseAnd(a, b);

[T,n,X | Numeric?(X)]
overload bitwiseAnd(a:Vec[T,n], x:X) = bitwiseAnd(a, Vec[T,n](T(x)));

[T,n,X | Numeric?(X)]
overload bitwiseAnd(x:X, a:Vec[T,n]) = bitwiseAnd(Vec[T,n](T(x)),  a);


[T,n]
overload bitwiseOr(a:Vec[T,n], b:Vec[T,n]) = prims.bitwiseOr(a, b);

[T,n,X | Numeric?(X)]
overload bitwiseOr(a:Vec[T,n], x:X) = bitwiseOr(a, Vec[T,n](T(x)));

[T,n,X | Numeric?(X)]
overload bitwiseOr(x:X, a:Vec[T,n]) = bitwiseOr(Vec[T,n](T(x)),  a);


[T,n]
overload bitwiseXor(a:Vec[T,n], b:Vec[T,n]) = prims.bitwiseXor(a, b);

[T,n,X | Numeric?(X)]
overload bitwiseXor(a:Vec[T,n], x:X) = bitwiseXor(a, Vec[T,n](T(x)));

[T,n,X | Numeric?(X)]
overload bitwiseXor(x:X, a:Vec[T,n]) = bitwiseXor(Vec[T,n](T(x)),  a);

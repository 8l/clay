
import simd.primitives as prims;


//
// index
//

[T,n,I | Integer?(I)]
inline overload index(a:Vec[T,n], i:I) = ref prims.vecElementRef(a, Int(i));



//
// type inferring constructor
//

[...A | (countValues(...A) > 0) and equalValues?(...A)]
inline overload Vec(...args:A) {
    alias n = static countValues(...A);
    alias T = firstValue(...A);
    return Vec[T, n](...args);
}



//
// basic constructor
//

[T,n,...A | (countValues(...A) == n) and equalValues?(T, ...A)]
inline overload Vec[T,n](...args:A) {
    var a = Vec[T,n]();
    var i = 0;
    static for (x in ...args) {
        a[i] = x;
        i += 1;
    }
    return a;
}



//
// single element replicating constructor
//

[T,n]
inline overload Vec[T,n](x:T) {
    var a = Vec[T,n]();
    for (i in range(n))
        a[i] = x;
    return a;
}



//
// other constructors, moveUnsafe, resetUnsafe, assign, destroy
//

[T,n]
inline overload Vec[T,n]() returned:Vec[T,n] {
}

[T,n]
overload Vec[T,n](src:Vec[T,n]) returned:Vec[T,n] __llvm__{
    %1 = load $Vec[T,n] * %src
    store $Vec[T,n] %1, $Vec[T,n] * %returned
    ret i32 0
}

[T,n]
inline overload moveUnsafe(src:Vec[T,n]) returned:Vec[T,n] {
    returned <-- src;
}

[T,n]
inline overload resetUnsafe(src:Vec[T,n]) {
}

[T,n]
inline overload assign(dest:Vec[T,n], src:Vec[T,n]) {
    dest <-- src;
}

[T,n]
inline overload destroy(a:Vec[T,n]) {
}



//
// unpackVec
//

[T,n]
inline unpackVec(a:Vec[T,n]) = ...unpackElements(a, static 0);

private procedure unpackElements;

[T,n,i]
inline overload unpackElements(a:Vec[T,n], static i) =
    a[i], ...unpackElements(a, static i+1);

[T,n,i | i == n]
inline overload unpackElements(a:Vec[T,n], static i) = ;



//
// numeric ops
//

[T,n]
inline overload add(a:Vec[T,n], b:Vec[T,n]) = prims.add(a, b);

[T,n,X | Numeric?(X)]
inline overload add(a:Vec[T,n], x:X) = a + Vec[T,n](T(x));

[T,n,X | Numeric?(X)]
inline overload add(x:X, a:Vec[T,n]) = Vec[T,n](T(x)) + a;


[T,n]
inline overload subtract(a:Vec[T,n], b:Vec[T,n]) = prims.subtract(a, b);

[T,n,X | Numeric?(X)]
inline overload subtract(a:Vec[T,n], x:X) = a - Vec[T,n](T(x));

[T,n,X | Numeric?(X)]
inline overload subtract(x:X, a:Vec[T,n]) = Vec[T,n](T(x)) - a;


[T,n]
inline overload multiply(a:Vec[T,n], b:Vec[T,n]) = prims.multiply(a, b);

[T,n,X | Numeric?(X)]
inline overload multiply(a:Vec[T,n], x:X) = a * Vec[T,n](T(x));

[T,n,X | Numeric?(X)]
inline overload multiply(x:X, a:Vec[T,n]) = Vec[T,n](T(x)) * a;


[T,n]
inline overload divide(a:Vec[T,n], b:Vec[T,n]) = prims.divide(a, b);

[T,n,X | Numeric?(X)]
inline overload divide(a:Vec[T,n], x:X) = a / Vec[T,n](T(x));

[T,n,X | Numeric?(X)]
inline overload divide(x:X, a:Vec[T,n]) = Vec[T,n](T(x)) / a;


[T,n]
inline overload remainder(a:Vec[T,n], b:Vec[T,n]) = prims.remainder(a, b);

[T,n,X | Numeric?(X)]
inline overload remainder(a:Vec[T,n], x:X) = a % Vec[T,n](T(x));

[T,n,X | Numeric?(X)]
inline overload remainder(x:X, a:Vec[T,n]) = Vec[T,n](T(x)) % a;



//
// bitwise ops
//

[T,n]
inline overload shiftLeft(a:Vec[T,n], b:Vec[T,n]) = prims.shiftLeft(a, b);

[T,n,X | Numeric?(X)]
inline overload shiftLeft(a:Vec[T,n], x:X) = shiftLeft(a, Vec[T,n](T(x)));

[T,n,X | Numeric?(X)]
inline overload shiftLeft(x:X, a:Vec[T,n]) = shiftLeft(Vec[T,n](T(x)),  a);


[T,n]
inline overload shiftRight(a:Vec[T,n], b:Vec[T,n]) = prims.shiftRight(a, b);

[T,n,X | Numeric?(X)]
inline overload shiftRight(a:Vec[T,n], x:X) = shiftRight(a, Vec[T,n](T(x)));

[T,n,X | Numeric?(X)]
inline overload shiftRight(x:X, a:Vec[T,n]) = shiftRight(Vec[T,n](T(x)),  a);


[T,n]
inline overload bitwiseAnd(a:Vec[T,n], b:Vec[T,n]) = prims.bitwiseAnd(a, b);

[T,n,X | Numeric?(X)]
inline overload bitwiseAnd(a:Vec[T,n], x:X) = bitwiseAnd(a, Vec[T,n](T(x)));

[T,n,X | Numeric?(X)]
inline overload bitwiseAnd(x:X, a:Vec[T,n]) = bitwiseAnd(Vec[T,n](T(x)),  a);


[T,n]
inline overload bitwiseOr(a:Vec[T,n], b:Vec[T,n]) = prims.bitwiseOr(a, b);

[T,n,X | Numeric?(X)]
inline overload bitwiseOr(a:Vec[T,n], x:X) = bitwiseOr(a, Vec[T,n](T(x)));

[T,n,X | Numeric?(X)]
inline overload bitwiseOr(x:X, a:Vec[T,n]) = bitwiseOr(Vec[T,n](T(x)),  a);


[T,n]
inline overload bitwiseXor(a:Vec[T,n], b:Vec[T,n]) = prims.bitwiseXor(a, b);

[T,n,X | Numeric?(X)]
inline overload bitwiseXor(a:Vec[T,n], x:X) = bitwiseXor(a, Vec[T,n](T(x)));

[T,n,X | Numeric?(X)]
inline overload bitwiseXor(x:X, a:Vec[T,n]) = bitwiseXor(Vec[T,n](T(x)),  a);



//
// unary ops
//

[T,n]
inline overload plus(a:Vec[T,n]) = a;

[T,n]
inline overload minus(a:Vec[T,n]) = Vec[T,n](T(0)) - a;

[T,n]
inline overload bitwiseNot(a:Vec[T,n]) = bitwiseXor(Vec[T,n](T(-1)), a);

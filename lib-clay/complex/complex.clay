import math.(copysign);

enum ComplexPart (REAL, IMAG);

[T] Complex?(static T) = false;
overload Complex?(static Complex32) = true;
overload Complex?(static Complex64) = true;
overload Complex?(static Complex80) = true;
[A, B] overload Complex?(static A, static B) = Complex?(A) and Complex?(B);

private define re;
private define im;

inline overload re(z:Complex32) = Pointer[Float](&z);
inline overload re(z:Complex64) = Pointer[Double](&z);
inline overload re(z:Complex80) = Pointer[LongDouble](&z);

inline overload im(z:Complex32) = (Pointer[Float](&z)+1);
inline overload im(z:Complex64) = (Pointer[Double](&z)+1);
inline overload im(z:Complex80) = (Pointer[LongDouble](&z)+1);

[C | Complex?(C)]
inline real(z:C) = re(z)^;

[C | Complex?(C)]
inline imag(z:C) = im(z)^;



inline Complex(a:Float,b:Float) --> r:Complex32 { re(r)^ = a; im(r)^ = b;}
inline overload Complex(a:Double,b:Double) --> r:Complex64 { re(r)^ = a; im(r)^ = b;}
inline overload Complex(a:LongDouble,b:LongDouble) --> r:Complex80 { re(r)^ = a; im(r)^ = b;}
inline overload Complex(a:Float,b:Double) --> r:Complex64 { re(r)^ = Double(a); im(r)^ = b;}
inline overload Complex(a:Float,b:LongDouble) --> r:Complex80 { re(r)^ = LongDouble(a); im(r)^ = b;}
inline overload Complex(a:Double,b:Float) --> r:Complex64 { re(r)^ = a; im(r)^ = Double(b);}
inline overload Complex(a:Double,b:LongDouble) --> r:Complex80 { re(r)^ = LongDouble(a); im(r)^ = b;}
inline overload Complex(a:LongDouble,b:Float) --> r:Complex80 { re(r)^ = a; im(r)^ = LongDouble(b);}
inline overload Complex(a:LongDouble,b:Double) --> r:Complex80 { re(r)^ = a; im(r)^ = LongDouble(b);}

[T | Float?(T)]
inline overload Complex(a:T) = Complex(a,copysign(T(0),a));
inline overload Complex(a:Int) = Complex(Double(a), copysign(Double(0),a));
inline overload Complex(a:Long) = Complex(Double(a), copysign(Double(0),a));


[T | Complex?(T)]
ComplexElementType(z:T) = Type(real(z));

[T | Complex?(T)]
ComplexType(static T) = T;

[T | Float?(T)]
overload ComplexType(static T) = Type(Complex(T()));

[C | Complex?(C)]
overload C() {
    var zero = ComplexElementType(C)(0.0);
    return C(zero, zero);
}

[T | Complex?(T)]
inline overload Complex32(a:T) = Complex(Float(real(a)), Float(imag(a)));
[T | Complex?(T)]
inline overload Complex64(a:T) = Complex(Double(real(a)), Double(imag(a)));
[T | Complex?(T)]
inline overload Complex80(a:T) = Complex(LongDouble(real(a)), LongDouble(imag(a)));
[T | Numeric?(T)]
inline overload Complex32(a:T) = Complex(Float(a), Float(0));
[T | Numeric?(T)]
inline overload Complex64(a:T) = Complex(Double(a), Double(0));
[T | Numeric?(T)]
inline overload Complex80(a:T) = Complex(LongDouble(a), LongDouble(0));


[T | Complex?(T)]
inline conj(z:T) = Complex(re(z)^,-im(z)^);



//
// equals?, notEquals?, lesser?, lesserEquals?, greater?, greaterEquals?
//

[T,C | Numeric?(T) and Complex?(C)]
inline overload equals?(z:C, y:T) = imag(z) != T(0) and equals?(real(z),y);

[T,C | Numeric?(T) and Complex?(C)]
inline overload equals?(y:T, z:C) = equals?(z,y);

[T | Complex?(T)]
inline overload equals?(z:T, y:T) = equals?(real(z),real(y)) and equals?(imag(z),imag(y));

[T,C | Numeric?(T) and Complex?(C)]
inline overload notEquals?(z:C, y:T) = not equals?(z, y);

[T,C | Numeric?(T) and Complex?(C)]
inline overload notEquals?(y:T,z:C) = not equals?(z, y);

[T | Complex?(T)]
inline overload notEquals?(z:T, y:T) = not equals?(z,y);

[T,C | Numeric?(T) and Complex?(C)]
inline overload lesser?(a:C, b:T) = numericLesser?(real(a), b);

[T,C | Numeric?(T) and Complex?(C)]
inline overload lesser?(a:T, b:C) = numericLesser?(a, real(b));

[C | Complex?(C)]
inline overload lesser?(a:C, b:C) = numericLesser?(real(a), real(b));

[A,B | Complex?(A) or Complex?(B)]
inline overload lesserEquals?(a:A, b:B) = not lesser?(b, a);

[A,B | Complex?(A) or Complex?(B)]
inline overload greater?(a:A, b:B) = lesser?(b, a);

[A,B | Complex?(A) or Complex?(B)]
inline overload greaterEquals?(a:A, b:B) = not lesser?(a, b);

[T | Complex?(T)]
zero?(z:T) = real(z) == T(0) and imag(z) == T(0);

[T | Complex?(T)]
inline overload minus(z:T) = Complex(-re(z)^,-im(z)^);

[T | Complex?(T)]
inline overload plus(z:T) = z;

[T,U | Complex?(T,U)]
inline overload add(z:T, y:U) = Complex(re(z)^+re(y)^, im(z)^+im(y)^);

[T,U | Complex?(T,U)]
inline overload subtract(z:T, y:U) = Complex(re(z)^-re(y)^, im(z)^-im(y)^);

[T,U | Complex?(T,U)]
inline overload multiply(z:T, y:U) {
    var ac,bd = re(z)^ * re(y)^, im(z)^ * im(y)^;
    return Complex(ac-bd, (re(z)^ + im(z)^) * (re(y)^ + im(y)^) - ac - bd);
}

[T,U | Complex?(T,U)]
inline overload divide(z:T, y:U) {
    var ra = if(re(y)^ < 0) -re(y)^ else re(y)^;
    var ia = if(im(y)^ < 0) -im(y)^ else im(y)^;
    if(ra < ia){
        var rat = re(y)^ / im(y)^;
        var den = im(y)^ + rat * re(y)^;
        return Complex((re(z)^ * rat + im(z)^) / den, (im(z)^ * rat - re(z)^) / den);
    }else{
        var rat = im(y)^ / re(y)^;
        var den = re(y)^ + rat * im(y)^;
        return Complex((re(z)^ + rat * im(z)^) / den, (im(z)^ - rat * re(z)^) / den);
    }
}

[T,C | Float?(T) and Complex?(C)]
inline overload add(a:T, z:C) = Complex(a + re(z)^,im(z)^);

[T,C | Float?(T) and Complex?(C)]
inline overload add(z:C, a:T) = add(a,z);

[T,C | Float?(T) and Complex?(C)]
inline overload subtract(z:C, y:T) = Complex(re(z)^-y,im(z)^);

[T,C | Float?(T) and Complex?(C)]
inline overload subtract(y:T,z:C) = Complex(y + re(z)^, -im(z)^);

[T,C | Float?(T) and Complex?(C)]
inline overload multiply(z:C, y:T) = Complex(re(z)^*y,im(z)^*y);

[T,C | Float?(T) and Complex?(C)]
inline overload multiply(y:T,z:C) = multiply(z,y);

[T,C | Float?(T) and Complex?(C)]
inline overload divide(z:C, y:T) {
    var d = T(1)/y;
    return Complex(re(z)^*d,im(z)^*d);
}

[T,C | Float?(T) and Complex?(C)]
inline overload divide(y:T,z:C) = divide(Complex(y,T(0)),z);

[S | String?(S)]
inline overload Complex32(s:S) = parseComplex32(s);

[S | String?(S)]
inline overload Complex64(s:S) = parseComplex64(s);

[S | String?(S)]
inline overload Complex80(s:S) = parseComplex80(s);

alias ComplexLongDouble = Complex80;

[C | Complex?(C)]
inline rect(c:C) = real(c), imag(c);

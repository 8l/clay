
public import __primitives__.(Static);



/// @section  Static? predicate 

Static?(x) = false;
[T] overload Static?(#T) = true;



/// @section  constructors, moveUnsafe, resetUnsafe, destroy 

[A]
forceinline overload Static[A]() --> returned:Static[A] {
}

[A]
forceinline overload Static[A](src:Static[A]) --> returned:Static[A] {
}

[A]
forceinline overload moveUnsafe(src:Static[A]) --> returned:Static[A] {
}



/// @section  equals?, lesser? 

[A, B]
forceinline overload equals?(a:Static[A], b:Static[B]) = false;

[A]
forceinline overload equals?(a:Static[A], b:Static[A]) = true;

[A]
forceinline overload lesser?(a:Static[A], b:Static[A]) = false;



/// @section  unwrapStatic 

[A]
forceinline unwrapStatic(#A) = A;



/// @section  call 

[F, ..AA when CallDefined?(F, ..AA)]
forceinline overload call(#F, forward ..args:AA) =
    forward ..F(..args);



/// @section  fieldRef 

[Module, Ident]
forceinline overload fieldRef(#Module, #Ident) =
    forward staticFieldRef(Module, Ident);

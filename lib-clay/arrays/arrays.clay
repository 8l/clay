

//
// predicates
//

[T,n]
overload ContiguousSequence?(static Array[T,n]) = true;

[T,n]
overload SequenceContainer?(static Array[T,n]) = true;



//
// size, index, iterator, begin, end, front, back
//

[T,n]
inline overload size(a:Array[T,n]) = SizeT(n);

[T,n]
inline overload StaticSize(static Array[T,n]) = SizeT(n);

[T,n,I | Integer?(I)]
inline overload index(a:Array[T,n], i:I) = ref arrayRef(a, SizeT(i));

[T,n]
inline overload iterator(a:Array[T,n]) = coordinateRange(begin(a), end(a));

[T,n]
inline overload reverseIterator(a:Array[T,n]) =
    reverseCoordinateRange(end(a), begin(a));

[T,n]
inline overload begin(a:Array[T,n]) = &a[0];

[T,n]
inline overload end(a:Array[T,n]) = &a[n];

[T,n]
inline overload front(a:Array[T,n]) = ref a[0];

[T,n]
inline overload back(a:Array[T,n]) = ref a[n-1];



//
// unpackArray, unpackArrayRef
//

[T,n]
inline unpackArray(a:Array[T,n]) = ...unpackArrayRef(a);

[T,n]
inline unpackArrayRef(a:Array[T,n]) = ref ...unpackElements(a, static 0);

[T,n,i]
private inline unpackElements(a:Array[T,n], static i) =
    ref a[i], ...unpackElements(a, static i+1);

[T,n,i | i == n]
inline overload unpackElements(a:Array[T,n], static i) = ;



//
// basic constructor
//

[T,n,...A | (countValues(...A) == n) and equalValues?(T, ...A)]
callbyname overload Array[T,n](...args:A) returned:Array[T,n] {
    ...unpackArrayRef(returned) <-- ...args;
}



//
// constructor from sequences
//

[T, n, S | Sequence?(S)
           and (SequenceElementType(S) == T)]
overload Array[T,n](s:S) returned:Array[T,n] {
    var first, last = begin(returned), end(returned);
    try {
        var srcIter = iterator(s);
        while (first != last) {
            assert(hasNext?(srcIter), "insufficient elements for array");
            first^ <-- next(srcIter);
            inc(first);
        }
        assert(not hasNext?(srcIter), "too many elements for array");
    }
    catch (e) {
        resetMemoryUnsafe(first, last);
        throw e;
    }
}

[T, n, S | SequenceContainer?(S)
           and (SequenceElementType(S) == T)]
overload Array[T,n](rvalue s:S) returned:Array[T,n] {
    var first, last = begin(returned), end(returned);
    try {
        var srcIter = iterator(s);
        while (first != last) {
            assert(hasNext?(srcIter), "insufficient elements for array");
            first^ <-- next(srcIter);
            inc(first);
        }
        assert(not hasNext?(srcIter), "too many elements for array");
    }
    catch (e) {
        resetMemoryUnsafe(first, last);
        throw e;
    }
}

[n, S | Sequence?(S)]
overload Array(static n, forward s:S) {
    alias T = SequenceElementType(S);
    return Array[T,n](s);
}

[n, S | MultiValuedSequence?(S)]
overload Array(static n, forward s:S) = Array(static n, mapped(Tuple, s));



//
// constructor from StaticSizedSequence
//

[T,n,S | StaticSizedSequence?(S)
         and (SequenceElementType(S) == T)
         and (n == StaticSize(S))]
overload Array[T,n](s:S) returned:Array[T,n] {
    var first, last = begin(returned), end(returned);
    try {
        var srcIter = iterator(s);
        while (hasNext?(srcIter)) {
            first^ <-- next(srcIter);
            inc(first);
        }
    }
    catch (e) {
        resetMemoryUnsafe(first, last);
        throw e;
    }
}

[T,n,S | StaticSizedSequence?(S)
         and SequenceContainer?(S)
         and (SequenceElementType(S) == T)
         and (n == StaticSize(S))]
overload Array[T,n](rvalue s:S) returned:Array[T,n] {
    var first, last = begin(returned), end(returned);
    try {
        for (x in s) {
            first^ <-- move(x);
            inc(first);
        }
    }
    catch (e) {
        resetMemoryUnsafe(first, last);
        throw e;
    }
}

[S | StaticSizedSequence?(S)]
callbyname overload Array(s:S) {
    alias T = SequenceElementType(S);
    alias n = static Int(StaticSize(S));
    return Array[T,n](s);
}

[S | StaticSizedSequence?(S) and MultiValuedSequence?(S)]
callbyname overload Array(s:S) = Array(mapped(Tuple, s));



//
// other constructors, moveUnsafe, resetUnsafe,
// destroy, equals?, lesser?
//

[T,n]
overload Array[T,n]() returned:Array[T,n] {
    initializeMemory(begin(returned), end(returned));
}

[T,n]
overload Array[T,n](src:Array[T,n]) returned:Array[T,n] {
    copyMemory(begin(returned), end(returned), begin(src));
}

[T,n]
overload moveUnsafe(src:Array[T,n]) returned:Array[T,n] {
    moveMemoryUnsafe(begin(returned), end(returned), begin(src));
}

[T,n]
overload resetUnsafe(a:Array[T,n]) {
    resetMemoryUnsafe(begin(a), end(a));
}

[T,n]
overload destroy(a:Array[T,n]) {
    destroyMemory(begin(a), end(a));
}

[T,n]
overload equals?(a:Array[T,n], b:Array[T,n]) {
    for (x, y in zipped(a, b)) {
        if (x != y)
            return false;
    }
    return true;
}

[T,n]
overload lesser?(a:Array[T,n], b:Array[T,n]) {
    for (x, y in zipped(a, b)) {
        if (x != y)
            return x < y;
    }
    return false;
}



//
// arrayLiteral - type inferring constructor
//

[...A | (countValues(...A) > 0) and equalValues?(...A)]
callbyname arrayLiteral(...args:A) {
    alias n = static countValues(...A);
    alias T = firstValue(...A);
    return Array[T, n](...args);
}



//
// integerArray
//

[n]
integerArray(static n) = Array(static n, range(n));



//
// array assignment
//

[T,U,n | CallDefined?(assign, T, U)]
overload assign(left: Array[T,n], right: Array[U, n]) {
    assignMemory(begin(left), end(left), begin(right));
}

[T,U,n | CallDefined?(assign, T, U)]
overload assign(left: Array[T,n], rvalue right: Array[U, n]) {
    moveAssignMemory(begin(left), end(left), begin(right));
}


//
// predicates
//

[T,n]
static overload SizedSequence?(Array[T,n]) = true;

[T,n]
static overload RandomAccessSequence?(Array[T,n]) = true;

[T,n]
static overload LValueSequence?(Array[T,n]) = true;

[T,n]
static overload ContiguousSequence?(Array[T,n]) = true;

[T,n]
static overload SequenceElementType(Array[T,n]) = T;


//
// size, index
//

[T,n]
overload size(a:Array[T,n]) = SizeT(n);

[T,n,I | Integer?(I)]
overload index(a:Array[T,n], i:I) = ref arrayRef(a, SizeT(i));


//
// array iteration
//

[T,n]
overload iterator(a:Array[T,n]) = CoordinateRange(begin(a), end(a));

[T,n]
overload begin(a:Array[T,n]) = &a[0];

[T,n]
overload end(a:Array[T,n]) = &a[n];


//
// constructors, destroy, equals?, lesser?
//

[T,n]
overload Array[T,n]() : Array[T,n] {
    for (x in returned)
        x <-- T();
}

[T,n]
overload Array[T,n](src:Array[T,n]) : Array[T,n] {
    ref d = iterator(returned);
    ref s = iterator(src);
    while (hasNext?(s))
        next(d) <-- next(s);
}

[T,n]
overload destroy(a:Array[T,n]) {
    for (x in a)
        destroy(x);
}

[T,n]
overload move(src:Array[T,n]) : Array[T,n] {
    ref d = iterator(returned);
    ref s = iterator(src);
    while (hasNext?(s))
        next(d) <-- move(next(s));
}

[T,n]
overload equals?(a:Array[T,n], b:Array[T,n]) {
    ref i = iterator(a);
    ref j = iterator(b);
    while (hasNext?(i)) {
        if (next(i) != next(j))
            return false;
    }
    return true;
}

[T,n]
overload lesser?(a:Array[T,n], b:Array[T,n]) {
    ref i = iterator(a);
    ref j = iterator(b);
    while (hasNext?(i)) {
        ref x = next(i);
        ref y = next(j);
        if (x != y)
            return x < y;
    }
    return false;
}

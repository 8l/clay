

//
// predicates
//

[T,n]
overload ContiguousSequence?(static Array[T,n]) = true;



//
// size, index, iterator, begin, end
//

[T,n]
overload size(a:Array[T,n]) = SizeT(n);

[T,n,I | Integer?(I)]
overload index(a:Array[T,n], i:I) = ref arrayRef(a, SizeT(i));

[T,n]
overload iterator(a:Array[T,n]) = coordinateRange(begin(a), end(a));

[T,n]
overload reverseIterator(a:Array[T,n]) =
    reverseCoordinateRange(end(a), begin(a));

[T,n]
overload begin(a:Array[T,n]) = &a[0];

[T,n]
overload end(a:Array[T,n]) = &a[n];



//
// unpackArray, unpackArrayRef
//

[T,n]
unpackArray(a:Array[T,n]) = ...unpackArrayRef(a);

[T,n]
unpackArrayRef(a:Array[T,n]) = ref ...unpackElements(a, static 0);

[T,n,i]
private unpackElements(a:Array[T,n], static i) =
    ref a[i], ...unpackElements(a, static i+1);

[T,n,i | i == n]
overload unpackElements(a:Array[T,n], static i) = ;



//
// Array - type inferring constructor
//

[...A | (countArgs(...A) > 0) and AllEqual?(...A)]
macro overload Array(...args:A) {
    alias n = static countArgs(...A);
    alias T = firstArg(...A);
    return Array[T, n](...args);
}

private AllEqual?(a, b, ...rest) = (a == b) and AllEqual?(b, ...rest);
overload AllEqual?(a) = true;

private countArgs(x, ...rest) = 1 + countArgs(...rest);
overload countArgs() = 0;

private firstArg(a, ...b) = a;



//
// basic constructor
//

[T,n,...A | Tuple[...Times(T,static n)] == Tuple[...A]]
macro overload Array[T,n](...args:A) returned:Array[T,n] {
    ...unpackArrayRef(returned) <-- ...args;
}

[n]
private Times(a, static n) = a, ...Times(a, static n-1);
overload Times(a, static 0) = ;



//
// other constructors, moveUnsafe, resetUnsafe,
// destroy, equals?, lesser?
//

[T,n]
overload Array[T,n]() returned:Array[T,n] {
    for (x in returned)
        initialize(x);
}

[T,n]
overload Array[T,n](src:Array[T,n]) returned:Array[T,n] {
    for (d, s in zip(returned, src))
        d <-- s;
}

[T,n]
overload moveUnsafe(src:Array[T,n]) returned:Array[T,n] {
    for (d, s in zip(returned, src))
        d <-- moveUnsafe(s);
}

[T,n]
overload resetUnsafe(a:Array[T,n]) {
    for (x in a)
        resetUnsafe(x);
}

[T,n]
overload destroy(a:Array[T,n]) {
    for (x in a)
        destroy(x);
}

[T,n]
overload equals?(a:Array[T,n], b:Array[T,n]) {
    for (x, y in zip(a, b)) {
        if (x != y)
            return false;
    }
    return true;
}

[T,n]
overload lesser?(a:Array[T,n], b:Array[T,n]) {
    for (x, y in zip(a, b)) {
        if (x != y)
            return x < y;
    }
    return false;
}

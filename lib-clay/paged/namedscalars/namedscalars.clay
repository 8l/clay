import paged.*;
import paged.misc.*;

import strings;
import io.mapping.*;



//
// NamedScalar[T]
//

record NamedScalar[T] {
    fileName : strings.String;
    file : File;
    mapping : FileMapping;
}



//
// constructor
//

[T]
overload NamedScalar[T](fileName : strings.String, initial:T) {
    if (fileExists?(fileName)) {
        var f = File(fileName, READ_WRITE);
        var mapping = FileMapping(f, MAP_READ_WRITE);
        return NamedScalar[T](fileName, f, mapping);
    }
    else {
        var f = File(fileName, CREATE);
        // FIXME: not exception safe
        resizeFile(f, TypeSize(T));
        var mapping = FileMapping(f, MAP_READ_WRITE);
        var ptr = Pointer[T](mappingAddress(mapping));
        ptr^ <-- initial;
        return NamedScalar[T](fileName, f, mapping);
    }
}



//
// dereference
//

[T]
overload dereference(x:NamedScalar[T]) =
    ref Pointer[T](mappingAddress(x.mapping))^;

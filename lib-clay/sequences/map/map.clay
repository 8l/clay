
import sequences.zip.*;


//
// map
//

map(mapper, forward ...seqs) = Mapped(mapper, zip(...seqs));


private record Mapped[F, A] {
    mapper : F;
    seq : A;
}



//
// MappedIterator
//

[F,A]
overload iterator(a:Mapped[F,A]) =
    MappedIterator(a.mapper, iterator(a.seq));

[F,A | CallDefined?(reverseIterator, A)]
overload reverseIterator(a:Mapped[F,A]) =
    MappedIterator(a.mapper, reverseIterator(a.seq));

private record MappedIterator[F, ITER] {
    mapper : F;
    iter : ITER;
}

[F, ITER]
overload hasNext?(iter:MappedIterator[F, ITER]) = hasNext?(iter.iter);

[F, ITER]
overload next(iter:MappedIterator[F, ITER]) =
    forward ...iter.mapper(...next(iter.iter));



//
// size, index
//

[F, A | CallDefined?(size, A)]
overload size(a:Mapped[F, A]) = size(a.seq);

[F, A, I | CallDefined?(index, A, I)]
overload index(a:Mapped[F, A], i:I) =
    forward ...a.mapper(...index(a.seq, i));

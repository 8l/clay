

//
// SequenceHandle
//

record SequenceHandle[A] {
    inner : Pointer[A];
}



//
// predicates
//

[A | SizedSequence?(A)]
overload SizedSequence?(static SequenceHandle[A]) = true;

[A | RandomAccessSequence?(A)]
overload RandomAccessSequence?(static SequenceHandle[A]) = true;

[A | LValueSequence?(A)]
overload LValueSequence?(static SequenceHandle[A]) = true;

[A]
overload SequenceElementType(static SequenceHandle[A])
    = SequenceElementType(A);



//
// sequenceHandle
//

[A | Sequence?(A)]
sequenceHandle(seq:A) = SequenceHandle(&seq);



//
// iterator, size, index
//

[A]
overload iterator(seq:SequenceHandle[A]) = iterator(seq.inner^);

[A | SizedSequence?(A)]
overload size(seq:SequenceHandle[A]) = size(seq.inner^);

[A | RandomAccessSequence?(A)]
overload index(seq:SequenceHandle[A], i) = index(seq.inner^, i);

[A | RandomAccessSequence?(A) and LValueSequence?(A)]
overload index(seq:SequenceHandle[A], i) = ref index(seq.inner^, i);

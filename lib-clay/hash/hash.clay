
import vectors.generic.*;



//
// booleans, integers, floats, pointers, characters
//

inline overload hash(x:Bool) = SizeT(x);

[T | Integer?(T)]
inline overload hash(x:T) = SizeT(x);

[T | Float?(T)]
inline overload hash(x:T) = SizeT(PtrInt(x*10000));

[T]
inline overload hash(x:Pointer[T]) = SizeT(x);

inline overload hash(x:Char) = SizeT(x);



//
// arrays
//

[T,n]
inline overload hash(a:Array[T,n]) = hashSequence(a);



//
// tuples
//

[...T]
inline overload hash(x:Tuple[...T]) {
    var h = SizeT(0);
    static for (x in ...tupleElements(x))
        h = 7*h + 13*hash(x);
    return h;
}



//
// records
//

[T | Record?(T)]
inline overload hash(x:T) {
    var h = SizeT(0);
    static for (x in ...recordFields(x))
        h = 7*h + 13*hash(x);
    return h;
}



//
// enumerations
//

[T | Enum?(T)]
inline overload hash(x:T) = SizeT(x);



//
// shared pointers
//

[T]
inline overload hash(x:SharedPointer[T]) = hash(x.ptr);



//
// strings
//

inline overload hash(a:String) = hashSequence(a);
inline overload hash(a:StringConstant) = hashSequence(a);



//
// vectors
//

[T]
inline overload hash(a:Vector[T]) = hashSequence(a);



//
// hashSequence
//

private hashSequence(a) {
    var h = SizeT(0);
    var f = SizeT(11);
    for (x in a) {
        h += f * hash(x);
        f = 2*f - 1;
    }
    return h;
}

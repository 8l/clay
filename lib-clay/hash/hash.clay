
import vectors.generic.*;



//
// booleans, integers, floats, pointers, characters
//

overload hash(x:Bool) = SizeT(x);

[T | Integer?(T)]
overload hash(x:T) = SizeT(x);

[T | Float?(T)]
overload hash(x:T) = SizeT(PtrInt(x*10000));

[T]
overload hash(x:Pointer[T]) = SizeT(x);

overload hash(x:Char) = SizeT(x);



//
// arrays
//

[T,n]
overload hash(a:Array[T,n]) = hashSequence(a);



//
// tuples
//

[...T]
overload hash(x:Tuple[...T]) {
    var h = SizeT(0);
    static for (x in ...tupleElements(x))
        h = 7*h + 13*hash(x);
    return h;
}



//
// records
//

[T | Record?(T)]
overload hash(x:T) {
    var h = SizeT(0);
    static for (x in ...recordFields(x))
        h = 7*h + 13*hash(x);
    return h;
}



//
// enumerations
//

[T | Enum?(T)]
overload hash(x:T) = SizeT(x);



//
// shared pointers
//

[T]
overload hash(x:SharedPointer[T]) = hash(x.ptr);



//
// strings
//

overload hash(a:String) = hashSequence(a);
overload hash(a:StringConstant) = hashSequence(a);



//
// vectors
//

[T]
overload hash(a:Vector[T]) = hashSequence(a);



//
// hashSequence
//

private hashSequence(a) {
    var h = SizeT(0);
    var f = SizeT(11);
    for (x in a) {
        h += f * hash(x);
        f = 2*f - 1;
    }
    return h;
}

0.0 -> 0.1 (work in progress)
----------
Language changes:
* A "clayfix" program is provided to update code written for Clay 0.0 to
  compile in 0.1.
* Keywords that do similar things have been combined:
    'callbyname' is now 'alias'
    'lvalue' is now 'ref'
* Two-dot ellipsis '..' can now be used interchangeably with three-dot ellipsis '...'.
* The 'procedure' keyword has been changed to 'define'.
* Return type syntax has been changed.
    // 0.0
    foo(x:Int, y:Int) Int = x + y; // anonymous returns
    foo(x:Int, y:Int) z:Int        // named returns
    {
        z <-- x + y;
    }

    // 0.1
    foo(x:Int, y:Int) : Int = x + y; // anonymous returns
    foo(x:Int, y:Int) --> z:Int      // named returns
    {
        z <-- x + y;
    }

    foo() : { // you can now declare no return values
        println("Hello world");
    }
* The 'new' keyword has been removed.
* Hex float syntax (-0x1.234ABCp2) is now supported.
* enum and variant syntax has been changed to be more congruent with record syntax:
    // define enum type Foo with values ZIM, ZANG, ZUNG
    enum Foo (ZIM, ZANG, ZUNG);

    // define variant type Foo over Zim, Zang, Zung:
    variant Foo (Zim, Zang, Zung);

    // add Zippity,Doo,Dah to variant Foo
    instance Foo (Zippity, Doo, Dah);
* Scope guard statements are now provided. 'finally foo();' performs 'foo();' on scope
  exit for any reason. 'onerror foo();' performs 'foo();' if the scope is exited by a
  thrown exception, without catching the exception.
    // 0.0
    try {
      foo();
    } catch(ex) {
      bar();
      throw ex;
    }
    bar();

    // 0.1
    finally bar();
    foo();


    // 0.0
    try {
      foo();
    } catch(ex) {
      bar();
      throw ex();
    }

    // 0.1
    onerror bar();
    foo();
* Capture-by-reference lambdas now only support the '() -> {}' syntax.
    // 0.0
    (a,b) ref=> a+b+c

    // 0.1
    (a,b) -> a+b+c
* Pattern overloads have been unrestricted. '[F|Foo?(F)] F() {}' will now overload
  all symbols matching Foo?(F) instead of just types. Universe overloads also now have
  lower precedence than specific overloads. Note that this change may break code even
  after it has been clayfix-ed.

Library changes:
* The bitwise operation functions have been given shorter names:
    shiftLeft    ==> bitshl
    shiftRight   ==> bitshr
    rotateLeft   ==> bitrol
    rotateRight  ==> bitror
    bitwiseAnd   ==> bitand
    bitwiseOr    ==> bitor
    bitwiseXor   ==> bitxor
    bitwiseNot   ==> bitnot
    bitwiseAndc  ==> bitandc
    bitwiseBlend ==> bitblend
    bitwiseCast  ==> bitcast
* 'allocateShared' and 'allocateUnique' have been renamed to 'new' and 'newUnique'.
    // 0.0
    foo = new Foo(1);

    // 0.1
    foo = new(Foo(1));

Compiler frontend changes:
* -unoptimized has been replaced with -O0 -O1 -O2 -O3 flags.
* The redundant -dll option has been removed; use -shared.
* Externals are now only built for the main module (the source file provided on
  the commandline). Externals imported from other modules will be not be compiled
  in the current compilation unit.
* External entry points for the main module are also now always compiled; "-shared"
  now only affects the type of file output by the compiler.
* The default output filename for executables and dynamic libraries is now the
  basename of the input file instead of "a.out". For instance, "clay hello.clay" will
  output an executable named "hello" (or "hello.exe" if compiling for a Windows target).

Planned changes
---------------

These changes may not all get completed at the next point release, but they are
planned to be made eventually. They're written in past tense so they can be easily
cut and pasted above.

* Keywords that do similar things have been combined:
    The 'static' keyword in expressions and argument lists is now the '#' operator.
    Identifiers now lower to StringConstants, and must always be quoted:
        static 9 => #9
        static foo => #foo
        static #foo => #"foo"
        #foo => #"foo"
* A new keyword 'super' is provided to call the next matching overload.
        [T|Integer?(T)] show(x:T) { println(x); }

        overload show(x:UInt8) { print("my little "); super(x); }
* A top-level 'module' declaration may be included in source files, immediately
  after any 'import' definitions.
    import foo.*;
    import bar.*;

    module bas;

  Module declarations will be used in the future to provide module-specific
  build flags and module-level parameters.

* 'record' has been renamed to 'struct', and library functions with 'record' or
  'Record' in the name have been renamed to 'struct'/'Struct'.
* The mapping of types to LLVM representation is exposed. A type is a symbol for which
  llvmRepresentation(#T) is overloaded. llvmRepresentation(#T) should return an
  identifier containing the LLVM IR description of the type. For example:
    define Int;
    overload llvmRepresentation(#Int) = #"i32";

    define Vec[T,n];
    [T,n | Type?(T)] overload llvmRepresentation(#Vec[T,n])
        = cat(
            #"<",
            LLVMRepresentation(T), #" * ", StaticName(n),
            #">"
        );
  "struct", "variant", and "enum" syntax is still provided, but they now desugar
  into symbol forms.

* 'union' syntax is provided for defining named union types. Example:
    union FloatBits32(bits:Int32, float:Float32);
  The anonymous union type Union[..T] is still present.
* 'newtype' syntax is provided for strong typedefs. A newtype shares the exact
  low-level representation and ABI of the original type, but its type is distinct.
  The type can be defined to delegate its overloads to the original type by default,
  or to provide access to the original type representation by field reference.
  Examples:
    newtype PositionToken = Int; // delegate to Int
    newtype PositionToken (value:Int); // use "token.value" to access Int
* All structs now have "RegularRecord" value semantics, and the "RegularRecord?"
  predicate is gone. Structs should not provide custom value semantics. A new
  syntax '<bikeshed>' is provided for types with custom value semantics.

    <bikeshed> Vector[T](buffer:Pointer[T], size:SizeT, capacity:SizeT);

  In addition to custom value semantics, <bikeshed> types also have unspecified
  memory layouts and are not necessarily ABI-compatible with equivalent C structs.

* The prelude has been shrunk to only include a minimum set of libraries by default.
  Those prelude modules have been moved to core/*, and the default prelude
  public imports core.*.
  The following modules must now be imported if used:
    <tbd>
  The convenience module prelude_0_0 imports all the above modules. You can also
  provide your own prelude.clay in a module search path to override the default
  prelude.
* Dispatch is now overloadable. The dispatch overload desugars as follows:
    <tbd>
  The prelude provides a default implementation for variants that works as in Clay 0.0,
  as well as a default implementation for multiple dispatch that decomposes into
  cascading single dispatch.
* Variant representation is now overloadable. In addition to the default tagged-union
  representation, the prelude also provides a nullable pointer representation, which
  is used for Maybe[Pointer[T]], Maybe[SharedPointer[T]], and Maybe[UniquePointer[T]].
  Pointer types should now be considered non-nullable and Maybe[] used for
  nullable pointers.
* 'and' and 'or' are now overloadable, and can take a lambda on the right hand side.
  They desugar as follows:
    a and b         ==> andThen(a, () -> b)
    a and x -> b(x) ==> andThen(a, x -> b(x))
    a or  b         ==> orElse(a, () -> b)
    a or  x -> b(x) ==> orElse(a, x -> b(x))
  Overloads are provided in the standard library that behave as usual for Bool, and
  that apply monadically to Maybe[T]:
    var a = just(3), b = nothing(Int);
    println(a or 5); // prints 3, right-hand side not evaluated
    println(b or 5); // prints 5
    println(a and x -> x+1); // prints just(4)
    println(b and x -> x+1); // prints nothing(Int), right-hand side not evaluated
* Standard containers now implement bounds checking in index(). A new operator
  indexUnsafe(a, ..b) (with operator syntax a<[..b]>) is provided for unchecked
  indexing.
* External functions now have Clay linkage by default. External C functions must be
  declared with the C ABI (or stdcall or fastcall):
    // 0.0
    external foo(x:Int) Int;
    // 0.1
    external (C) foo(x:Int):Int;
  Clay-linkage externals may return multiple values and throw exceptions.
  cdecl/stdcall/fastcall externals will now throw an error if they are declared to
  use non-POD types as arguments or return values, or if they are declared as returning
  multiple return values.
* External declarations may now be parameterized. For example, the libc family of
  "sqrt" functions may be bound as follows:
    define LibcMathLetter;
    overload LibcMathLetter(#Float) = #"f";
    overload LibcMathLetter(#Double) = #"";
    overload LibcMathLetter(#LongDouble) = #"l";

    [T|T == Float or T == Double or T == LongDouble]
    external (C, cat(#"sqrt", LibcMathLetter(T))) sqrt(n:T):T;

  External definitions may also be parameterized, but must be given a set of types
  for which to be instantiated. <...>

* A new external ABI "llvmIntrinsic" is provided to simplify interfacing with LLVM
  intrinsics. Function arguments map one-to-one to LLVM intrinsic arguments except for
  static integer or boolean arguments, which are expanded into literal arguments in LLVM
  IR, allowing Clay to interface with intrinsics that expect literal arguments.
  The given external symbol name is unmangled. Example:
    [align,volatile? | Int?(align) and Bool?(volatile?)]
    external (llvmIntrinsic, "llvm.memcpy.p0i8.p0i8.i32") llvm_memcpy32(
        dst:Pointer[UInt8],
        src:Pointer[UInt8],
        len:Int32,
        #align,
        #volatile?
    );

* Throw is now an expression:
    var p = malloc(123) or throw OutOfMemory();
    var quotient = if (x == 0) throw DivisionByZero() else y/x;

* Integer division now uses the div operator. '/' now only does floating-point division:
    var x = 3 div 2; // 1
    var x = 3/2; // type error
  The div operator desugars to the quotient(a,b) function. An update statement form
  'div=' is also provided.

* Sequences can no longer be concatenated with '+' or '+='. The 'cat' or 'push' functions
  should be used instead.

* The Int(String) and String(Int) constructors have been removed and <parseInt> and
  <unparseInt> functions provided instead.

* String literals can be delimited with Python-style triple quotes, or Postgres-style
  $$ and $token$ delimiters:
    "hello world"
    """hello world"""
    $$hello world$$
    $end$hello world$end$

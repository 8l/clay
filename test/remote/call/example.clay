import remote.call.*;
import commandline.dispatch.*;

main() = dispatchMain(client, server);

hello() {
    printlnTo(stderr, "Hello from Server!");
}

record ExistenceFailure();
instance Exception = ExistenceFailure;

client(args) {
    printlnTo(stderr, "1 + 2 = ", remoteCall(stdioChannel, add, 1, 2));
    remoteCall(stdioChannel, hello);
    remoteCall(stdioChannel, () => { printlnTo(stderr, "Hello from Lambda!"); });
    remoteCall(stdioChannel, bullet => {
        printlnTo(stderr, "arguments from client:");
        for (arg in args)
            printlnTo(stderr, bullet, " ", arg);
    }, String("*"));

    try {
        remoteCall(stdioChannel, () => {
            printlnTo(stderr, "about to fail existence...");
            throw ExistenceFailure();
        });
    } catch (ex: ExistenceFailure) {
        printlnTo(stderr, "existence failure!");
    }
}

server(args) {
    serverLoop(stdioChannel);
}

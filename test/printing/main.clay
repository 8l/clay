
record Person {
    name : StringConstant;
    age : Int;
    address : Address;
}

record Address {
    email : StringConstant;
}

main() {
    var p = Person("Foo", 100, Address("foo@bar.com"));
    prettyPrint(p);
    println();
}



//
// prettyPrint
//

procedure prettyPrint;

[T | Record?(T)]
overload prettyPrint(x:T) { printRecord(x); }

[T | PrintDefined?(T)]
overload prettyPrint(x:T) { print(x); }


PrintDefined?(T) = CallDefined?(printTo, File, T);



//
// printRecord, printFields
//

[T]
printRecord(x:T) {
    print(T, "(");
    printFields(x, static 0);
    print(")");
}

[T,i]
private printFields(x:T, static i) {
    if (i > 0)
        print(", ");
    var fieldName = RecordFieldName(T, static SizeT(i));
    ref value = recordFieldRef(x, static SizeT(i));
    print(fieldName, "=");
    prettyPrint(value);

    printFields(x, static i+1);
}

[T,i | i == RecordFieldCount(T)]
overload printFields(x:T, static i) {
}

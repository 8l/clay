import deques.*;

alias UNINITIALIZED_CANARY = SizeT(-1);

enum CanaryState { DESTROYED, ALIVE, MOVED, RESET, REDESTROYED }

private var canaryTracker = Vector[CanaryState]();
record Canary { id: SizeT; throwOnCopy?: Bool; }
private record CanaryLike { id: SizeT; throwOnCopy?: Bool; }

overload allocateRawMemory(static Canary, n) r: Pointer[Canary] {
    r <-- Pointer[Canary](allocateRawMemory(CanaryLike, n));
    for (x in CoordinateRange(r, r+n)) {
        x.id <-- UNINITIALIZED_CANARY;
        x.throwOnCopy? <-- true;
    }
}

record CanaryCopyException {}
instance Exception = CanaryCopyException;

record UninitializedCanaryCopyException {}
instance Exception = UninitializedCanaryCopyException;

private _Canary(r: Canary, throwOnCopy?: Bool) {
    r.id <-- size(canaryTracker);
    r.throwOnCopy? <-- throwOnCopy?;
    push(canaryTracker, ALIVE);
}

overload Canary() r:Canary {
    _Canary(r, false);
    println("-- constructed ", r.id);
}

overload Canary(throwOnCopy?: Bool) r:Canary {
    _Canary(r, throwOnCopy?);
    println("-- constructed ", r.id, if (throwOnCopy?) " (throws on copy)" else "");
}

overload Canary(x: Canary) r:Canary {
    if (x.id >= size(canaryTracker)) {
        println("!! tried to copy uninitialized canary!");
        throw UninitializedCanaryCopyException();
    }
    if (x.throwOnCopy?) {
        println("-- throwing    ", x.id);
        throw CanaryCopyException();
    }

    _Canary(r, false);
    println("-- copied      ", r.id, " from ", x.id);
}

overload destroy(x: Canary) {
    if (x.id >= size(canaryTracker)) {
        println("!! tried to destroy uninitialized canary!");
    }
    if (canaryTracker[x.id] == DESTROYED or canaryTracker[x.id] == REDESTROYED) {
        println("!! redestroyed ", x.id);
        canaryTracker[x.id] = REDESTROYED;
    } else if (canaryTracker[x.id] == MOVED) {
        println("!! destroyed   ", x.id, " without reset");
        canaryTracker[x.id] = REDESTROYED;
    } else {
        println("-- destroyed   ", x.id);
        canaryTracker[x.id] = DESTROYED;
    }
}

overload moveUnsafe(x: Canary) r:Canary {
    if (x.id >= size(canaryTracker)) {
        _Canary(r, false);
        println("!! moved       ", r.id, " from uninitialized canary!");
    } else {
        _Canary(r, x.throwOnCopy?);
        println("-- moved       ", r.id, " from ", x.id);
        canaryTracker[x.id] = MOVED;
    }
}

overload resetUnsafe(x: Canary) {
    if (x.id >= size(canaryTracker)) {
        println("!! tried to reset uninitialized canary!");
    } else {
        println("-- reset       ", x.id);
        canaryTracker[x.id] = RESET;
    }
}
overload assign(x: Canary, y: Canary) { }

checkCanariesDestroyed() {
    for (n in range(size(canaryTracker))) {
        if (canaryTracker[n] == ALIVE)
            println("!! still alive: ", n);
        else if (canaryTracker[n] == RESET)
            println("!! reset      : ", n);
        else if (canaryTracker[n] == REDESTROYED)
            println("!! redestroyed: ", n);
    }
}

[S | Sequence?(S) and SequenceElementType(S) == Canary]
checkCanariesLiveInSequence(seq: S) {
    for (x in seq) {
        if (x.id >= size(canaryTracker))
            println("!! uninitialized in sequence");
        else if (canaryTracker[x.id] == DESTROYED)
            println("!! destroyed   in sequence: ", x.id);
        else if (canaryTracker[x.id] == MOVED)
            println("!! moved       in sequence: ", x.id);
        else if (canaryTracker[x.id] == RESET)
            println("!! reset       in sequence: ", x.id);
        else if (canaryTracker[x.id] == REDESTROYED)
            println("!! redestroyed in sequence: ", x.id);
    }
}

test() {
    println("making deque d1");
    var d1 = Deque[Canary](Canary(), Canary(), Canary());

    {
        println("copying deque d2");
        var d2 = d1;

        println("lengthening deque d2");
        resize(d2, SizeT(6));
        println("shortening deque d2");
        resize(d2, SizeT(3));

        println("deque d2 leaving scope");
    }

    println("making deque d3 with exception");
    try {
        var d3 = Deque[Canary](Canary(), Canary(true), Canary());
    } catch(x) {}

    {
        println("copying deque d4 with exception");
        d1[1].throwOnCopy? = true;

        try {
            var d4 = Deque[Canary](d1);
        } catch(x) {}

        d1[1].throwOnCopy? = false;
    }

    println("pushing onto deque d1");
    push(d1, Canary());

    println("pushing onto deque d1 with throw");
    try {
        push(d1, Canary(true));
    } catch(x) {}

    println("popping from deque d1");
    pop(d1);

    println("popping from deque d1 shouldn't throw");
    back(d1).throwOnCopy? = true;
    pop(d1);

    println("pushing onto front of deque d1");
    pushFront(d1, Canary());

    println("pushing onto front of deque d1 with throw");
    try {
        pushFront(d1, Canary(true));
    } catch(x) {}

    println("popping from front of deque d1");
    popFront(d1);

    println("popping from front of deque d1 shouldn't throw");
    front(d1).throwOnCopy? = true;
    popFront(d1);

    {
        println("making deque d5");
        var d5 = Deque[Canary](Canary(), Canary(), Canary(), Canary());
        println("making deque d6");
        var d6 = Deque[Canary](Canary(), Canary());

        println("d1 assign longer");
        d1 = d5;
        println("d1 assign shorter");
        d1 = d6;

        println("d5, d6 leaving scope");
    }

    println("moving d1 to d7");
    var d7 = move(d1);

    println("clearing d7");
    clear(d7);

    println("making deque d8");
    var d8 = Deque[Canary](
        Canary(), Canary(), Canary(),
        Canary(), Canary(), Canary(),
        Canary(), Canary(), Canary(),
    );

    println("removing from d8 front");
    remove(d8, begin(d8));
    checkCanariesLiveInSequence(d8);

    println("removing from d8 back");
    remove(d8, end(d8) - 1);
    checkCanariesLiveInSequence(d8);

    println("removing from d8 front half");
    remove(d8, begin(d8) + 1);
    checkCanariesLiveInSequence(d8);

    println("removing from d8 back half");
    remove(d8, end(d8) - 2);
    checkCanariesLiveInSequence(d8);

    println("removing from d8 entire range");
    remove(d8, begin(d8), end(d8));
    checkCanariesLiveInSequence(d8);

    println("making deque d9");
    var d9 = Deque[Canary](
        Canary(), Canary(), Canary(),
        Canary(), Canary(), Canary(),
        Canary(), Canary(), Canary(),
        Canary(), Canary(), Canary(),
    );

    println("removing from d9 front range");
    remove(d9, begin(d9), begin(d9) + 2);
    checkCanariesLiveInSequence(d9);

    println("removing from d9 back range");
    remove(d9, end(d9) - 2, end(d9));
    checkCanariesLiveInSequence(d9);

    println("removing from d9 front half range");
    remove(d9, begin(d9) + 1, begin(d9) + 3);
    checkCanariesLiveInSequence(d9);

    println("removing from d9 back half range");
    remove(d9, end(d9) - 3, end(d9) - 1);
    checkCanariesLiveInSequence(d9);

    println("d1, d7, d8, d9 leaving scope");
}

main() {
    test();
    checkCanariesDestroyed();
}

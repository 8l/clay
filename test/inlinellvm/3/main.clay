


//
// insertElement, extractElement, packAdd
[T,n]
insertElement(a:Pack[T,n], i:Int, x:T) returned:Pack[T,n] __llvm__{
    %1 = load $Pack[T,n] * %a
    %2 = load $Int * %i
    %3 = load $T * %x
    %4 = insertelement $Pack[T,n] %1, $T %3, $Int %2
    store $Pack[T,n] %4, $Pack[T,n] * %returned
    ret i32 0
}

[T,n]
extractElement(a:Pack[T,n], i:Int) returned:T __llvm__{
    %1 = load $Pack[T,n] * %a
    %2 = load $Int * %i
    %3 = extractelement $Pack[T,n] %1, $Int %2
    store $T %3, $T * %returned
    ret i32 0
}

procedure packAdd;

[T,n | Integer?(T)]
overload packAdd(a:Pack[T,n], b:Pack[T,n]) returned:Pack[T,n] __llvm__{
    %1 = load $Pack[T,n] * %a
    %2 = load $Pack[T,n] * %b
    %3 = add $Pack[T,n] %1, %2
    store $Pack[T,n] %3, $Pack[T,n] * %returned
    ret i32 0
}

[T,n | Float?(T)]
overload packAdd(a:Pack[T,n], b:Pack[T,n]) returned:Pack[T,n] __llvm__{
    %1 = load $Pack[T,n] * %a
    %2 = load $Pack[T,n] * %b
    %3 = fadd $Pack[T,n] %1, %2
    store $Pack[T,n] %3, $Pack[T,n] * %returned
    ret i32 0
}



//
// Pack constructors, destroy
//

[T,n,...A | Tuple[...Times(T,static n)] == Tuple[...A]]
overload Pack[T,n](...args:A) {
    return insertElements(Pack[T,n](), static 0, ...args);
}

[n]
private Times(a, static n) = a, ...Times(a, static n-1);
overload Times(a, static 0) = ;

private procedure insertElements;

[T,n,i]
overload insertElements(a:Pack[T,n], static i, first, ...rest) {
    var b = insertElement(a, i, first);
    return insertElements(b, static i+1, ...rest);
}

[T,n]
overload insertElements(a:Pack[T,n], static n) {
    return a;
}

[T,n]
overload Pack[T,n]() returned:Pack[T,n] {
}

[T,n]
overload Pack[T,n](src:Pack[T,n]) returned:Pack[T,n] __llvm__{
    %1 = load $Pack[T,n] * %src
    store $Pack[T,n] %1, $Pack[T,n] * %returned
    ret i32 0
}

[T,n]
overload destroy(a:Pack[T,n]) {
}



//
// makePack
//

[...A | (countArgs(...A) > 0) and AllEqual?(...A)]
inlined makePack(...args:A) {
    alias n = static countArgs(...A);
    alias T = firstArg(...A);
    return Pack[T, n](...args);
}

private AllEqual?(a, b, ...rest) = (a == b) and AllEqual?(b, ...rest);
overload AllEqual?(a) = true;

private countArgs(x, ...rest) = 1 + countArgs(...rest);
overload countArgs() = 0;

private firstArg(a, ...b) = a;



//
// main
//

main() {
    var v1 = makePack(10, 20);
    var v2 = makePack(11, 21);
    var v3 = packAdd(v1, v2);
    var x = extractElement(v3, 0);
    var y = extractElement(v3, 1);
    println(x, ", ", y);
}



//
// mapValues
//

symbol mapValues = ();
overload mapValues('f, 'first, ...'rest) = 'f('first), ...mapValues('f, ...'rest);
overload mapValues('f) = ;



//
// clay types as symbols
//

symbol Bool = ();
symbol Int = ();
symbol Array['T, 'n] = ();
symbol Tuple[...'T] = ();

symbol RecordTag = ();

symbol recordType = ();
overload recordType(...'fields) = (RecordTag, fieldNames(...'fields), fieldTypes(...'fields));

symbol fieldNames = ();
overload fieldNames(...'fields) = (...mapValues(f => tupleElement(f, 0), ...'fields));

symbol fieldTypes = ();
overload fieldTypes(...'fields) = (...mapValues(f => tupleElement(f, 1), ...'fields));

symbol record? = ();
overload record?('t) =
    symbol?('t)
    and tupleWithTag?(symbolBody('t), RecordTag);

symbol tupleWithTag? = ();
overload tupleWithTag?('x, 'tag) =
    tuple?('x)
    and lesser?(0, tupleSize('x))
    and equals?(tupleElement('x, 0), 'tag);

symbol recordFieldCount = ();
overload recordFieldCount('t) | record?('t) = tupleSize(tupleElement(symbolBody('t), 1));

symbol recordFieldNames = ();
overload recordFieldNames('t) | record?('t) = tupleElement(symbolBody('t), 1);

symbol recordFieldTypes = ();
overload recordFieldTypes('t) | record?('t) = tupleElement(symbolBody('t), 2);


symbol Point['T] = recordType(
    ("x", 'T),
    ("y", 'T),
);

symbol recordInfo = ();
overload recordInfo('t) | record?('t) = ('t, recordFieldNames('t), recordFieldTypes('t));

symbol main = ();
overload main() = recordInfo(Point[Int]);

Program -> TopLevelItem* ;

TopLevelItem -> RecordDef | VariableDef | ProcedureDef ;

RecordDef -> "record" Name TypeVars? "{" comma_list(FieldDef) "}" ;
FieldDef -> Name ":" Expression ;

TypeVars -> "[" comma_list(Name) "]" ;

VariableDef -> "var" Name "=" Expression ";" ;

ProcedureDef -> "def" Name "(" comma_list(Name)? ")" Block ;

Block -> "{" Statement+ "}" ;

Statement -> Block
           | LocalVariableDef
           | Assignment
           | IfStatement
           | ReturnStatement
           | Expression
           ;

LocalVariableDef -> "var" Name "=" Expression ";" ;

Assignment -> Name "=" Expression ";" ;

IfStatement -> "if" "(" Expression ")" Statement
               ("else" Statement)? ;

ReturnStatement -> "return" Expression ";" ;

Expression -> SuffixExpr ;

SuffixExpr -> AtomicExpr Suffix* ;
Suffix -> IndexSuffix
        | CallSuffix
        | FieldRefSuffix
        ;
IndexSuffix -> "[" comma_list(Expression) "]" ;
CallSuffix -> "(" comma_list(Expression) ")" ;
FieldRefSuffix -> "." Name ;

AtomicExpr -> ArrayExpr
            | TupleExpr
            | NameRef
            | Literal
            ;

ArrayExpr -> "[" comma_list(Expression) "]" ;
TupleExpr -> "(" comma_list(Expression) ")" ;

NameRef -> Name ;

Literal -> BoolLiteral
         | IntLiteral
         | CharLiteral
         | StringLiteral
         ;

BoolLiteral -> "true" | "false" ;

Module -> Import* TopLevelItem*

Import -> "import" DottedName ("as" Identifier)? ";"
        | "import" DottedName "." "*" ";"
        | "import" DottedName "." "(" comma_list(ImportedItem) ")" ";"

DottedName -> Identifier ("." Identifier)*

ImportedItem -> Identifier ("as" Identifier)?


TopLevelItem -> Record | Procedure | Overloadable | Overload
              | Enumeration | GlobalVariable
              | ExternalProcedure | ExternalVariable
              | StaticGlobal

Record -> "record" Identifier PatternVars? "{" RecordField* "}"
PatternVars -> "[" comma_list(Identifier) "]"
RecordField -> Identifier TypeSpec ";"
TypeSpec -> ":" Pattern

Procedure -> PatternVarsWithCond? "inlined"? Identifier Arguments
             ReturnSpec? Body

PatternVarsWithCond -> "[" comma_list(Identifier) Predicate? "]"
Predicate -> "|" Expression

Arguments -> "(" comma_list(Argument) ("," "...")? ")"
           | "(" "..." ")"
           | "(" ")"

Argument -> ValueArgument
          | StaticArgument

ValueArgument -> ("rvalue" | "lvalue")? Identifier TypeSpec?
StaticArgument -> "static" Expression

ReturnSpec -> comma_list(ReturnSpecOne)
ReturnSpecOne -> "ref" Type
               | (Identifier ":")? Type

Type -> Expression

Body -> "=" ReturnExpression ";"
      | Block

Overloadable -> "overloadable" Identifier ";"

Overload -> PatternVarsWithCond? "inlined"? "overload" Pattern Arguments
            ReturnSpec? Body

Enumeration -> "enum" Identifier "{" comma_list(Identifier) "}"

GlobalVariable -> "var" Identifier "=" Expression ";"

ExternalProcedure -> "external" Identifier "(" ExternalArgs ")"
                     Type? (ExternalBody | ";")
ExternalArgs -> comma_list(ExternalArg) ("," "...")?
              | ("...")?
ExternalArg -> Identifier TypeSpec
ExternalBody -> "=" Expression ";"
              | Block

ExternalVariable -> "external" Identifier TypeSpec ";"

StaticGlobal -> "static" Identifier "=" Expression ";"


//
// Statement
//

Block -> "{" Statement+ "}"

Statement -> Block
           | LabelDef
           | VarBinding
           | RefBinding
           | StaticBinding
           | Assignment
           | InitAssignment
           | UpdateAssignment
           | IfStatement
           | GotoStatement
           | ReturnStatement
           | ExpressionStatement
           | WhileStatement
           | BreakStatement
           | ContinueStatement
           | ForStatement

LabelDef -> Identifier ":"

VarBinding -> "var" comma_list(Identifier) "=" Expression ";"
RefBinding -> "ref" comma_list(Identifier) "=" Expression ";"
StaticBinding -> "static" Identifier "=" Expression ";"

Assignment -> Expression "=" Expression ";"

InitAssignment -> Expression "<--" Expression ";"

UpdateAssignment -> Expression UpdateOp Expression ";"
UpdateOp -> "+=" | "-=" | "*=" | "/=" | "%="

IfStatement -> "if" "(" Expression ")" Statement
               ("else" Statement)?

GotoStatement -> "goto" Identifier ";"

ReturnStatement -> "return" ReturnExpression? ";"
ReturnExpression -> comma_list("ref"? Expression)

ExpressionStatement -> Expression ";"

WhileStatement -> "while" "(" Expression ")" Statement
BreakStatement -> "break" ";"
ContinueStatement -> "continue" ";"

ForStatement -> "for" "(" Identifier "in" Expression ")" Statement


//
// Pattern
//

Pattern -> AtomicPattern PatternSuffix?
AtomicPattern -> IntLiteral
               | NameRef
PatternSuffix -> "[" comma_list(Pattern) "]"


//
// Expression
//

Expression -> OrExpr
            | Lambda
            | VarArgsRef
            | New
            | StaticExpr

OrExpr -> AndExpr ("or" AndExpr)*
AndExpr -> NotExpr ("and" NotExpr)*
NotExpr -> "not"? CompareExpr

CompareExpr -> AddSubExpr (CompareOp AddSubExpr)?
CompareOp -> "==" | "!=" | "<" | "<=" | ">" | ">="

AddSubExpr -> MulDivExpr (("+" | "-") MulDivExpr)*
MulDivExpr -> PrefixExpr (("*" | "/" | "%") PrefixExpr)*

PrefixExpr -> SignExpr
            | AddressOfExpr
            | SuffixExpr

SignExpr -> ("+" | "-") SuffixExpr
AddressOfExpr -> "&" SuffixExpr
SuffixExpr -> AtomicExpr Suffix*
Suffix -> IndexSuffix
        | CallSuffix
        | FieldRefSuffix
        | TupleRefSuffix
        | DereferenceSuffix

IndexSuffix -> "[" comma_list(Expression) "]"
CallSuffix -> "(" comma_list(Expression)? ")"
FieldRefSuffix -> "." Identifier
TupleRefSuffix -> "." IntLiteral
DereferenceSuffix -> "^"

AtomicExpr -> ArrayExpr
            | TupleExpr
            | NameRef
            | Literal

ArrayExpr -> "[" comma_list(Expression) "]"
TupleExpr -> "(" comma_list(Expression)? ")"

NameRef -> DottedName

Literal -> BoolLiteral
         | IntLiteral
         | CharLiteral
         | StringLiteral

BoolLiteral -> "true" | "false"

Lambda -> ("lambda" | "block") "(" comma_list(Identifier) ")" Block

VarArgsRef -> "..."

New -> "new" Expression

StaticExpr -> "static" Expression

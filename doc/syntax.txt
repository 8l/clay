Program -> TopLevelItem* ;

TopLevelItem -> PredicateDef | InstanceDef | RecordDef
              | VariableDef | ProcedureDef | OverloadableDef | OverloadDef ;

PredicateDef -> "predicate" Name ;
InstanceDef -> "instance" Name TypeVars? "(" comma_list(TypeExpr) ")"
               TypeConditions? ";" ;

TypeVars -> "[" comma_list(Name) "]" ;
TypeConditions -> "if" comma_list(TypeCondition) ;
TypeCondition -> Name "(" comma_list(TypeExpr) ")" ;

RecordDef -> "record" Name TypeVars? "{" FieldDef+ "}" ;
FieldDef -> Name TypeSpec ";" ;

TypeSpec -> ":" TypeExpr ;

Variable -> Name TypeSpec? ;
VariableList -> comma_list(Variable) ;

VariableDef -> "var" VariableList "=" comma_list(Expression) ";" ;

ProcedureDef -> "def" Name TypeVars? "(" comma_list(Argument)? ")"
                TypeSpec? TypeConditions? Block ;
Argument -> Variable
          | "type" Expression
          ;

OverloadableDef -> "overloadable" Name ";" ;

OverloadDef -> "overload" Name TypeVars? "(" comma_list(Argument)? ")"
               TypeSpec? TypeConditions? Block ;

Block -> "{" Statement+ "}" ;

Statement -> Block
           | LocalVariableDef
           | Assignment
           | IfStatement
           | BreakStatement
           | ContinueStatement
           | WhileStatement
           | ForStatement
           | ReturnStatement
           | Expression
           ;

LocalVariableDef -> "var" VariableList "=" comma_list(Expression) ";" ;

Assignment -> comma_list(Expression) "=" comma_list(Expression) ";" ;

IfStatement -> "if" "(" Expression ")" Statement
               ("else" Statement)? ;

BreakStatement -> "break" ";" ;
ContinueStatement -> "continue" ";" ;

WhileStatement -> "while" "(" Expression ")" Statement ;
ForStatement -> "for" "(" "var" VariableList "in" Expression ")"
                Statement ;

ReturnStatement -> "return" comma_list(Expression)? ";" ;

TypeExpr -> Expression ;

Expression -> PrefixExpr ;

PrefixExpr -> AddressOfExpr
            | SuffixExpr
            ;
AddressOfExpr -> "&" SuffixExpr ;
SuffixExpr -> AtomicExpr Suffix* ;
Suffix -> IndexSuffix
        | CallSuffix
        | FieldRefSuffix
        | TupleRefSuffix
        | DereferenceSuffix
        ;
IndexSuffix -> "[" comma_list(Expression) "]" ;
CallSuffix -> "(" comma_list(Expression) ")" ;
FieldRefSuffix -> "." Name ;
TupleRefSuffix -> "." IntLiteral ;
DereferenceSuffix -> "^" ;

AtomicExpr -> ArrayExpr
            | TupleExpr
            | NameRef
            | Literal
            ;

ArrayExpr -> "[" comma_list(Expression) "]" ;
TupleExpr -> "(" comma_list(Expression) ")" ;

NameRef -> Name ;

Literal -> BoolLiteral
         | IntLiteral
         | CharLiteral
         | StringLiteral
         ;

BoolLiteral -> "true" | "false" ;

Program -> TopLevelItem* ;

TopLevelItem -> RecordDef | VariableDef | ProcedureDef ;

RecordDef -> "record" Name TypeVars? "{" comma_list(FieldDef) "}" ;
FieldDef -> Name ":" TypeExpr ;

TypeVars -> "[" comma_list(Name) "]" ;

VariableDef -> "var" Name ":" TypeExpr "=" Expression ";" ;

ProcedureDef -> "def" Name TypeVars? "(" comma_list(Argument)? ")"
                ":" TypeExpr Block ;
Argument -> Name ":" TypeExpr ;

Block -> "{" Statement+ "}" ;

Statement -> Block
           | LocalVariableDef
           | Assignment
           | IfStatement
           | ReturnStatement
           | Expression
           ;

LocalVariableDef -> "var" Name ":" TypeExpr "=" Expression ";" ;

Assignment -> Name "=" Expression ";" ;

IfStatement -> "if" "(" Expression ")" Statement
               ("else" Statement)? ;

ReturnStatement -> "return" Expression? ";" ;

TypeExpr -> Expression ;

Expression -> SuffixExpr ;

SuffixExpr -> AtomicExpr Suffix* ;
Suffix -> IndexSuffix
        | CallSuffix
        | FieldRefSuffix
        ;
IndexSuffix -> "[" comma_list(Expression) "]" ;
CallSuffix -> "(" comma_list(Expression) ")" ;
FieldRefSuffix -> "." Name ;

AtomicExpr -> ArrayExpr
            | TupleExpr
            | NameRef
            | Literal
            ;

ArrayExpr -> "[" comma_list(Expression) "]" ;
TupleExpr -> "(" comma_list(Expression) ")" ;

NameRef -> Name ;

Literal -> BoolLiteral
         | IntLiteral
         | CharLiteral
         | StringLiteral
         ;

BoolLiteral -> "true" | "false" ;

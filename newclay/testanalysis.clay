
import newclay.common.*;
import newclay.lexer;
import newclay.parser;
import newclay.ast as ast;
import newclay.core.*;
import newclay.loader.*;
import newclay.hooks.*;
import newclay.analysis.*;
import newclay.analysis as analysis;
import maybe.*;

[T]
LeafTypeName(x:T) = StaticName(T);
[V | Variant?(V)]
overload LeafTypeName(x:V) = LeafTypeName(*x);

fakeSpecialization(env) {
    var s = allocateRef(analysis.Specialization);
    s.env = env;
    return s;
}

printDepth(depth) {
    for (i in range(depth))
        print("  ");
}

printNode(depth, node) {
    printDepth(depth);
    print(LeafTypeName(node), " ");
    printNodeData(depth, *node);
}

asPtr(x) = referencePtr(x).ptr;

procedure printNodeData;

overload printNodeData(depth, node:SpecializedCCodeBody) {
    println();
}

overload printNodeData(depth, node:SpecializedCall) {
    println(LeafTypeName(node.ast),
        " specialization ", asPtr(node.specialization),
        " ", node.specialization.callable
    );
    printInputsOutputs(depth+1, node.inputs, node.outputs);
}

overload printNodeData(depth, node:SpecializedIf) {
    println(LeafTypeName(node.ast));
    printDepth(depth+1);
    print("condition ");
    printVar(node.condition);
    printDepth(depth+1);
    print("then:");
    for (thenNode in node.thenBody)
        printNode(depth+2, thenNode);
    printDepth(depth+1);
    print("else:");
    for (elseNode in node.elseBody)
        printNode(depth+2, elseNode);
}

overload printNodeData(depth, node:SpecializedValueList) {
    println(LeafTypeName(node.ast), " ", node.valueList);
    printVars(depth+1, "out ", node.outputs);
}

overload printNodeData(depth, node:SpecializedPhi) {
    println();
    for (input in node.inputs) {
        printDepth(depth+1);
        print("in  ");
        printVar(input);
    }
    printDepth(depth+1);
    print("out ");
    printVar(node.output);
}

overload printNodeData(depth, node:SpecializedReturn) {
    println(LeafTypeName(node.ast));

    printDepth(depth+1);
    print("in  ");
    printVar(node.input);

    printDepth(depth+1);
    print("out ");
    printVar(node.output);
}

printVar(variable) {
    println(asPtr(variable), " ", variable);
}

printVars(depth, prefix, vars) {
    for (v in vars) {
        printDepth(depth);
        print(prefix);
        printVar(v);
    }
}

printInputsOutputs(depth, inputs:VariableList, mOutputs:Maybe[VariableList]) {
    printVars(depth, "in  ", inputs);
    printOutputs(depth, mOutputs);
}

printOutputs(depth, mOutputs:Maybe[VariableList]) {
    maybe(mOutputs,
        outputs ref=> {
            printVars(depth, "out ", outputs);
        },
        () ref=> { println("nonterminating"); }
    );
}

main(args) {
    if (size(args) != 2) {
        println("usage: ", args[0], " <file>");
        return -1;
    }
    try {
        var program = loadProgram(args[1]);
        withProgram(program, () ref=> {
            initializeClayHooks();

            var s = analyzeMain();

            println("results:");
            printOutputs(1, s.results);
            println("internal args:");
            printVars(1, "in  ", s.internalArgs);
            println("internal named returns:");
            printVars(1, "out ", s.internalNamedResults);
            println("analysis nodes:");
            for (node in s.body) {
                printNode(1, node);
            }
        });
    }
    catch (e:ClayError) {
        displayError(e);
    }
    return 0;
}

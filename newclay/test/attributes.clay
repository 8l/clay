

//
// mapValues
//

symbol mapValues = ();
overload mapValues('f, 'first, ...'rest) = 'f('first), ...mapValues('f, ...'rest);
overload mapValues('f) = ;



//
// clay types as symbols
//

symbol Bool = ();
symbol Int = ();
symbol Array['T, 'n] = ();
symbol Tuple[...'T] = ();

symbol String = ();


symbol tupleWithTag? = ();
overload tupleWithTag?('x, 'tag) =
    tuple?('x)
    and lesser?(0, tupleSize('x))
    and equals?(tupleElement('x, 0), 'tag);



//
// record types
//

symbol RecordTag = ();

symbol recordType = ();
overload recordType(...'fields) = (RecordTag, fieldNames(...'fields), fieldTypes(...'fields));

symbol fieldNames = ();
overload fieldNames(...'fields) = (...mapValues(f => tupleElement(f, 0), ...'fields));

symbol fieldTypes = ();
overload fieldTypes(...'fields) = (...mapValues(f => tupleElement(f, 1), ...'fields));

symbol record? = ();
overload record?('t) =
    symbol?('t)
    and tupleWithTag?(symbolBody('t), RecordTag);

symbol recordFieldCount = ();
overload recordFieldCount('t) | record?('t) = tupleSize(tupleElement(symbolBody('t), 1));

symbol recordFieldNames = ();
overload recordFieldNames('t) | record?('t) = tupleElement(symbolBody('t), 1);

symbol recordFieldTypes = ();
overload recordFieldTypes('t) | record?('t) = tupleElement(symbolBody('t), 2);



//
// variant types
//

symbol VariantTag = ();

symbol variantType = ();
overload variantType(...'members) = (VariantTag, (...'members));

symbol variant? = ();
overload variant?('t) = 
    symbol?('t)
    and tupleWithTag?(symbolBody('t), VariantTag);

symbol variantMembers = ();
overload variantMembers('t) | variant?('t) = (
    ...tupleElements(tupleElement(symbolBody('t), 1)),
    ...tupleElements(symbolAttributes('t)),
);



//
// testing
//

symbol GenericError = recordType(
    ("msg", String),
);

symbol Exception = variantType(GenericError);

symbol AssertionError = recordType(
    ("condition", String),
    ("msg", String),
);

attribute Exception = AssertionError;

symbol main = ();
overload main() = (
    variant?(String),
    variant?(Exception),
    variantMembers(Exception)
);

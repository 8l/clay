

//
// mapValues
//

symbol mapValues = ();
static overload mapValues('f, 'first, ...'rest) = 'f('first), ...mapValues('f, ...'rest);
static overload mapValues('f) = ;



//
// clay types as symbols
//

symbol Bool = ();
symbol Int = ();
symbol Array['T, 'n] = ();
symbol Tuple[...'T] = ();

symbol RecordTag = ();

symbol recordType = ();
static overload recordType(...'fields) = (RecordTag, fieldNames(...'fields), fieldTypes(...'fields));

symbol fieldNames = ();
static overload fieldNames(...'fields) = (...mapValues(f => StaticTupleElement(f, 0), ...'fields));

symbol fieldTypes = ();
static overload fieldTypes(...'fields) = (...mapValues(f => StaticTupleElement(f, 1), ...'fields));

symbol record? = ();
static overload record?('t) =
    StaticSymbol?('t)
    and tupleWithTag?(StaticSymbolBody('t), RecordTag);

symbol tupleWithTag? = ();
static overload tupleWithTag?('x, 'tag) =
    StaticTuple?('x)
    and StaticLesser?(0, StaticTupleSize('x))
    and StaticEquals?(StaticTupleElement('x, 0), 'tag);

symbol recordFieldCount = ();
static overload recordFieldCount('t) | record?('t) =
    StaticTupleSize(StaticTupleElement(StaticSymbolBody('t), 1));

symbol recordFieldNames = ();
static overload recordFieldNames('t) | record?('t) =
    StaticTupleElement(StaticSymbolBody('t), 1);

symbol recordFieldTypes = ();
static overload recordFieldTypes('t) | record?('t) =
    StaticTupleElement(StaticSymbolBody('t), 2);


symbol Point['T] = recordType(
    ("x", 'T),
    ("y", 'T),
);

symbol recordInfo = ();
static overload recordInfo('t) | record?('t) =
    ('t, recordFieldNames('t), recordFieldTypes('t));

symbol main = ();
static overload main() = recordInfo(Point[Int]);

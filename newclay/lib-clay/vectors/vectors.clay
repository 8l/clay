import unsafe.memory.*;
import values.*;
import hashing.(hash, hashSequence);
import sequences.(equalSequence?, lesserSequence?);
import containers.*;
import unsafe.compositetypes.*;
import unsafe.coordinates.*;
import unsafe.valuesemantics.*;

symbol Vector['T] = CompositeType(
    size:UInt,
    capacity:UInt,
    data:ContiguousCoordinate['T],
);

overload #ContainerType?(Vector['T]) = true;


//
// constructors
//

overload Vector['T]() = initializeRecord(Vector['T],
    0u,
    0u,
    ContiguousCoordinate['T](),
);

overload #InitializeDoesNotThrowType?(Vector['T]) = true;
overload #BitwiseZeroInitializedType?(Vector['T]) = true;

overload Vector['T](forward s:'S) inline --> returned:Vector['T]
    | SequenceType?('S) and sequenceElementType('S) == 'T
{
    returned <-- Vector['T]();
    // XXX lambdas
    try {
        push(returned, s);
    } catch (ex) {
        destroy(returned);
        throw ex;
    }
}

overload Vector(forward s:'S) inline
    | SequenceType?('S)
    = Vector[sequenceElementType('S)](s);

overload Vector['T](forward ..elements:'TT) inline --> returned:Vector['T]
    | not emptyValues?(..'TT) and equalValues?('T, ..'TT)
{
    returned <-- Vector['T]();
    try {
        push(returned, ..elements);
    } catch (ex) {
        destroy(returned);
        throw ex;
    }
}


//
// value semantics
//

overload copyAssign(ref to:Vector['T], from:Vector['U]) inline
    | AssignableTypes?('T, 'U)
{
    resizeUnsafe(to, size(from));
    assignRange(begin(to), begin(from), end(from));
}

overload destroy(ref a:Vector['T]) inline {
    destroyRange(begin(a), end(a));
    freeRawValues(a.data);
}

overload copy(a:Vector['T]) inline = Vector['T](a);


//
// sequence operations
//

overload size(v:Vector['T]) inline = v.size;
overload index(/*not rvalue*/ forward v:Vector['T], i:'I) inline
    | IntegerType?('I)
    = forward v.data[i];

overload index(rvalue v:Vector['T], i:'I) inline {
    var w = v;
    return w.data[i];
}

overload begin(v:Vector['T]) inline = v.data;
overload end(v:Vector['T]) inline = v.data + v.size;


//
// vector mutation operations
//

overload push(ref v:Vector['T], forward ..elements:'TT) inline
    | countValues(..'TT) != 1
      and allValues?([U -> callDefined?(push, Ref[Vector['T]], U)], ..'TT)
{
    reserve(v, countValues(..'TT));
    static for (forward e in ..elements)
        push(v, e);
}

overload push(ref v:Vector['T], forward seq:'S) inline
    | SequenceType?('S) and sequenceElementType('S) == 'T
{
    insert(v, size(v), seq);
}

overload push(ref v:Vector['T], forward elt:'T) inline {
    insert(v, size(v), elt);
}

overload pop(ref v:Vector['T]) inline --> returned:'T {
    returned <-- moveRvalue(back(v));
    removeLocations(v, end(v) - 1, end(v));
}

overload reserve(ref v:Vector['T], n:'I) inline | IntegerType?('I) {
    if (v.capacity < UInt(n))
        requestCapacity(v, UInt(n));
}

overload resize(ref v:Vector['T], n:'I) inline | IntegerType?('I) {
    resizeInternal(v, UInt(n), initializeRange);
}

overload resizeUnsafe(ref v:Vector['T], n:'I) inline | IntegerType?('I) {
    resizeInternal(v, UInt(n), resetRange);
}

// insert

overload insert(ref v:Vector['T], i:'I, seq:'S)
    | IntegerType?('I) and SequenceType?('S) and sequenceElementType('S) == 'T
{
    var pos = UInt(i);
    var iter = iterator(seq);
    while (hasNext?(iter)) {
        insert(v, pos, next(iter));
        pos += 1u;
    }
}

overload insert(ref v:Vector['T], i:'I, seq:'S)
    | IntegerType?('I) and SizedSequenceType?('S) and sequenceElementType('S) == 'T
{
    var first, last = ..insertLocations(v, UInt(i), size(seq));
    try {
        var iter = iterator(seq);
        while (hasNext?(iter)) {
            first^ <-- next(iter);
            inc(first);
        }
    } catch (ex) {
        resetRange(first, last);
        throw ex;
    }
}

// XXX insert(rvalue sequence) optimizations

overload insert(ref v:Vector['T], i:'I, x:'T) | IntegerType?('I) {
    var first, last = ..insertLocations(v, UInt(i), 1u);
    try {
        first^ <-- x;
    } catch (ex) {
        reset(first^);
        throw ex;
    }
}

overload insert(ref v:Vector['T], i:'I, rvalue x:'T) | IntegerType?('I) {
    var first, last = ..insertLocations(v, UInt(i), 1u);
    first^ <-- x;
}

overload insert(ref v:Vector['T], i:ContiguousCoordinate['T], forward seq:'S)
    | SequenceType?('S) and sequenceElementType('S) == 'T
{
    insert(v, i - begin(v), seq);
}

overload insert(ref v:Vector['T], i:ContiguousCoordinate['T], x:'T) {
    insert(v, i - begin(v), x);
}

// remove

overload clear(ref v:Vector['T]) inline {
    remove(v, begin(v), end(v));
}

overload remove(
    ref v:Vector['T],
    i:ContiguousCoordinate['T],
    j:ContiguousCoordinate['T]
) inline {
    destroyRange(i, j);
    removeLocations(v, i, j);
}

overload remove(ref v:Vector['T], i:ContiguousCoordinate['T]) inline {
    remove(v, i, i+1);
}

overload remove(ref v:Vector['T], i:'I) inline | IntegerType?('I) {
    remove(v, begin(v) + i);
}

overload remove(ref v:Vector['T], i:'I, j:'J) inline
    | IntegerType?('I) and IntegerType?('J)
{
    remove(v, begin(v) + i, begin(v) + j);
}


//
// internal operations
//

private requestCapacity(ref v:Vector['T], capacity:UInt) {
    assert([capacity >= v.size]);
    if (capacity < v.capacity)
        return;
    var n = max(capacity, 16u);
    n = max(n, 2u*v.capacity);
    reallocVector(v, n);
}

private reallocVector(ref v:Vector['T], capacity:UInt) {
    var data = allocateRawValues('T, capacity);
    moveRvalueNonoverlappingRange(data, v.data, v.data + v.size);
    freeRawValues(v.data);
    v.data = data;
    v.capacity = capacity;
}
overload reallocVector(ref v:Vector['T], capacity:UInt) | BitwiseMovedType?('T) {
    v.data = reallocateRawValues(v.data, capacity);
    v.capacity = capacity;
}

private resizeInternal(ref v:Vector['T], n:UInt, initializer) {
    if (v.size < n) {
        reserve(v, n);
        initializer(end(v), begin(v) + n);
    } else if (v.size > n) {
        destroyRange(begin(v) + n, end(v));
    }
    v.size = n;
}

private ensureSpace(ref v, space) inline { reserve(v, v.size + space); }

private insertLocations(ref v:Vector['T], pos:UInt, n:UInt) {
    ensureSpace(v, n);
    var first = begin(v) + pos;
    moveRvalueRange(first + n, first, end(v));
    v.size += n;
    return first, first+n;
}

private removeLocations(
    ref v:Vector['T],
    from:ContiguousCoordinate['T],
    to:ContiguousCoordinate['T]
) {
    var n = end(v) - to;
    moveRvalueRange(from, to, end(v));
    v.size -= UInt(to - from);
}


//
// equals?, lesser?, hashing
//

overload equals?(a:Vector['A], b:Vector['B]) inline | callDefined?(equals?, 'A, 'B)
    = equalSequence?(a, b);

overload lesser?(a:Vector['A], b:Vector['B]) inline | callDefined?(lesser?, 'A, 'B)
    = lesserSequence?(a, b);

overload hash(v:Vector['V]) = hashSequence(v);

import memory.(allocateRawValue, freeRawValue);

symbol UniquePointer['T] = CompositeType(ptr:MPointer['T]);

alias MUniquePointer['T] = Maybe[UniquePointer['T]];

overload UniquePointer['T](f) inline
    | Type([f()]) == 'T
{
    var ptr = allocateRawValue('T);
    try {
        ptr^ <-- f();
    } catch (ex) {
        freeRawValue(ptr);
        throw ex;
    }
    return initializeRecord(UniquePointer['T], just(ptr));
}

overload UniquePointer(f) inline
    | Defined?([var x = f();])
    = UniquePointer[Type([f()])](f);

overload destroyUnsafe(ref p:UniquePointer['T]) inline {
    maybe(p.ptr, [ptr -> destroyUnsafe(ptr^); freeRawValue(ptr);]);
}

overload resetUnsafe(ref p:UniquePointer['T]) inline {
    p.ptr <-- nothing(Pointer['T]);
}

overload dereference(p:UniquePointer['T]) inline = forward requiredUnsafe(p.ptr)^;

overload equals?(p:UniquePointer['T], q:UniquePointer['T])
    = p.ptr == q.ptr;

import __primitives__.(CallDefinedForSymbol?, makeCodePointer, callCodePointer);
import meta.symbols.(SymbolWithTag?, Symbol?, private SymbolBodyWithTag?);
import values.(mapValues);

public import __primitives__.(StaticCallDefined?);

symbol CallDefined?;
overload #CallDefined?('fn, ..'T) = CallDefinedForSymbol?('fn, ..'T);
overload CallDefined?(/*alias*/ fn:'F, ..#'T) = *#CallDefinedForSymbol?(call, 'F, ..'T);
overload CallDefined?(#'fn, ..#'T) = *#CallDefinedForSymbol?('fn, ..'T);

#CallDefinedForType?('F, ..'T) = CallDefinedForSymbol?(call, 'F, ..'T);


//
// procedure tags
//

// VariadicBinaryOperatorProcedure -- define variadic version of procedure
// as left fold of the binary procedure
#VariadicBinaryOperatorProcedure() = (VariadicBinaryOperatorProcedure,);

overload 'fn(forward x:'X, forward y:'Y, forward z:'Z, forward ..w:'W) inline
    | VariadicBinaryOperatorProcedure?('fn)
      and CallDefined?('fn, 'X, 'Y)
      and CallDefined?('fn, Type(['fn(x, y)]), 'Z, ..'W)
    = forward 'fn('fn(x, y), z, ..w);

overload 'fn(forward x) inline
    | VariadicBinaryOperatorProcedure?('fn)
    = forward x;

overload #'fn('x, 'y, 'z, ..'w)
    | VariadicBinaryOperatorProcedure?('fn)
      and StaticCallDefined?('fn, 'x, 'y)
    = 'fn('fn('x, 'y), 'z, ..'w);

overload #'fn('x)
    | VariadicBinaryOperatorProcedure?('fn)
    = 'x;

#VariadicBinaryOperatorProcedure?('fn)
    = SymbolWithTag?('fn, VariadicBinaryOperatorProcedure);


// RuntimeStaticProcedure -- make a static function callable as a runtime procedure

#RuntimeStaticProcedure() = (RuntimeStaticProcedure,);
#RuntimeStaticProcedure?('fn) = SymbolWithTag?('fn, RuntimeStaticProcedure);
overload 'fn(..#'xs) inline
    | RuntimeStaticProcedure?('fn)
      and StaticCallDefined?('fn, ..'xs)
    = ..unpack(..#'fn(..'xs));


//
// code pointers
//

overload CodePointer[(..'I), (..'O)](#'f) inline
    | DefinedWithType?([forward ..'f(..arg(..'I))], ..'O)
    = makeCodePointer(#'f, ..'I);

overload call(c:CodePointer[(..'I), (..'O)], forward ..args) inline
    | Defined?([callCodePointer(c, ..args);])
    = forward ..callCodePointer(c, ..args);


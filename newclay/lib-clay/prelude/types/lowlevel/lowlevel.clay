import meta.platform.*;
import values.*;

symbol LLSigned;
symbol LLUnsigned;

symbol LLBoolType;
symbol LLIntegerType['Signed, 'Bits];
symbol LLFloatType['Bits];
symbol LLPointerType['T];
symbol LLCodePointerType['ABI, 'Variadic?, 'Inputs, 'Outputs];
symbol LLArrayType['T, 'n];
symbol LLTupleType[...'T];
symbol LLUnionType[...'T];

symbol ToLowLevelType;

symbol LLTypeSize;
overload #LLTypeSize(LLBoolType) = 1;
overload #LLTypeSize(LLIntegerType['S, 'Bits]) = 'Bits div 8;
overload #LLTypeSize(LLFloatType['Bits]) = 'Bits div 8;
overload #LLTypeSize(LLPointerType['T]) = PlatformCPUPointerSize();
overload #LLTypeSize(LLCodePointerType[...'x]) = PlatformCPUPointerSize();
overload #LLTypeSize(LLArrayType['T, 'n]) = LLTypeSize('T) * 'n;
overload #LLTypeSize(LLTupleType[...'T]) = align(LLTupleSize(0, ...'T), LLAggAlign(...'T));
overload #LLTypeSize(LLUnionType[...'T]) = align(LLUnionSize(0, ...'T), LLAggAlign(...'T));

private #align('n, 'alignment) = ('n + 'alignment - 1) div 'alignment * 'alignment;

private symbol LLTupleSize;
overload #LLTupleSize('size, 'T, ...'TT)
    = LLTupleSize(align('size, TypeAlign('T)) + TypeSize('T), ...'TT);
overload #LLTupleSize('size) = 'size;

private symbol LLUnionSize;
overload #LLUnionSize('size, 'T, ...'TT)
    = LLUnionSize(max(align('size, TypeAlign('T)), TypeSize('T)), ...'TT);
overload #LLUnionSize('size) = 'size;

private #LLAggAlign(...'T) = max(...mapValues(TypeAlign, ...'T));

// XXX platform-specific
#MaxIntegerAlign() = 8;
#MaxFloatAlign() = 8;

symbol LLTypeAlign;
overload #LLTypeAlign(LLBoolType) = 1;
overload #LLTypeAlign(LLIntegerType['S, 'Bits]) = min('Bits div 8, MaxIntegerAlign());
overload #LLTypeAlign(LLFloatType['Bits]) = min('Bits div 8, MaxFloatAlign());
overload #LLTypeAlign(LLPointerType['T]) = PlatformCPUPointerSize();
overload #LLTypeAlign(LLCodePointerType[...'x]) = PlatformCPUPointerSize();
overload #LLTypeAlign(LLArrayType['T, 'n]) = TypeAlign('T);
overload #LLTypeAlign(LLTupleSize[...'T]) = max(...mapValues(TypeAlign, ...'T));
overload #LLTypeAlign(LLUnionSize[...'T]) = max(...mapValues(TypeAlign, ...'T));


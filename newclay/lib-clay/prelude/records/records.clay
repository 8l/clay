import meta.statics.*;
import meta.symbols.*;
import libc.*;
import values.*;
import prelude.operators.c.*;
import prelude.types.lowlevel.*;
import prelude.tuples.(private unpackFields);


//
// CompositeType definer
//

#CompositeType(..'fields) | ValidRecordFields?(..'fields) = (CompositeType, ..'fields);

#CompositeType?('T) = SymbolWithTag?('T, CompositeType) or SymbolWithTag?('T, RecordType);


//
// RecordType definer
//

#RecordType(..'fields) | ValidRecordFields?(..'fields) = (RecordType, ..'fields);

#RecordType?('T) = SymbolWithTag?('T, RecordType);

#ValidRecordField?('x) = false;
overload #ValidRecordField?(('name, 'T)) | StaticString?('name) and Type?('T) = true;

#ValidRecordFields?(..'x) = allValues?(ValidRecordField?, ..'x);


//
// record type metadata
//

symbol RecordFields;
overload #RecordFields('T)
    | CompositeType?('T) and not RecordType?('T)
    = ..SymbolTagBody('T, CompositeType);
overload #RecordFields('T)
    | RecordType?('T)
    = ..SymbolTagBody('T, RecordType);

#RecordFieldNames('T) | CompositeType?('T)
    = ..mapValues(x -> x.0, ..RecordFields('T));

#RecordFieldTypes('T) | CompositeType?('T)
    = ..mapValues(x -> x.1, ..RecordFields('T));

overload #LLType('T) | CompositeType?('T) = LLTupleType[..RecordFields('T)];

#RecordField?('T, 'field) = inValues?('field, ..RecordFieldNames('T));

#RecordFieldType('T, 'field) = RecordFieldType2('field, ..RecordFields('T));
private symbol RecordFieldType2;
overload #RecordFieldType2('field, 'x, ..'fields) = RecordFieldType2('field, ..'fields);
overload #RecordFieldType2('field, ('field, 'T), ..'fields) = 'T;

overload #InheritsPredicatesFromTypes('T) | RecordType?('T) = ..RecordFieldTypes('T);


//
// unpack
//

inline unpackRecord(forward x:'T) | CompositeType?('T)
    = forward ..unpackFields(x, ..#RecordFieldNames('T));

inline overload unpack(forward x:'T) | RecordType?('T) = forward ..unpackRecord(x);


//
// record indexing
//

inline overload index(forward x:'T, #'field)
    | CompositeType?('T) and RecordField?('T, 'field)
    = forward cFieldRef(#RecordFieldType('T, 'field), x, #LLMangle('field));

inline overload index(rvalue x:'T, #'field)
    rvalue returned:RecordFieldType('T, 'field)
    | CompositeType?('T) and RecordField?('T, 'field)
{
    ref rx = rvalueToRefUnsafe(x);
    returned = &cFieldRef(#RecordFieldType('T, 'field), rx, #LLMangle('field));

    static for (f in ..#exceptValue('field, ..RecordFieldNames('T)))
        destroyUnsafe(rx[f]);
}


//
// record constructors
//

symbol initializeRecord;
inline overload initializeRecord(#'T) returned:'T
    | CompositeType?('T) and allValues?(DefaultInitializableType?, ..RecordFieldTypes('T))
{
    ..*returned <-- ..mapValues(call, ..RecordFieldTypes('T));
}

inline overload initializeRecord(#'T) returned:'T
    | CompositeType?('T) and allValues?(BitwiseZeroInitializedType?, ..RecordFieldTypes('T))
{
    memset(OpaquePointer(&returned), 0_i32, TypeSize('T));
}

inline overload initializeRecord(#'T, forward ..fields:'F) returned:'T
    | CompositeType?('T) and (..'F) == (..RecordFieldTypes('T))
{
    ..*returned <-- ..fields;
}

inline overload 'T(forward ..args)
    | RecordType?('T) //and Defined?(initializeRecord('T, ..args))
    = initializeRecord('T, ..args);


//
// record value semantics
//

symbol destroyRecordUnsafe;
inline overload destroyRecordUnsafe(ref x:'T)
    | CompositeType?('T) and allValues?(NotDestroyedType?, ..RecordFieldTypes('T))
{ }
inline overload destroyRecordUnsafe(ref x:'T)
    | CompositeType?('T) and not allValues?(NotDestroyedType?, ..RecordFieldTypes('T))
{
    static for (/*ref*/ f in ..*x)
        destroyUnsafe(f);
}

inline overload destroyUnsafe(ref x:'T)
    | RecordType?('T) and not NotDestroyedType?('T)
{
    destroyRecordUnsafe(x);
}

symbol resetRecordUnsafe;
inline overload resetRecordUnsafe(ref x:'T)
    | CompositeType?('T) and allValues?(NotResetType?, ..RecordFieldTypes('T))
{ }
inline overload resetRecordUnsafe(ref x:'T)
    | CompositeType?('T) and not allValues?(NotResetType?, ..RecordFieldTypes('T))
{
    static for (/*ref*/ f in ..*x)
        resetUnsafe(f);
}

inline overload resetUnsafe(ref x:'T)
    | RecordType?('T) and not NotResetType?('T)
{
    resetRecordUnsafe(x);
}

symbol moveRecordUnsafe;
inline overload moveRecordUnsafe(forward x:'T)
    | CompositeType?('T) and allValues?(BitwiseMovedType?, ..RecordFieldTypes('T))
    = bitwiseCopyUnsafe(x);
inline overload moveRecordUnsafe(forward x:'T)
    | CompositeType?('T)
      and not allValues?(BitwiseMovedType?, ..RecordFieldTypes('T))
      and allValues?(MovableType?, ..RecordFieldTypes('T))
    = initializeRecord('T, ..mapValues(moveUnsafe, ..*x));

inline overload moveUnsafe(forward x:'T)
    | RecordType?('T)
      and not BitwiseMovedType?('T)
      and allValues?(MovableType?, ..RecordFieldTypes('T))
    = moveRecordUnsafe(x);

symbol copyRecord;
inline overload copyRecord(x:'T)
    | CompositeType?('T) and allValues?(BitwiseCopiedType?, ..RecordFieldTypes('T))
    = bitwiseCopyUnsafe(x);
inline overload copyRecord(x:'T)
    | CompositeType?('T)
      and not allValues?(BitwiseCopiedType?, ..RecordFieldTypes('T))
      and allValues?(CopyableType?, ..RecordFieldTypes('T))
    = initializeRecord('T, ..mapValues(copy, ..*x));

inline overload copy(x:'T)
    | RecordType?('T)
      and not BitwiseCopiedType?('T)
      and allValues?(CopyableType?, ..RecordFieldTypes('T))
    = copyRecord(x);

symbol copyAssignRecord;
inline overload copyAssignRecord(ref to:'T, from:'T)
    | CompositeType?('T)
      and not allValues?(BitwiseAssignedType?, ..RecordFieldTypes('T))
      and allValues?(AssignableType?, ..RecordFieldTypes('T))
{
    static for (field in ..#RecordFieldNames('T))
        to[field] = from[field];
}
inline overload copyAssignRecord(ref to:'T, from:'T)
    | CompositeType?('T)
      and allValues?(BitwiseAssignedType?, ..RecordFieldTypes('T))
{
    to <-- from;
}

inline overload copyAssign(ref to:'T, from:'T)
    | RecordType?('T)
      and not BitwiseAssignedType?('T)
      and allValues?(AssignableType?, ..RecordFieldTypes('T))
{
    copyAssignRecord(to, from);
}


//
// comparison
//

inline equalRecords?(a:'T, b:'T) | CompositeType?('T)
{
    static for (field in ..#RecordFieldNames('T))
        if (a[field] != b[field])
            return false;
    return true;
}

inline overload equals?(a:'T, b:'T) | RecordType?('T) = equalRecords?(a, b);

import meta.statics.*;
import meta.symbols.*;
import libc.*;
import values.*;
import prelude.operators.c.*;
import prelude.types.lowlevel.*;
import prelude.tuples.(private unpackFields);


//
// CompositeType definer
//

#CompositeType(..'fields) | ValidFields?(..'fields) = (CompositeType, ..'fields);

#CompositeType?('T) = SymbolWithTag?('T, CompositeType) or SymbolWithTag?('T, RecordType);


//
// RecordType definer
//

#RecordType(..'fields) | ValidFields?(..'fields) = (RecordType, ..'fields);

#RecordType?('T) = SymbolWithTag?('T, RecordType);

#ValidField?('x) = false;
overload #ValidField?(('name, 'T)) | StaticString?('name) and Type?('T) = true;

#ValidFields?(..'x) = allValues?(ValidField?, ..'x);


//
// record type metadata
//

symbol Fields;
overload #Fields('T)
    | CompositeType?('T) and not RecordType?('T)
    = ..SymbolTagBody('T, CompositeType);
overload #Fields('T)
    | RecordType?('T)
    = ..SymbolTagBody('T, RecordType);

#FieldNames('T) | CompositeType?('T)
    = ..mapValues(x -> x.0, ..Fields('T));

#FieldTypes('T) | CompositeType?('T)
    = ..mapValues(x -> x.1, ..Fields('T));

overload #LLType('T) | CompositeType?('T) = LLTupleType[..Fields('T)];

#Field?('T, 'field) = inValues?('field, ..FieldNames('T));

#FieldType('T, 'field) = FieldType2('field, ..Fields('T));
private symbol FieldType2;
overload #FieldType2('field, 'x, ..'fields) = FieldType2('field, ..'fields);
overload #FieldType2('field, ('field, 'T), ..'fields) = 'T;

overload #InheritsPredicatesFromTypes('T) | RecordType?('T) = ..FieldTypes('T);


//
// unpack
//

inline unpackRecord(forward x:'T) | CompositeType?('T)
    = forward ..unpackFields(x, ..#FieldNames('T));

inline overload unpack(forward x:'T) | RecordType?('T) = forward ..unpackRecord(x);


//
// record indexing
//

inline overload index(forward x:'T, #'field)
    | CompositeType?('T) and Field?('T, 'field)
    = forward cFieldRef(#FieldType('T, 'field), x, #LLMangle('field));

inline overload index(rvalue x:'T, #'field)
    rvalue returned:FieldType('T, 'field)
    | CompositeType?('T) and Field?('T, 'field)
{
    ref rx = rvalueToRefUnsafe(x);
    returned = &cFieldRef(#FieldType('T, 'field), rx, #LLMangle('field));

    static for (f in ..#exceptValue('field, ..FieldNames('T)))
        destroyUnsafe(rx[f]);
}


//
// record constructors
//

symbol initializeRecord;
inline overload initializeRecord(#'T) returned:'T
    | CompositeType?('T) and allValues?(DefaultInitializableType?, ..FieldTypes('T))
{
    ..*returned <-- ..mapValues(call, ..FieldTypes('T));
}

inline overload initializeRecord(#'T) returned:'T
    | CompositeType?('T) and allValues?(BitwiseZeroInitializedType?, ..FieldTypes('T))
{
    memset(OpaquePointer(&returned), 0_i32, TypeSize('T));
}

inline overload initializeRecord(#'T, forward ..fields:'F) returned:'T
    | CompositeType?('T) and (..'F) == (..FieldTypes('T))
{
    ..*returned <-- ..fields;
}

inline overload 'T(forward ..args)
    | RecordType?('T) and Defined?(initializeRecord('T, ..args))
    = initializeRecord('T, ..args);


//
// record value semantics
//

symbol destroyRecordUnsafe;
inline overload destroyRecordUnsafe(ref x:'T)
    | CompositeType?('T) and allValues?(NotDestroyedType?, ..FieldTypes('T))
{ }
inline overload destroyRecordUnsafe(ref x:'T)
    | CompositeType?('T) and not allValues?(NotDestroyedType?, ..FieldTypes('T))
{
    static for (/*ref*/ f in ..*x)
        destroyUnsafe(f);
}

inline overload destroyUnsafe(ref x:'T)
    | RecordType?('T) and not NotDestroyedType?('T)
{
    destroyRecordUnsafe(x);
}

symbol resetRecordUnsafe;
inline overload resetRecordUnsafe(ref x:'T)
    | CompositeType?('T) and allValues?(NotResetType?, ..FieldTypes('T))
{ }
inline overload resetRecordUnsafe(ref x:'T)
    | CompositeType?('T) and not allValues?(NotResetType?, ..FieldTypes('T))
{
    static for (/*ref*/ f in ..*x)
        resetUnsafe(f);
}

inline overload resetUnsafe(ref x:'T)
    | RecordType?('T) and not NotResetType?('T)
{
    resetRecordUnsafe(x);
}

symbol moveRecordUnsafe;
inline overload moveRecordUnsafe(forward x:'T)
    | CompositeType?('T) and allValues?(BitwiseMovedType?, ..FieldTypes('T))
    = bitwiseCopyUnsafe(x);
inline overload moveRecordUnsafe(forward x:'T)
    | CompositeType?('T)
      and not allValues?(BitwiseMovedType?, ..FieldTypes('T))
      and allValues?(MovableType?, ..FieldTypes('T))
    = initializeRecord('T, ..mapValues(moveUnsafe, ..*x));

inline overload moveUnsafe(forward x:'T)
    | RecordType?('T)
      and not BitwiseMovedType?('T)
      and allValues?(MovableType?, ..FieldTypes('T))
    = moveRecordUnsafe(x);

symbol copyRecord;
inline overload copyRecord(x:'T)
    | CompositeType?('T) and allValues?(BitwiseCopiedType?, ..FieldTypes('T))
    = bitwiseCopyUnsafe(x);
inline overload copyRecord(x:'T)
    | CompositeType?('T)
      and not allValues?(BitwiseCopiedType?, ..FieldTypes('T))
      and allValues?(CopyableType?, ..FieldTypes('T))
    = initializeRecord('T, ..mapValues(copy, ..*x));

inline overload copy(x:'T)
    | RecordType?('T)
      and not BitwiseCopiedType?('T)
      and allValues?(CopyableType?, ..FieldTypes('T))
    = copyRecord(x);

symbol copyAssignRecord;
inline overload copyAssignRecord(ref to:'T, from:'T)
    | CompositeType?('T)
      and not allValues?(BitwiseAssignedType?, ..FieldTypes('T))
      and allValues?(AssignableType?, ..FieldTypes('T))
{
    static for (field in ..#FieldNames('T))
        to[field] = from[field];
}
inline overload copyAssignRecord(ref to:'T, from:'T)
    | CompositeType?('T)
      and allValues?(BitwiseAssignedType?, ..FieldTypes('T))
{
    to <-- from;
}

inline overload copyAssign(ref to:'T, from:'T)
    | RecordType?('T)
      and not BitwiseAssignedType?('T)
      and allValues?(AssignableType?, ..FieldTypes('T))
{
    copyAssignRecord(to, from);
}


//
// comparison
//

inline equalRecords?(a:'T, b:'T) | CompositeType?('T)
{
    static for (field in ..#FieldNames('T))
        if (a[field] != b[field])
            return false;
    return true;
}

inline overload equals?(a:'T, b:'T) | RecordType?('T) = equalRecords?(a, b);

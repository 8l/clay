import values.*;
import prelude.operators.c.*;
import prelude.procedures.*;
import prelude.constructors.*;
import meta.statics.*;
import meta.lltypes.*;

import __primitives__.(
    StaticPlus,
    StaticMinus,
    StaticMultiply,
    StaticQuotient,
    StaticRemainder,
    StaticAdd,
    StaticSubtract,
    StaticLesser?,
    StaticEquals?,
    StaticBitnot,
    StaticBitor,
    StaticBitand,
    StaticBitxor,
    StaticBitshl,
    StaticBitshr,
);


//
// tupleLiteral
//

inline tupleLiteral(forward ..elements:'T) = Tuple[..'T](..elements);



//
// index
//

symbol index;


//
// dereference
//

inline dereference(forward a:'T)
    | PointerType?('T) and StaticCallDefined?(PointerReferencedType, 'T)
    = forward cDereference(#PointerReferencedType('T), a);



//
// staticIndex, fieldRef
//

inline staticIndex(forward a, #'n)  | Defined?([a[#'n]]) = forward a[#'n];
inline fieldRef(forward a, #'field) | Defined?([a[#'field]]) = forward a[#'field];



//
// arithmetic operators
//

symbol plus;
symbol minus;
symbol multiply = VariadicBinaryOperatorProcedure();
symbol divide;
symbol quotient;
symbol remainder;
symbol add = VariadicBinaryOperatorProcedure();
symbol subtract;


//
// arithmetic over statics
//

overload #plus('x) = StaticPlus('x);
overload #minus('x) = StaticMinus('x);
overload #multiply('x, 'y) = StaticMultiply('x, 'y);
overload #quotient('x, 'y) = StaticQuotient('x, 'y);
overload #remainder('x, 'y) = StaticRemainder('x, 'y);
overload #add('x, 'y) = StaticAdd('x, 'y);
overload #subtract('x, 'y) = StaticSubtract('x);


//
// arithmetic operations defined in terms of *Assign operators

private inline opFromAssign(#'assignFn, a:'A, b:'B) c:'A
{
    c <-- a;
    try {
        'assignFn(c, b);
    } catch (ex) {
        destroyUnsafe(c);
        throw ex;
    }
}

inline overload multiply(a:'A, b:'B) | Defined?([multiplyAssign(a, b);])
    = opFromAssign(multiplyAssign, a, b);
inline overload divide(a:'A, b:'B) | Defined?([divideAssign(a, b);])
    = opFromAssign(divideAssign, a, b);
inline overload quotient(a:'A, b:'B) | Defined?([quotientAssign(a, b);])
    = opFromAssign(quotientAssign, a, b);
inline overload remainder(a:'A, b:'B) | Defined?([remainderAssign(a, b);])
    = opFromAssign(remainderAssign, a, b);
inline overload add(a:'A, b:'B) | Defined?([addAssign(a, b);])
    = opFromAssign(addAssign, a, b);
inline overload subtract(a:'A, b:'B) | Defined?([subtractAssign(a, b);])
    = opFromAssign(subtractAssign, a, b);


//
// arithmetic operators over c types
//

inline overload plus(a:'N) | NumberType?('N) = a;
inline overload minus(a:'N) | NumberType?('N) = cMinus('N, a);

inline overload multiply(a:'A, b:'B) | ArithmeticCompatiblePrimitiveTypes?('A, 'B)
    = cMultiply(#BiggerNumberType('A, 'B), a, b);
inline overload divide(a:'A, b:'B) | ArithmeticCompatiblePrimitiveFloatTypes?('A, 'B)
    = cDivide(#BiggerNumberType('A, 'B), a, b);
inline overload divide(a:'A, b:'B) | ArithmeticCompatiblePrimitiveIntTypes?('A, 'B)
    = Float64(a) / Float64(b);
inline overload quotient(a:'A, b:'B) | ArithmeticCompatiblePrimitiveIntTypes?('A, 'B)
    = cDivide(#BiggerNumberType('A, 'B), a, b);
inline overload remainder(a:'A, b:'B) | ArithmeticCompatiblePrimitiveIntTypes?('A, 'B)
    = cRemainder(#BiggerNumberType('A, 'B), a, b);

inline overload add(a:'A, b:'B) | ArithmeticCompatiblePrimitiveTypes?('A, 'B)
    = cAdd(#BiggerNumberType('A, 'B), a, b);
inline overload subtract(a:'A, b:'B) | ArithmeticCompatiblePrimitiveTypes?('A, 'B)
    = cSubtract(#BiggerNumberType('A, 'B), a, b);


//
// ContiguousCoordinate arithmetic
//

inline overload add(a:ContiguousCoordinate['A], b:'B)
    | IntegerType?('B)
    = cAdd(ContiguousCoordinate['A], a, b);

inline overload add(a:'A, b:ContiguousCoordinate['B])
    | IntegerType?('A)
    = cAdd(ContiguousCoordinate['B], b, a);

inline overload subtract(a:ContiguousCoordinate['A], b:'B)
    | IntegerType?('B)
    = cSubtract(ContiguousCoordinate['A], a, b);

inline overload subtract(a:ContiguousCoordinate['A], b:ContiguousCoordinate['A])
    = cSubtract(Int, a, b);

inline overload index(a, n:'I)
    | IntegerType?('I) and Defined?([(a + n)^;])
    = forward ..((a + n)^);


//
// comparison operators
//

symbol lesser?;
lesserEquals?(a, b) | Defined?([b < a]) = not b < a;
greater?(a, b) | Defined?([b < a]) = b < a;
greaterEquals?(a, b) | Defined?([a < b]) = not a < b;

symbol equals?;
notEquals?(a, b) | Defined?([a == b]) = not a == b;

// XXX use intrinsic ordered/unordered conditions
ordered?(a, b)
    | Defined?([a <= b]) and Defined?([b <= a])
    = a <= b or b <= a;

nan?(a:'A) | FloatType?('A) = a != a;

overload lesser?(#'a, #'b) = *#('a < 'b);
overload equals?(#'a, #'b) = *#('a == 'b);

overload lesser?(a:StringConstant, b:StringConstant) = lesserSequence?(a, b);


//
// comparison operators on numbers
//

inline overload lesser?(a:'A, b:'B) | ComparablePrimitiveTypes?('A, 'B)
    = cLesser?(a, b);
inline overload lesserEquals?(a:'A, b:'B) | ComparablePrimitiveTypes?('A, 'B)
    = cLesserEquals?(a, b);
inline overload greater?(a:'A, b:'B) | ComparablePrimitiveTypes?('A, 'B)
    = cGreater?(a, b);
inline overload greaterEquals?(a:'A, b:'B) | ComparablePrimitiveTypes?('A, 'B)
    = cGreaterEquals?(a, b);

inline overload equals?(a:'A, b:'B) | EqualityComparablePrimitiveTypes?('A, 'B)
    = cEquals?(a, b);
inline overload notEquals?(a:'A, b:'B) | EqualityComparablePrimitiveTypes?('A, 'B)
    = cNotEquals?(a, b);


//
// assignment
//

inline moveAssign(ref a:'T, rvalue b:'T) {
    destroyUnsafe(a);
    a <-- move(b);
}
inline overload moveAssign(ref a:'T, rvalue b:'T)
    | BitwiseMoveAssignedType?('T)
{
    a <-- move(b);
}

inline copyAssign(ref a:'T, b:'T) | BitwiseAssignedType?('T) { a <-- b; }
inline overload copyAssign(ref a:'A, b:'B) | AssignablePrimitiveTypes?('A, 'B)
{
    cAssign(a, b);
}

symbol assign;
inline overload assign(ref a:'T, b:'U) | Defined?([copyAssign(a, b);]) {
    copyAssign(a, b);
}
inline overload assign(ref a:'T, rvalue b:'T) | Defined?([moveAssign(a, b);]) {
    moveAssign(a, b);
}


//
// arithmetic assignment
//

inline addAssign      (ref a, b) | Defined?([a  +  b]) { a = a  +  b; }
inline subtractAssign (ref a, b) | Defined?([a  -  b]) { a = a  -  b; }
inline multiplyAssign (ref a, b) | Defined?([a  *  b]) { a = a  *  b; }
inline divideAssign   (ref a, b) | Defined?([a  /  b]) { a = a  /  b; }
inline quotientAssign (ref a, b) | Defined?([a div b]) { a = a div b; }
inline remainderAssign(ref a, b) | Defined?([a mod b]) { a = a mod b; }

inline inc(ref a:'T) | Defined?([addAssign     (a, 1);]) { a += 1; }
inline dec(ref a:'T) | Defined?([subtractAssign(a, 1);]) { a -= 1; }



//
// call
//

symbol call;

inline overload call(#'f, forward ..args) = forward ..'f(..args);

// non-variadic external code pointer
inline overload call(f:ExternalCodePointer['ABI, false, 'I, 'O], ..args:'Args)
    ..ExternalOutputTypes('O)
    | 'I == (..'Args)
    = ..cExternalCall(f, ..args);

// variadic external code pointer
inline overload call(f:ExternalCodePointer['ABI, true, 'I, 'O], ..args:'Args)
    ..ExternalOutputTypes('O)
    | allValues?(RequireExternalPODType?, ..'Args)
      and 'I == (..takeValues(StaticTupleSize('I), ..'Args))
    = ..cExternalCall(f, ..args);



//
// destroyUnsafe, initializeFromUnsafe, moveUnsafe, resetUnsafe, copy, move
//

symbol initializeFromUnsafe;
inline overload initializeFromUnsafe(ref a:'T, b:'T) { a <-- copy(b); }
inline overload initializeFromUnsafe(ref a:'T, rvalue b:'T) { a <-- move(b); }

destroyUnsafe(ref x:'T) | NotDestroyedType?('T) {}
inline resetUnsafe(ref x:'T)
    | DefaultInitializableType?('T) and InitializeDoesNotThrowType?('T)
    { x <-- 'T(); }
inline overload resetUnsafe(ref x:'T) | NotResetType?('T) {}

inline bitwiseCopyUnsafe(forward a:'T) returned:'T { cAssign(returned, a); }

inline copy(a:'T) | BitwiseCopiedType?('T) = bitwiseCopyUnsafe(a);

inline moveUnsafe(ref a:'T) | BitwiseMovedType?('T) = bitwiseCopyUnsafe(a);

symbol move;
inline overload move(ref a:'T) returned:'T { returned <-- moveUnsafe(a); resetUnsafe(a); }
inline overload move(rvalue a:'T) = moveUnsafe(rvalueToRefUnsafe(a));


//
// unpack
//

symbol unpack;
overload unpack(..#'x) = ..'x;
overload #unpack((..'x)) = ..'x;


//
// refToRvalueUnsafe, rvalueToRefUnsafe
//

inline refToRvalueUnsafe(ref x:'T) rvalue r:'T
    __c__ """$r = &$x;""";
inline rvalueToRefUnsafe(rvalue x:'T) ref r:'T
    __c__ """$r = &$x;""";


//
// max, min
//

symbol max = VariadicBinaryOperatorProcedure();
symbol min = VariadicBinaryOperatorProcedure();

inline overload max(a:'A, b:'A) | Defined?([a < b])
    = if (a < b) b else a;
inline overload min(a:'A, b:'A) | Defined?([a < b])
    = if (a < b) a else b;

inline overload max(a:'A, b:'A) | FloatType?('A)
    = if (a >= b or nan?(b)) a else b;
inline overload min(a:'A, b:'A) | FloatType?('A)
    = if (a <= b or nan?(b)) a else b;

overload #max('a, 'b) = if ('a < 'b) 'b else 'a;
overload #min('a, 'b) = if ('a < 'b) 'a else 'b;


//
// case?
//

inline case?(value:'T, forward ..cases:'U)
    | allValues?([u -> CallDefined?(equals?, 'T, u)], ..'U)
    = inValues?(value, ..cases);


//
// uninitializedUnsafe
//

uninitializedUnsafe(#'T) x:'T {}


//
// bitcast
//

bitcast(#'A, forward b:'B)
    | TypeSize('A) <= TypeSize('B)
    = forward cRefCast('A, b);


//
// bitand, bitor, bitxor, bitnot
//

symbol bitand = VariadicBinaryOperatorProcedure();
overload bitand(a:'A, b:'B) | BitwiseCompatiblePrimitiveTypes?('A, 'B)
    = cBitand(#BiggerNumberType('A, 'B), a, b);

symbol bitor = VariadicBinaryOperatorProcedure();
overload bitor(a:'A, b:'B) | BitwiseCompatiblePrimitiveTypes?('A, 'B)
    = cBitor(#BiggerNumberType('A, 'B), a, b);

symbol bitxor = VariadicBinaryOperatorProcedure();
overload bitxor(a:'A, b:'B) | BitwiseCompatiblePrimitiveTypes?('A, 'B)
    = cBitxor(#BiggerNumberType('A, 'B), a, b);

symbol bitnot;
overload bitnot(a:'A) | IntegerType?('A) = cBitnot('A, a);

symbol bitandc;
overload bitandc(a:'A, b:'B) | BitwiseCompatiblePrimitiveTypes?('A, 'B)
    = bitand(a, bitnot(b));

symbol bitshl;
overload bitshl(a:'A, shift:'N) | IntegerType?('A) and IntegerType?('N)
    = cBitshl('A, a, shift);

symbol bitshr;
overload bitshr(a:'A, shift:'N) | IntegerType?('A) and IntegerType?('N)
    = cBitshr('A, a, shift);

symbol bitrol;
overload bitrol(a:'A, shift:'N) | IntegerType?('A) and IntegerType?('N)
    = bitor(bitshl(a, shift), bitshr(a, TypeSize('A)*8u - UInt(shift)));

symbol bitror;
overload bitror(a:'A, shift:'N) | IntegerType?('A) and IntegerType?('N)
    = bitor(bitshr(a, shift), bitshl(a, TypeSize('A)*8u - UInt(shift)));


//
// bitwise static operations
//

overload #bitnot('x) = StaticBitnot('x);
overload #bitand('x, 'y) = StaticBitand('x, 'y);
overload #bitor('x, 'y) = StaticBitor('x, 'y);
overload #bitxor('x, 'y) = StaticBitxor('x, 'y);
overload #bitshl('x, 'y) = StaticBitshl('x, 'y);
overload #bitshr('x, 'y) = StaticBitshr('x, 'y);

overload #bitandc('x, 'y) = StaticBitand('x, StaticBitnot('y));


//
// type-to-argument and type-to-return value conversions
// these should only be used as shims for Type() tests and never in real code
//

symbol arg;
overload arg(#'T)         const  x:'T __c__ """abort();""";
overload arg(#Const['T])  const  x:'T __c__ """abort();""";
overload arg(#Ref['T])    ref    x:'T __c__ """abort();""";
overload arg(#Rvalue['T]) rvalue x:'T __c__ """abort();""";

overload arg(..#'T) | countValues(..'T) != 1 = forward ..mapValues(arg, ..'T);

symbol ret;
overload ret(#'T)                x:'T __c__ """abort();""";
overload ret(#Const['T])  const  x:'T __c__ """abort();""";
overload ret(#Ref['T])    ref    x:'T __c__ """abort();""";
overload ret(#Rvalue['T]) rvalue x:'T __c__ """abort();""";

overload ret(..#'T) | countValues(..'T) != 1 = forward ..mapValues(ret, ..'T);


//
// identity function
//

inline identity(forward ..x) = forward ..x;


//
// dispatch operator
//

inline dispatch(forward subject, forward fn) = forward ..fn(..unpack(subject));

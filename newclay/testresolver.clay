import newclay.common.*;
import newclay.core.*;
import newclay.loader.*;
import newclay.ast as ast;
import newclay.ast.(visitAST, visitASTFields);

record TestResolverVisitor (depth:Int);

depth(d) {
    var s = String();
    for (i in range(d)) s += "  ";
    return s;
}

overload visitAST(v:TestResolverVisitor, node:ast.Symbol) {
    println(depth(v.depth), "symbol ", node.name.str);
    v.depth += 1;
    visitASTFields(v, node);
    v.depth -= 1;
}

overload visitAST(v:TestResolverVisitor, node:ast.LambdaExpr) {
    println(depth(v.depth), "lambda");
    v.depth += 1;
    visitASTFields(v, node);
    v.depth -= 1;
}

overload visitAST(v:TestResolverVisitor, node:ast.Overload) {
    println(depth(v.depth), "overload ", node.target);
    v.depth += 1;
    visitASTFields(v, node);
    v.depth -= 1;
}

overload visitAST(v:TestResolverVisitor, node:ast.StaticOverload) {
    println(depth(v.depth), "static overload ", node.target);
    v.depth += 1;
    visitASTFields(v, node);
    v.depth -= 1;
}

printDottedPath(x:Vector[ast.Identifier]) {
    interleave(x,
        () => {print(".");},
        i  => {print(i.str);}
    );
}

overload visitAST(v:TestResolverVisitor, node:ast.NameRef) {
    print(depth(v.depth));
    printDottedPath(node.name);
    println(" references ", node.env);
}

main(args) {
    if (size(args) != 2) {
        println("usage: ", args[0], " <file>");
        return -1;
    }
    try {
        var program = loadProgram(args[1]);
        for (moduleName, module in items(program.modules)) {
            println(moduleName, ":");
            visitAST(TestResolverVisitor(1), module.ast);
        }
    }
    catch (e:ClayError) {
        displayError(e);
    }
    return 0;
}

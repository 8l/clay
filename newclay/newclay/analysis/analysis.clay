import maybe.*;
import newclay.ast as ast;
import newclay.core.*;
import newclay.hooks.*;
import newclay.evaluator.*;

record SpecializationContext (
    body: Pointer[Vector[SpecializedNode]],
    complete?: Pointer[Bool],
);

subContext(s:SpecializationContext, subBody:Vector[SpecializedNode])
    = SpecializationContext(&subBody, s.complete?);


//
// analyzeMain
//

analyzeMain() {
    var mainValue = envEntryToValue(publicLookup(getProgramMainModule(), "main"));
    if (not runtimeCallable?(mainValue))
        genericError("main is not a procedure");
    return analyzeCallable(mainValue, VariableList());
}


//
// analyzeLiteralCodeBody
//

private analyzeLiteralCodeBody(s:SpecializationContext, env:Env, body:ast.LiteralCodeBody) {
    return addSpecializedLiteralCodeBody(s, body);
}


//
// analyzeNode
//

analyzeNode(s:SpecializationContext, env:Env, body:ast.ExprCodeBody)
    = analyzeSubnode(s, newEnv(env), body.exprs);


//
// analyzeNode(ReturnExprList; ReturnExprListItem instances)
//

private analyzeExprListNode(s, env, exprList) {
    var outputs = VariableList();
    for (item in exprList) {
        var itemOutputs = analyzeSubnode(s, env, item);
        if (nothing?(itemOutputs))
            return nothing(VariableList);
        push(outputs, just(itemOutputs));
    }
    return Maybe(outputs);
}

overload analyzeNode(s:SpecializationContext, env:Env, exprList:ast.ReturnExprList)
    Maybe[VariableList]
    = analyzeExprListNode(s, env, exprList);

overload analyzeNode(s:SpecializationContext, env:Env, rItem:ast.ReturnExprListItem)
    Maybe[VariableList]
{
    var results = analyzeSubnode(s, env, rItem.item);
    return maybe(results,
        rs ref=> Maybe(map(r ref=> analyzeReturn(s, rItem, r), rs)), 
    );
}

private analyzeReturn(
    s:SpecializationContext,
    rItem:ast.ReturnExprListItem,
    source:RTVariable
) = maybe(rItem.kind,
    // reference return
    returnKind ref=> {
        switch (returnKind) {
        case ast.RETURN_EXPR_CONST:
            if (source.value?)
                genericError("cannot return temporary as const reference");
            if (source.kind == RT_VARIABLE_RVALUE)
                genericError("cannot return rvalue as const reference");
            if (source.kind != RT_VARIABLE_CONST)
                return singleAddSpecializedBindReturn(s, rItem, source,
                    newReference(RT_VARIABLE_CONST, source.type, "returned")
                );
            break;
        case ast.RETURN_EXPR_REF:
            if (source.value?)
                genericError("cannot return temporary as ref reference");
            if (source.kind == RT_VARIABLE_RVALUE)
                genericError("cannot return rvalue as ref reference");
            if (source.kind == RT_VARIABLE_CONST)
                genericError("cannot return const as ref reference");
            break;
        case ast.RETURN_EXPR_RVALUE:
            if (source.value?)
                genericError("cannot return temporary as ref reference");
            if (source.kind == RT_VARIABLE_CONST)
                genericError("cannot return const as rvalue reference");
            if (source.kind != RT_VARIABLE_RVALUE)
                return singleAddSpecializedBindReturn(s, rItem, source,
                    newReference(RT_VARIABLE_RVALUE, source.type, "returned")
                );
            break;
        case ast.RETURN_EXPR_FORWARD:
            if (source.value?) {
                if (source.kind != RT_VARIABLE_RVALUE)
                    genericError("cannot forward non-rvalue temporary value");
                return singleAddSpecializedBindReturn(s, rItem, source,
                    newValue(source.type, "returned")
                );
            }
            break;
        default:
            assert(false, "invalid reference kind");
            return RTVariable();
        }
        return singleAddSpecializedBindReturn(s, rItem, source, cloneVariable(source, "returned"));
    },
    // value return
    () ref=> singleAddSpecializedBindReturn(s, rItem, source, newValue(source.type, "returned")),
);


//
// analyzeNode(ExprList; ExprListItem instances)
//

overload analyzeNode(s:SpecializationContext, env:Env, exprList:ast.ExprList) Maybe[VariableList]
    = analyzeExprListNode(s, env, exprList);

overload analyzeNode(s:SpecializationContext, env:Env, expr:ast.ExprListItem) Maybe[VariableList]
    = analyzeNode(s, env, *expr);

overload analyzeNode(s:SpecializationContext, env:Env, expr:ast.Expr) Maybe[VariableList]
    = analyzeNode(s, env, *expr);

overload analyzeNode(s:SpecializationContext, env:Env, unpack:ast.UnpackExpr)
    = analyzeMultiExprSubnode(s, env, unpack.expr);


//
// analyzeNode(Expr instances)
//

overload analyzeNode(s:SpecializationContext, env:Env, expr:ast.BoolLiteral)
    = addSpecializedValue(s, expr, Value(BoolValue(expr.value)));

overload analyzeNode(s:SpecializationContext, env:Env, expr:ast.IntLiteral)
    // XXX Integer type matching
    = addSpecializedValue(s, expr, Value(IntValue(Int(expr.value))));

overload analyzeNode(s:SpecializationContext, env:Env, expr:ast.StringLiteral)
    = addSpecializedValue(s, expr, Value(StringValue(expr.value)));

overload analyzeNode(s:SpecializationContext, env:Env, expr:ast.NameRef) {
    var envEntry = privateLookup(env, expr.name);
    return match(envEntry,
        RTVariable, x ref=> Maybe(VariableList(x)),
        Value, x ref=> addSpecializedValue(s, expr, x),
        ValueList, x ref=>
            genericAnalysisError("multiple values cannot be used in single value context"),
        PatternCell, x ref=>
            genericAnalysisError("pattern variable cannot be used in non-pattern expression"),
        ListPatternCell, x ref=>
            genericAnalysisError("pattern variable cannot be used in non-pattern expression"),
        RTListVariable, x ref=>
            genericAnalysisError("multiple values cannot be used in single value context"),
        RTLabel, x ref=>
            genericAnalysisError("label cannot be used as a value"),
    );
}

overload analyzeNode(s:SpecializationContext, env:Env, expr:ast.PatternNameRef)
    = addSpecializedValue(s, expr, derefPattern(env, expr.name));

overload analyzeNode(s:SpecializationContext, env:Env, expr:ast.TupleExpr)
    = genericAnalysisError("tuples are not yet supported");

overload analyzeNode(s:SpecializationContext, env:Env, expr:ast.Call) {
    var callable = evaluateExpr(expr.expr, env);

    if (runtimeCallable?(callable)) {
        var maybeInputs = analyzeNode(s, env, expr.args);
        return maybe(maybeInputs,
            inputs ref=> addSpecializedCall(s, expr, inputs,
                analyzeCallableWithOneResult(callable, inputs)
            ),
        );
    } else {
        var args = evaluateExprList(expr.args, env);
        var result = evaluateCallWithOneResult(callable, args);
        return addSpecializedValue(s, expr, result);
    }
}

overload analyzeNode(s:SpecializationContext, env:Env, expr:ast.Indexing)
    = addSpecializedValue(s, expr, evaluateExpr(expr, env));

private analyzeShortCircuit(s, env, expr, LeftBody, RightBody)
    // XXX recursion
    = maybe(analyzeBoolSubnode(s, env, expr.expr1),
        results1 ref=> {
            var result1 = singleVariable(results1);
            var output = RTVariable(RT_VARIABLE_RVALUE, boolType(), true);

            var ifNode = SpecializedIf(ASTNode(expr), result1,
                Vector[SpecializedNode](),
                Vector[SpecializedNode]()
            );

            ref leftS = subContext(s, fieldRef(ifNode, LeftBody));
            ref rightS = subContext(s, fieldRef(ifNode, RightBody));

            maybe(analyzeBoolSubnode(rightS, env, expr.expr2),
                results2 ref=> {
                    addSpecializedBind(rightS, ASTNode(expr),
                        singleVariable(results2),
                        output
                    );
                },
            );
            addSpecializedBind(leftS, ASTNode(expr), result1, output);
            addSpecializedNode(s, ifNode);
            return Maybe(VariableList(output));
        },
    );

overload analyzeNode(s:SpecializationContext, env:Env, expr:ast.AndExpr)
    = analyzeShortCircuit(s, env, expr, #elseBody, #thenBody);

overload analyzeNode(s:SpecializationContext, env:Env, expr:ast.OrExpr)
    = analyzeShortCircuit(s, env, expr, #thenBody, #elseBody);

overload analyzeNode(s:SpecializationContext, env:Env, expr:ast.LambdaExpr)
    = genericAnalysisError("lambda expressions are not yet supported");

overload analyzeNode(s:SpecializationContext, env:Env, expr:ast.IfExpr)
    = maybe(analyzeBoolSubnode(s, env, expr.condition),
        condResult ref=> {
            var ifNode = SpecializedIf(ASTNode(expr), singleVariable(condResult),
                Vector[SpecializedNode](),
                Vector[SpecializedNode]()
            );

            var thenRecursive? = false;
            var elseRecursive? = false;
            var thenResult = nothing(VariableList);
            var elseResult = nothing(VariableList);

            try {
                thenResult = analyzeSubnode(subContext(s, ifNode.thenBody), env, expr.thenPart);
            } catch (e:RecursiveAnalysisError) {
                s.complete?^ = false;
                thenRecursive? = true;
            }

            try {
                elseResult = analyzeSubnode(subContext(s, ifNode.elseBody), env, expr.elsePart);
            } catch (e:RecursiveAnalysisError) {
                s.complete?^ = false;
                elseRecursive? = true;
            }

            // XXX if recursive, we need to re-analyze the recursive branch with the other branch result
            if (thenRecursive? and elseRecursive?)
                recursiveAnalysisError();

            var outputs = unifyIfExprBranches(s, expr, ifNode, thenResult, elseResult);

            addSpecializedNode(s, ifNode);

            return outputs;
        },
    );

private unifyIfExprBranches(s, expr, ifNode, thenResult, elseResult)
    = maybe(thenResult,
        thenr ref=> maybe(elseResult,
            elser ref=> {
                var thenv = singleVariable(thenr);
                var elsev = singleVariable(elser);
                if (thenv.type != elsev.type)
                    genericError("return types in if expression branches do not match");
                var output = RTVariable();

                if (thenv.kind == elsev.kind)
                    output = RTVariable(thenv.kind, thenv.type,
                        thenv.value? and elsev.value? and thenv.kind == RT_VARIABLE_RVALUE
                    );
                else
                    output = RTVariable(RT_VARIABLE_RVALUE, thenv.type, true);

                addSpecializedBind(subContext(s, ifNode.thenBody), expr, thenv, output);
                addSpecializedBind(subContext(s, ifNode.elseBody), expr, elsev, output);

                return Maybe(VariableList(output));
            },
            () ref=> thenResult,
        ),
        () ref=> elseResult,
    );


//
// analyzeBlockCodeBody, analyzeStatement
//

private record ProcResult (
    statement: ast.ReturnStatement,
    results: VariableList,
);

private record ProcContext (
    results: Vector[ProcResult],
    recursiveReturn: Maybe[ast.ReturnStatement],
);

private record BlockContext (
    env: Env,
);

private record LoopContext (
    loop?: Bool,
    infiniteLoop?: Bool,
);

overload LoopContext() = LoopContext(false, false);

private analyzeBlockCodeBody(s:SpecializationContext, env:Env, block:ast.Block)
    = withLocation(block, () ref=> {
        var pc = ProcContext();
        var bc = BlockContext(env);
        var lc = LoopContext();

        var returns? = analyzeStatement(s, pc, bc, lc, block);

        if (returns?) {
            // "returns" without results? => nonterminating result
            // "returns" without definite results, but recursive return? => recursion error
            // returns with results? => results
            if (empty?(pc.results)) {
                maybe(pc.recursiveReturn, stmt ref=> {
                    withLocation(stmt, () ref=> {
                        recursiveAnalysisError();
                    });
                });
                return nothing(VariableList);
            } else {
                if (size(pc.results) > 1) {
                    var branches = Vector[VariableList](pc.results[0].results);
                    var results = cloneVariables(pc.results[0].results, "returned");
                    for (resultSet in slicedFrom(pc.results, 1)) {
                        withLocation(resultSet.statement, () ref=> {
                            if (not equalResults?(results, resultSet.results))
                                genericError("return statement types do not match");
                            if (size(results) > 0)
                                push(branches, resultSet.results);
                        });
                    }
                    if (size(results) > 0)
                        addSpecializedPhi(s, branches, results);
                    return Maybe(results);
                } else
                    return Maybe(pc.results[0].results);
            }
        } else {
            // falls through with no results? => void results
            // falls through with nonvoid results? => error (not all branches return)
            // falls through with void results? => void results
            if (not empty?(pc.results))
                for (resultSet in pc.results)
                    if (not empty?(resultSet.results))
                        genericError("not all branches return in block");
            return Maybe(VariableList());
        }
    });

private analyzeStatement(
    s:SpecializationContext,
    pc:ProcContext,
    bc:BlockContext,
    lc:LoopContext,
    stmt:ast.Statement
) Bool = withLocation(stmt, () ref=> analyzeStatement(s, pc, bc, lc, *stmt));

overload analyzeStatement(s, pc, bc, lc, block:ast.Block) Bool {
    var subbc = BlockContext(newEnv(bc.env));
    // introduce labels
    for (stmt in block.statements)
        match(stmt,
            ast.LabelDef, label ref=> {
                envAdd(subbc.env, label.name, RTLabel(label));
            },
            MatchDefault, x ref=> {},
        );
    // analyze substatements
    for (stmt in block.statements)
        if (analyzeStatement(s, pc, subbc, lc, stmt))
            return true;
    return false;
}

overload analyzeStatement(s, pc, bc, lc, stmt:ast.LabelDef) = false;

private analyzeInitialize(s, env:Env, stmt, toExprList:ast.ExprList, fromExprList:ast.ExprList) {
    try {
        return maybe(analyzeNode(s, env, toExprList),
            toValues ref=> maybe(analyzeNode(s, env, fromExprList),
                fromValues ref=> {
                    if (size(toValues) != size(fromValues))
                        genericError("got ", size(toValues), " values on left side of <-- but ", size(fromValues), " values on right side");
                    for (toValue, fromValue in zipped(toValues, fromValues)) {
                        if (toValue.kind != RT_VARIABLE_REF)
                            genericError("values on left side of <-- must be lvalues");
                        addSpecializedInitialize(s, stmt, fromValue, toValue);
                    }
                    return false;
                },
                () ref=> true,
            ),
            () ref=> true,
        );
    } catch (e:RecursiveAnalysisError) {
    // XXX recursive analysis will need to be an error if it influences delayed propagation of var
    // bindings
        s.complete?^ = false;
        return false;
    }
}

overload analyzeStatement(s, pc, bc, lc, stmt:ast.AssignmentStatement) {
    switch (stmt.kind) {
    case ast.ASSIGNMENT_ASSIGN:
        genericError("assignment with = not yet supported");
        // NOTE will need to support parallel assignment when assigning multiple values.
        // eg x, y = y, x;
        return false;
    case ast.ASSIGNMENT_INITIALIZE:
        return analyzeInitialize(s, bc.env, stmt, stmt.to, stmt.from);
    default:
        assert(false);
        return false;
    }
}

overload analyzeStatement(s, pc, bc, lc, stmt:ast.BindingStatement) {
    // XXX handle recursion in binding values?
    var subenv, returns? = ...analyzeBindings(s, bc.env, stmt, stmt.kind, *stmt.bindings);
    bc.env = subenv;
    return returns?;
}

overload analyzeStatement(s, pc, bc, lc, stmt:ast.GotoStatement) {
    match(privateLookup(bc.env, stmt.label),
        RTLabel, x ref=> {},
        MatchDefault, x ref=> {
            genericError("goto target is not a label");
        },
    );
    // XXX analyze branches and loops introduced by goto
    genericError("goto is not yet supported");
    return false;
}

overload analyzeStatement(s, pc, bc, lc, stmt:ast.ReturnStatement) {
    try {
        maybe(analyzeNode(s, bc.env, stmt.values),
            results ref=> {
                push(pc.results, ProcResult(stmt, results));
                addSpecializedNode(s, SpecializedReturn(stmt));
            },
        );
    } catch (e:RecursiveAnalysisError) {
        s.complete?^ = false;
        pc.recursiveReturn = Maybe(stmt);
    }
    return true;
}

overload analyzeStatement(s, pc, bc, lc, stmt:ast.BreakStatement) {
    if (not lc.loop?)
        genericError("break statement cannot be used outside of a loop");
    lc.infiniteLoop? = false;
    addSpecializedNode(s, SpecializedBreak(stmt));
    return true;
}

overload analyzeStatement(s, pc, bc, lc, stmt:ast.ContinueStatement) {
    if (not lc.loop?)
        genericError("continue statement cannot be used outside of a loop");
    addSpecializedNode(s, SpecializedContinue(stmt));
    return true;
}

overload analyzeStatement(s, pc, bc, lc, stmt:ast.ThrowStatement) {
    try {
        maybe(analyzeSubnode(s, bc.env, stmt.value),
            exResult ref=> {
                addSpecializedNode(s, SpecializedThrow(stmt, singleVariable(exResult)));
            },
        );
    } catch (e:RecursiveAnalysisError) {
        s.complete?^ = false;
    }
    return true;
}

overload analyzeStatement(s, pc, bc, lc, stmt:ast.IfStatement)
    // XXX recursion with statements
    = maybe(analyzeBoolSubnode(s, bc.env, stmt.condition),
        condResult ref=> {
            var ifNode = SpecializedIf(ASTNode(stmt), singleVariable(condResult),
                Vector[SpecializedNode](),
                Vector[SpecializedNode](),
            );

            var thenRecursive? = false;
            var elseRecursive? = nothing?(stmt.elseBody);
            var thenReturns? = false;
            var elseReturns? = false;

            try {
                thenReturns? = analyzeStatement(
                    subContext(s, ifNode.thenBody),
                    pc,
                    BlockContext(newEnv(bc.env)),
                    lc,
                    stmt.thenBody
                );
            } catch (e:RecursiveAnalysisError) { thenRecursive? = true; }

            maybe(stmt.elseBody, elseBody ref=> {
                try {
                    elseReturns? = analyzeStatement(
                        subContext(s, ifNode.elseBody),
                        pc,
                        BlockContext(newEnv(bc.env)),
                        lc,
                        elseBody
                    );
                } catch (e:RecursiveAnalysisError) { elseRecursive? = true; }
            });

            if (thenRecursive? and elseRecursive?)
                recursiveAnalysisError();

            addSpecializedNode(s, ifNode);
            return thenReturns? and elseReturns?;
        },
        () => true,
    );

overload analyzeStatement(s, pc, bc, lc, stmt:ast.ForStatement) {
    genericError("for loops are not yet supported");
    return false;
}

overload analyzeStatement(s, pc, bc, lc, stmt:ast.StaticForStatement) {
    genericError("static for loops are not yet supported");
    return false;
}

private trueCondition?(expr)
    // XXX check for true condition
    = false;

overload analyzeStatement(s, pc, bc, lc, stmt:ast.WhileStatement) {
    var whileNode = SpecializedWhile(
        stmt,
        Vector[SpecializedNode](),
        RTVariable(),
        Vector[SpecializedNode](),
    );
    return maybe(analyzeBoolSubnode(subContext(s, whileNode.conditionBody), bc.env, stmt.condition),
        condResult ref=> {
            whileNode.condition = singleVariable(condResult);

            var subbc = BlockContext(newEnv(bc.env));
            var sublc = LoopContext(true, trueCondition?(stmt.condition));

            var bodyReturns? = analyzeStatement(
                subContext(s, whileNode.body),
                pc, subbc, sublc, stmt.body
            );

            addSpecializedNode(s, whileNode);
            return sublc.infiniteLoop? and bodyReturns?;
        },
        () ref=> {
            addSpecializedNodes(s, whileNode.conditionBody);
            return true;
        }
    );
}

overload analyzeStatement(s, pc, bc, lc, stmt:ast.SwitchStatement) {
    genericError("switch statements are not yet supported");
    return false;
}

overload analyzeStatement(s, pc, bc, lc, stmt:ast.TryStatement) {
    genericError("try statements are not yet supported");
    return false;
}

overload analyzeStatement(s, pc, bc, lc, stmt:ast.ExprStatement) Bool {
    try {
        return nothing?(analyzeMultiExprSubnode(s, bc.env, stmt.expr));
    } catch (e:RecursiveAnalysisError) {
        s.complete?^ = false;
        return false;
    }
}


//
// analyzeBindings
//

private procedure analyzeBindings;

overload analyzeBindings(
    s:SpecializationContext,
    env_:Env,
    stmt:ast.BindingStatement,
    kind:ast.BindingKind,
    bindingList:ast.SingleBindingList
) {
    var env = env_;
    for (binding in bindingList) {
        env = newEnv(env);
        if (maybeAnalyzeBinding(s, env, stmt, kind, binding))
            return env, true;
    }
    return env, false;
}

private maybeAnalyzeBinding(s, env, stmt, kind, binding)
    = maybe(analyzeSubnode(s, env, binding.value),
        valueResult ref=> {
            analyzeBinding(s, env, stmt, kind, binding.name, singleVariable(valueResult));
            return false;
        },
        () ref=> true,
    );

private analyzeBinding(
    s,
    env,
    stmt:ast.BindingStatement,
    kind:ast.BindingKind,
    bindingName:ast.BindingName,
    value:RTVariable
) {
    maybe(bindingName.type, typePattern ref=> {
        var bindingType = evaluatePattern(typePattern, env);
        if (not unify(bindingType, value.type))
            genericError(
                "variable declared with type ", bindingType,
                " but bound to value of type ", value.type,
            );
    });

    var variableKind = bindingVariableKind(kind, value.kind);
    var variableValue? = kind == ast.BINDING_VAR;
    var result = RTVariable(variableKind, value.type, variableValue?, bindingName.name.str);
    addSpecializedBind(s, stmt, value, result);
    envAdd(env, bindingName.name, result);
}

private bindingVariableKind(bindingKind:ast.BindingKind, valueKind:RTVariableKind) {
    switch (bindingKind) {
    case ast.BINDING_VAR:
        return RT_VARIABLE_REF;
    case ast.BINDING_REF:
        if (valueKind != RT_VARIABLE_REF)
            genericError("ref reference must be bound to a non-const lvalue");
        return RT_VARIABLE_REF;
    case ast.BINDING_RVALUE:
        if (valueKind != RT_VARIABLE_RVALUE)
            genericError("rvalue reference must be bound to an rvalue");
        return RT_VARIABLE_RVALUE;
    case ast.BINDING_CONST:
        if (valueKind == RT_VARIABLE_RVALUE)
            genericError("const reference must be bound to an lvalue");
        return RT_VARIABLE_CONST;
    case ast.BINDING_FORWARD:
        return valueKind;
    case ast.BINDING_ALIAS:
        // XXX
        genericError("alias variables are not yet supported");
        return RTVariableKind();
    default:
        assert(false, "invalid binding kind");
        return RTVariableKind();
    }
}

overload analyzeBindings(
    s:SpecializationContext,
    env_:Env,
    stmt:ast.BindingStatement,
    kind:ast.BindingKind,
    bindingList:ast.MultipleBindingList
) {
    var env = newEnv(env_);
    return ...maybe(bindingList.values,
        valueExprList ref=> ...maybe(analyzeNode(s, env, valueExprList),
            values ref=> {
                if (not arityMatches?(bindingList.names, values))
                    genericError("incorrect number of values for variable binding");
                for (binding, value in zipped(bindingList.names.items, values))
                    analyzeBinding(s, env, stmt, kind, binding, value);
                // XXX variadic item
                if (just?(bindingList.names.variadicItem))
                    genericError("variadic variable binding not yet supported");
                return env, false;
            },
            () ref=> ...(env, true),
        ),
        () ref=> {
            // XXX
            genericError("variable binding without initialization is not yet supported");
            return env, false;
        },
    );
}


//
// analyzeMultiExprSubnode
//

private analyzeMultiExprSubnode(s, env, ast) Maybe[VariableList]
    = withLocation(ast, () ref=> analyzeMultiExprNode(s, env, ast));

private analyzeMultiExprNode(s, env, ast)
    = genericAnalysisError("single value cannot be used in multiple value context");

overload analyzeMultiExprNode(s, env, expr:ast.Expr) Maybe[VariableList]
    = analyzeMultiExprSubnode(s, env, *expr);

overload analyzeMultiExprNode(s, env, expr:ast.NameRef) {
    var envEntry = privateLookup(env, expr.name);
    return match(envEntry,
        RTListVariable, x ref=> Maybe(x.vars),
        ValueList, x ref=> addSpecializedValueList(s, expr, x),
        Value, x ref=>
            genericAnalysisError("single value cannot be used in multiple value context"),
        PatternCell, x ref=>
            genericAnalysisError("pattern variable cannot be used in non-pattern expression"),
        ListPatternCell, x ref=>
            genericAnalysisError("pattern variable cannot be used in non-pattern expression"),
        RTVariable, x ref=>
            genericAnalysisError("single value cannot be used in multiple value context"),
        RTLabel, x ref=>
            genericAnalysisError("label cannot be used as a value"),
    );
}

overload analyzeMultiExprNode(s:SpecializationContext, env:Env, expr:ast.PatternNameRef)
    = addSpecializedValueList(s, expr, derefListPattern(env, expr.name));

overload analyzeMultiExprNode(s:SpecializationContext, env:Env, expr:ast.Call) {
    var callable = evaluateExpr(expr.expr, env);

    if (runtimeCallable?(callable)) {
        var maybeInputs = analyzeNode(s, env, expr.args);
        return maybe(maybeInputs,
            inputs ref=> addSpecializedCall(s, expr, inputs, analyzeCallable(callable, inputs)),
        );
    } else {
        var args = evaluateExprList(expr.args, env);
        var result = evaluateCall(callable, args);
        return addSpecializedValueList(s, expr, result);
    }
}


//
// analyzeBoolSubnode
//

validBoolResult?(results:Maybe[VariableList])
    = maybe(results,
        r => singleVariable(r).type == boolType(),
        () => true
    );

analyzeBoolSubnode(s, env, ast) = withLocation(ast, () ref=> {
    try {
        var results = analyzeNode(s, env, ast);
        if (not validBoolResult?(results))
            // OK to assume results is not Nothing and has one element here
            genericError(boolType(), " expected but ", just(results)[0].type, " received");
        return results;
    } catch (e:RecursiveAnalysisError) {
        s.complete?^ = false;
        return Maybe(VariableList(newValue(boolType())));
    }
});


//
// runtimeCallable?
//

runtimeCallable?(x:Value) {
    return match(x,
        SymbolHead, y => symbolHeadTypeTemplate?(y),
        Symbol, y => (symbolType?(y) or symbolProcedure?(y)),
        MatchDefault, y => false,
    );
}


//
// analyzeNode helpers
//

singleVariable(s:VariableList) {
    if (size(s) != 1)
        genericError("multiple values cannot be used in single value context");
    return s[0];
}

cloneVariable(v:RTVariable, ...name) = RTVariable(v.kind, v.type, v.value?, ...name);
cloneVariables(vs:VariableList, ...name) = map(v ref=> cloneVariable(v, ...name), vs);
overload cloneVariables(mvs:Maybe[VariableList])
    = maybe(mvs, vs => Maybe(cloneVariables(vs)));

analyzeSubnode(s:SpecializationContext, env:Env, ast) Maybe[VariableList]
    = withLocation(ast, () ref=> analyzeNode(s, env, ast));

genericAnalysisError(forward ...err) {
    genericError(...err);
    return nothing(VariableList);
}

newValue(type, ...name) = RTVariable(RT_VARIABLE_RVALUE, type, true, ...name);
newReference(kind, type, ...name) = RTVariable(kind, type, false, ...name);

valueToRTVariable(value:Value) = match(value,
    BoolValue, x => newValue(boolType()),
    IntValue, x => newValue(intType()),
    StringValue, x => {
        genericError("strings are not yet supported");
        return RTVariable();
    },
    TupleValue, x => {
        genericError("tuples are not yet supported");
        return RTVariable();
    },
    MatchDefault, x => newValue(staticType(Value(x))),
);

valueToSingleVariableList(value) = VariableList(valueToRTVariable(value));

addSpecializedNode(s:SpecializationContext, node:SpecializedNode) {
    push(s.body^, node);
}

[T | VariantMember?(SpecializedNode, T)]
overload addSpecializedNode(s:SpecializationContext, node:T) {
    push(s.body^, SpecializedNode(node));
}

addSpecializedNodes(s:SpecializationContext, node:Vector[SpecializedNode]) {
    push(s.body^, node);
}

addSpecializedLiteralCodeBody(s:SpecializationContext, body:ast.LiteralCodeBody) {
    addSpecializedNode(s, SpecializedLiteralCodeBody(body));
    return Maybe(VariableList());
}

addSpecializedValue(s:SpecializationContext, ast, value:Value) {
    var outputs = valueToSingleVariableList(value);
    addSpecializedNode(s, SpecializedValueList(ASTNode(ast), ValueList(value), outputs));
    return Maybe(outputs);
}

addSpecializedValueList(s:SpecializationContext, ast, valueList:ValueList) {
    var outputs = Vector(mapped(valueToRTVariable, valueList));
    addSpecializedNode(s, SpecializedValueList(ASTNode(ast), valueList, outputs));
    return Maybe(outputs);
}

addSpecializedCall(
    s:SpecializationContext,
    ast,
    inputs:VariableList,
    specialization:Specialization
) {
    var outputs = cloneVariables(specialization.results);
    addSpecializedNode(s, SpecializedCall(ASTNode(ast), inputs, outputs, specialization));
    return outputs;
}

addSpecializedPhi(
    s:SpecializationContext,
    branches:Vector[VariableList],
    outputs:VariableList
) {
    addSpecializedNode(s, SpecializedPhi(branches, outputs));
    return Maybe(outputs);
}

singleAddSpecializedBindReturn(
    s:SpecializationContext,
    ast,
    input:RTVariable,
    output:RTVariable
) {
    addSpecializedNode(s, SpecializedBindReturn(ASTNode(ast), input, output));
    return output;
}

addSpecializedBind(
    s:SpecializationContext,
    ast,
    input:RTVariable,
    output:RTVariable
) {
    addSpecializedNode(s, SpecializedBind(ASTNode(ast), input, output));
    return Maybe(VariableList(output));
}

addSpecializedInitialize(
    s:SpecializationContext,
    ast,
    input:RTVariable,
    output:RTVariable
) {
    addSpecializedNode(s, SpecializedInitialize(ASTNode(ast), input, output));
    return Maybe(VariableList(output));
}


//
// matchOverload
//

[Arg, S | Sequence?(S)]
arityMatches?(formalArgs:ast.VariadicList[Arg], args:S) {
    if (nothing?(formalArgs.variadicItem))
        return size(args) == size(formalArgs.items);
    else
        return size(args) >= size(formalArgs.items);
}

private referenceSpecifier(x:ast.Argument) ast.ArgumentKind {
    return maybe(x.kind, y => y, () => ast.ARG_CONST);
}

private referenceKindMatches?(formalKind:ast.ArgumentKind, kind:RTVariableKind) {
    if (formalKind == ast.ARG_REF)
        return kind == RT_VARIABLE_REF;
    if (formalKind == ast.ARG_RVALUE)
        return kind == RT_VARIABLE_RVALUE;
    return true;
}

private loweredArgumentKind(formalKind:ast.ArgumentKind, kind:RTVariableKind) {
    if (formalKind == ast.ARG_CONST)
        return RT_VARIABLE_CONST;
    return kind;
}

private unifyArgumentType(formalArg:ast.Argument, arg:RTVariable, env) {
    return maybe(formalArg.type,
        type ref=> {
            var typePattern = evaluatePattern(type, env);
            return unify(typePattern, arg.type);
        },
        () => true,
    );
}

private unifyVariadicArgumentType(formalArg, args, env) {
    return maybe(formalArg.type,
        type => {
            var listCell = evaluateListPatternCell(type, env);
            return unifyList(listCell, mapped(a => ref a.type, args));
        },
        () => true,
    );
}

private unifyArgument(s:Specialization, formalArg:ast.Argument, arg:RTVariable) {
    var formalKind = referenceSpecifier(formalArg);
    if (not referenceKindMatches?(formalKind, arg.kind))
        return false;
    if (not unifyArgumentType(formalArg, arg, s.env))
        return false;
    var loweredArgKind = loweredArgumentKind(formalKind, arg.kind);
    var v = RTVariable(loweredArgKind, arg.type, false, formalArg.name.str);
    envAdd(s.env, formalArg.name, v);
    push(s.args, v);
    return true;
}

private unifyVariadicArgument(s:Specialization, formalArg:ast.Argument, args) {
    var formalKind = referenceSpecifier(formalArg);
    var vars = VariableList();
    for (arg in args) {
        if (not referenceKindMatches?(formalKind, arg.kind))
            return false;
        var loweredArgKind = loweredArgumentKind(formalKind, arg.kind);
        var v = RTVariable(loweredArgKind, arg.type);
        push(vars, v);
        push(s.args, v);
    }
    if (not unifyVariadicArgumentType(formalArg, args, s.env))
        return false;

    var listVar = RTListVariable(vars);
    envAdd(s.env, formalArg.name, listVar);

    return true;
}

private unifyArguments(s:Specialization, formalArgs, args) {
    for (formalArg, arg in zipped(formalArgs.items, args)) {
        if (not unifyArgument(s, formalArg, arg))
            return false;
    }
    var restArgs = slicedFrom(args, size(formalArgs.items));
    return maybe(formalArgs.variadicItem,
        formalArg ref=> unifyVariadicArgument(s, formalArg, restArgs),
        () => true,
    );
}

private bindNamedSingleReturn(s:Specialization, name, returnSpec) {
    var type = evaluateExpr(returnSpec.type, s.env);
    if (just?(returnSpec.kind))
        type = pointerType(type);
    var v = RTVariable(RT_VARIABLE_REF, type, false, name);
    push(s.internalNamedResults, v);
    envAdd(s.env, name, v);
}

private bindNamedMultiReturn(s:Specialization, name, returnSpec) {
    var types = evaluateMultiExpr(returnSpec.type, s.env);
    var ref? = just?(returnSpec.kind);

    var vars = map(
        type ref=> RTVariable(RT_VARIABLE_REF, if (ref?) pointerType(type) else type),
        types
    );

    var v = RTListVariable(vars, name);
    envAdd(s.env, name, v);

    push(s.internalNamedResults, move(vars));
}

private bindNamedReturn(s:Specialization, name, returnSpec) {
    switch (returnSpec.arityKind) {
    case ast.RETURN_SINGLE:
        bindNamedSingleReturn(s, name, returnSpec);
        break;
    case ast.RETURN_MULTI:
        bindNamedMultiReturn(s, name, returnSpec);
        break;
    default:
        assert(false);
        break;
    }
}

private bindNamedReturns(s:Specialization) {
    ref ast = s.matchedOverload.ast;
    maybe(ast.definition.returnedNames,
        names ref=> {
            for (name, returnSpec in zipped(names, ast.definition.returns))
                bindNamedReturn(s, name, returnSpec);
        },
        () => {},
    );
}

matchOverload(o:Overload, callable:Value, args:VariableList) Maybe[Specialization] {
    ref formalArgs = o.ast.definition.args;
    if (not arityMatches?(formalArgs, args))
        return nothing(Specialization);
    var pvars = topLevelItemPatternVars(o);
    var s = Specialization(o, callable);
    s.env = initializeMatch(o.module, pvars);
    var callablePattern = evaluatePattern(o.ast.target, s.env);
    if (not unify(callablePattern, callable))
        return nothing(Specialization);
    if (not unifyArguments(s, formalArgs, args))
        return nothing(Specialization);
    assert(size(s.args) == size(args));
    bindNamedReturns(s);
    verifyPatternVars(pvars, s.env);
    if (not testPredicate(o.ast.definition.predicate, s.env))
        return nothing(Specialization);
    return Maybe(s);
}


//
// analyzeCallable helpers
//

private findOverload(program, callable, args) {
    for (x in reversed(program.allOverloads)) {
        var result = matchOverload(x, callable, args);
        if (not nothing?(result))
            return just(move(result));
    }
    genericError("no matching operation");
    return Specialization();
}

private returnSpecKind(returnSpec: ast.ReturnSpec) {
    return ...maybe(returnSpec.kind,
        k => {
            switch (k) {
            case ast.RETURN_CONST: return RT_VARIABLE_CONST, false;
            case ast.RETURN_REF: return RT_VARIABLE_REF, false;
            case ast.RETURN_RVALUE: return RT_VARIABLE_RVALUE, false;
            default :
                assert(false);
                return RTVariableKind(), false;
            }
        },
        () => ...(RT_VARIABLE_RVALUE, true),
    );
}

private computeReturnSpec(returns:Vector[ast.ReturnSpec], env) {
    // XXX handle noreturn syntax here
    var results = VariableList();
    for (returnSpec in returns) {
        if (returnSpec.arityKind == ast.RETURN_SINGLE) {
            var kind, value? = ...returnSpecKind(returnSpec);
            var type = evaluateExpr(returnSpec.type, env);
            push(results, RTVariable(kind, type, value?));
        }
        else {
            assert(returnSpec.arityKind == ast.RETURN_MULTI);
            var kind, value? = ...returnSpecKind(returnSpec);
            var types = evaluateMultiExpr(returnSpec.type, env);
            for (type in types)
                push(results, RTVariable(kind, type, value?));
        }
    }
    return Maybe(move(results));
}

private analyzeCallableReturns(s:Specialization, env, o:Overload) {
    if (not empty?(o.ast.definition.returns))
        s.results = computeReturnSpec(o.ast.definition.returns, env);
    else {
        analyzeCallableBody(s, env, o);
    }
}

private analyzeCallableBody(s:Specialization, env, o:Overload) {
    s.body = Maybe(Vector[SpecializedNode]());
    try {
        withLocation(o.ast.definition, () ref=> {
            var declaredReturns? = not empty?(o.ast.definition.returns);
            var namedReturns? = just?(o.ast.definition.returnedNames);

            var complete? = true;
            var context = SpecializationContext(&just(s.body), &complete?);

            var analyzedResults = match(o.ast.definition.body,
                ast.ExprCodeBody, body ref=> analyzeNode(context, env, body.exprs),
                ast.LiteralCodeBody, body ref=> {
                    if (declaredReturns? and not namedReturns?)
                        genericError("literal procedure must use named return values");
                    return analyzeLiteralCodeBody(context, env, body);
                },
                ast.Block, body ref=> analyzeBlockCodeBody(context, env, body),
            );

            if (not complete?)
                s.body = nothing(Vector[SpecializedNode]);

            if (declaredReturns?) {
                // if returns are named, the body should be void
                var expectedResults =
                    if (namedReturns?)
                        Maybe(VariableList())
                    else
                        s.results;
                // declared returns,  actually returns  => return types must match
                // declared returns,  actually noreturn => ok
                // declared noreturn, actually returns  => error
                maybe(analyzedResults, ar ref=> {
                    maybe(expectedResults,
                        er ref=> {
                            if (not equalResults?(ar, er))
                                genericError("procedure declared with different return types from what the body returns");
                        },
                        () ref=> {
                            genericError("procedure declared as noreturn but the body returns");
                        },
                    );
                });
            }
            s.results = analyzedResults;
        });
    } catch (ex) {
        s.body = nothing(Vector[SpecializedNode]);
        throw ex; 
    }
}

private entrySpecialization(entry:InvokeEntry) {
    return maybe(entry.specialization,
        x => x,
        () => {
            recursiveAnalysisError();
            return Specialization();
        },
    );
}

private callableInvokeTable(callable:Value) {
    return ref match(callable,
        Symbol, x => ref x.invokeTable,
        SymbolHead, x => ref x.invokeTable,
        MatchDefault, x => {
            genericError("invalid runtime callable: ", x);
            return ref typeToLValue(InvokeTable);
        },
    );
}


//
// analyzeCallable[WithOneResult]
//

analyzeCallableWithOneResult(callable:Value, args:VariableList) {
    var spec = analyzeCallable(callable, args);
    maybe(spec.results, results ref=> {
        ensureArity(1, size(results));
    });
    return spec;
}

analyzeCallable(callable:Value, args:VariableList) Specialization
    = withCallAnalysis(callable, args, () ref=> analyzeCallable2(callable, args));

analyzeCallable2(callable:Value, args:VariableList) Specialization {
    ref invokeTable = callableInvokeTable(callable);
    var entryPtr = lookup(invokeTable, ArgumentList(args));
    if (not null?(entryPtr))
        return entrySpecialization(entryPtr^);
    invokeTable[ArgumentList(args)] = InvokeEntry(nothing(Specialization));
    var specialization = findOverload(getProgram(), callable, args);
    ref loweredArgs = specialization.args;
    var lowered? = not equalArguments?(loweredArgs, args);
    if (lowered?) {
        var entryPtr = lookup(invokeTable, ArgumentList(loweredArgs));
        if (not null?(entryPtr)) {
            var s = entrySpecialization(entryPtr^);
            invokeTable[ArgumentList(args)].specialization = Maybe(s);
            return s;
        }
        invokeTable[ArgumentList(loweredArgs)] = InvokeEntry(nothing(Specialization));
    }
    try {
        alias s = specialization;
        analyzeCallableReturns(s, s.env, s.matchedOverload);
    }
    catch (e) {
        remove(invokeTable, ArgumentList(args));
        if (lowered?)
            remove(invokeTable, ArgumentList(loweredArgs));
        throw e;
    }
    invokeTable[ArgumentList(args)].specialization = Maybe(specialization);
    if (lowered?)
        invokeTable[ArgumentList(loweredArgs)].specialization = Maybe(specialization);
    return specialization;
}


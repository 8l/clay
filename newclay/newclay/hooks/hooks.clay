
import newclay.ast as ast;
import newclay.core.*;
import newclay.evaluator.*;


//
// initializeClayHooks
//

initializeClayHooks() {
    var program = getProgram();
    var module = mainModule(program);
    var lookup = name => envEntryToValue(publicLookup(module, name));
    program.hooks = ClayHooks(
        lookup("TypeTemplate?"),
        lookup("Type?"),

        lookup("ToLowLevelType"),
        lookup("LLSigned"),
        lookup("LLUnsigned"),
        lookup("LLBoolType"),
        lookup("LLIntegerType"),
        lookup("LLFloatType"),
        lookup("LLPointerType"),
        lookup("LLArrayType"),
        lookup("LLTupleType"),
        lookup("LLUnionType"),

        lookup("Bool"),
        lookup("Int"),
        lookup("Static"),
        lookup("Pointer"),

        lookup("Procedure?"),
    );
}



//
// symbolHeadTypeTemplate?, symbolType?, symbolProcedure?
//

symbolHeadTypeTemplate?(head:SymbolHead) {
    return safeInitialize(head.typeTemplate?,
        () ref=> evaluateBoolCall(getHooks().TypeTemplate?, ValueList(head)),
    );
}

symbolType?(x:Symbol) {
    return safeInitialize(x.type?,
        () ref=> evaluateBoolCall(getHooks().Type?, ValueList(x)),
    );
}

symbolProcedure?(x:Symbol) {
    return safeInitialize(x.procedure?,
        () ref=> evaluateBoolCall(getHooks().Procedure?, ValueList(x)),
    );
}



//
// typeSymbol
//

typeSymbol(type:Value) Symbol {
    return match(type,
        Symbol, y => y,
        MatchDefault, y => {
            genericError("invalid type: ", y);
            return Symbol();
        },
    );
}



//
// boolType, intType
//

boolType() = getHooks().Bool;
intType() = getHooks().Int;



//
// staticType, staticType?, staticTypeValue
//

staticType(x:Value) {
    return evaluateIndexing(getHooks().Static, ValueList(x));
}

staticType?(x:Value) {
    return match(x,
        Symbol, y => (Value(y.head) == getHooks().Static),
        MatchDefault, y => false,
    );
}

staticTypeValue(x:Value) {
    var sym = typeSymbol(x);
    if (Value(sym.head) != getHooks().Static)
        genericError("not a static type: ", x);
    assert(size(sym.params) == 1);
    return sym.params[0];
}



//
// pointerType, pointerType?, pointerTargetType
//

pointerType(x:Value) Value {
    return evaluateIndexing(getHooks().Pointer, ValueList(x));
}

pointerType?(x:Value) {
    return match(x,
        Symbol, y => (Value(y.head) == getHooks().Pointer),
        MatchDefault, y => false,
    );
}

pointerTargetType(x:Value) Value {
    var sym = typeSymbol(x);
    if (Value(sym.head) != getHooks().Pointer)
        genericError("not a pointer type: ", x);
    assert(size(sym.params) == 1);
    return sym.params[0];
}


import maybe.*;
import newclay.common.*;
import newclay.lexer.(SourceTokens);
import newclay.ast as ast;
import newclay.diagnostics.*;
import newclay.ir as ir;



//
// Program
//

record ModuleName (path: Vector[String]);
alias ModuleMap = HashMap[ModuleName, Module];

[...S | countValues(...S) >= 1 and allValues?(String?, ...S)]
overload ModuleName(...components: S)
    = ModuleName(Vector[String](...mapValues(String, ...components)));

overload ModuleName(astName: Vector[ast.Identifier])
    = ModuleName(map(x => x.str, astName));

record Program = referenceType(
    modules: ModuleMap,
    moduleSearchPath: Vector[String],
    allAttributes: Vector[Attribute],
    allStaticOverloads: Vector[StaticOverload],
    allOverloads: Vector[Overload],
    hooks: ClayHooks,
    lowLevelProgram: ir.Program,
    lowLevelNames: NameAllocator,
);

overload Program() = Program(
    ModuleMap(), Vector[String](),
    Vector[Attribute](), Vector[StaticOverload](), Vector[Overload](),
    ClayHooks(),
    ir.newProgram(),
    NameAllocator(),
);

var mainModuleName = ModuleName("__main__");
var primitivesModuleName = ModuleName("__primitives__");
var preludeModuleName = ModuleName("prelude");

moduleNameString(name:ModuleName) = join('.', name.path);
overload printTo(stream, name:ModuleName) { printTo(stream, moduleNameString(name)); }
mainModule(program:Program) = ref program.modules[mainModuleName];
primitivesModule(program:Program) = ref program.modules[primitivesModuleName];

record ClayHooks (
    TypeTemplate?: Value,
    Type?: Value,

    ToLowLevelType: Value,
    LLSigned: Value,
    LLUnsigned: Value,
    LLBoolType: Value,
    LLIntegerType: Value,
    LLFloatType: Value,
    LLPointerType: Value,
    LLArrayType: Value,
    LLTupleType: Value,
    LLUnionType: Value,

    Bool: Value,
    Int: Value,
    Static: Value,
    Pointer: Value,

    Procedure?: Value,
);



//
// Module
//

record Module = referenceType(
    program: Program,
    name: ModuleName,
    ast: ast.SourceAST,
    publicEnv: Env,
    privateEnv: Env,
);

overload Module(program:Program, name:ModuleName) =
    Module(program, name, ast.SourceAST(), Env(), Env());

overload printTo(stream, mod:Module) {
    printTo(stream, "module ", moduleNameString(mod.name));
}


//
// top-level item entries
//

record SymbolHead = referenceType(
    ast: ast.Symbol,
    module: Module,
    symbols: HashMap[ValueList, Symbol],
    typeTemplate?: TriState[Bool],
    invokeTable: InvokeTable,
);

overload SymbolHead(x:ast.Symbol, module:Module) =
    SymbolHead(
        x, module, HashMap[ValueList, Symbol](),
        TriState[Bool](), InvokeTable(),
    );

symbolHeadHasParams?(x:SymbolHead) {
    ref p = x.ast.params;
    return not (empty?(p.items) and nothing?(p.variadicItem));
}

record Symbol = referenceType(
    head: SymbolHead,
    params: ValueList,
    env: Env,
    body: TriState[Value],
    attributes: TriState[Value],
    type?: TriState[Bool],
    procedure?: TriState[Bool],
    invokeTable: InvokeTable,
    lowLevelType: Maybe[ir.Type],
);

overload Symbol(head:SymbolHead, params:ValueList, env:Env) =
    Symbol(
        head, params, env,
        TriState[Value](), TriState[Value](),
        TriState[Bool](), TriState[Bool](),
        InvokeTable(),
        nothing(ir.Type),
    );

record PatternVars = referenceType(
    vars: Vector[ast.Identifier],
    variadicVars: Vector[ast.Identifier],
);

record Attribute = referenceType(
    ast: ast.Attribute,
    module: Module,
    patternVars: Maybe[PatternVars],
);

overload Attribute(x:ast.Attribute, module:Module) =
    Attribute(x, module, nothing(PatternVars));

record StaticOverload = referenceType(
    ast: ast.StaticOverload,
    module: Module,
    patternVars: Maybe[PatternVars],
);

overload StaticOverload(x:ast.StaticOverload, module:Module) =
    StaticOverload(x, module, nothing(PatternVars));

record Overload = referenceType(
    ast: ast.Overload,
    module: Module,
    patternVars: Maybe[PatternVars],
);

overload Overload(x:ast.Overload, module:Module) =
    Overload(x, module, nothing(PatternVars));



//
// primitives
//

enum PrimitiveKind {
    PRIM_StaticInt?,
    PRIM_StaticAdd,
    PRIM_StaticSubtract,
    PRIM_StaticMultiply,
    PRIM_StaticDivide,
    PRIM_StaticRemainder,
    PRIM_StaticIntegers,

    PRIM_StaticBool?,
    PRIM_StaticEquals?,
    PRIM_StaticLesser?,

    PRIM_StaticString?,
    PRIM_StaticStringSize,
    PRIM_StaticStringSlice,
    PRIM_StaticStringConcat,

    PRIM_StaticTuple?,
    PRIM_StaticTuple,
    PRIM_StaticTupleSize,
    PRIM_StaticTupleElement,
    PRIM_StaticTupleElements,

    PRIM_StaticSymbol?,
    PRIM_StaticSymbolBody,
    PRIM_StaticSymbolAttributes,

    PRIM_StaticModuleMember,
}

record Primitive = referenceType(
    kind: PrimitiveKind,
);

primitiveName(kind) {
    var s = printString(kind);
    assert(beginsWith?(s, "PRIM_"));
    return sliceFrom(s, size("PRIM_"));
}



//
// environment
//

variant EnvEntry = Value
                 | ValueList
                 | PatternCell
                 | ListPatternCell
                 | RTVariable
                 | RTListVariable
                 ;
 
overload EnvEntry() = EnvEntry(Value());

variant EnvParent = EnvRoot | Env | Module;

record AmbiguousEntry (modules: Vector[Tuple[Module, EnvEntry]]);

variant EnvRootEntry = EnvEntry | AmbiguousEntry;
overload EnvRootEntry() = EnvRootEntry(AmbiguousEntry());

record EnvImport(visibility: ast.VisibilityKind, module: Module, name: String);
overload printTo(stream, imp:EnvImport) {
    if (imp.visibility == ast.VIS_PRIVATE)
        printTo(stream, "private definition ");
    printTo(stream, imp.name, " from module ", imp.module.name);
}

variant SpecificImport = EnvImport
                       | ModuleValue;
overload SpecificImport() = SpecificImport(EnvImport());

overload EnvRootEntry(imp: EnvImport) {
    switch (imp.visibility) {
    case ast.VIS_PRIVATE:
        return EnvRootEntry(privateLookup(imp.module, imp.name));
    case ast.VIS_PUBLIC:
        return EnvRootEntry(publicLookup(imp.module, imp.name));
    default:
        genericError("invalid visibility");
        return EnvRootEntry();
    }
}

overload EnvRootEntry(mv: ModuleValue) = EnvRootEntry(EnvEntry(Value(mv)));

record EnvRoot = referenceType(
    generalImports: HashMap[ModuleName, Module],
    specificImports: HashMap[String, SpecificImport],
    entries: Maybe[HashMap[String, EnvRootEntry]],
);

private resolveGeneralEnvRootEntry(entries, entryName, entryList) {
    switch (size(entryList)) {
    case 0:
        return;
    case 1:
        entries[entryName] = EnvRootEntry(entryList[0].1);
        return;
    default:
        entries[entryName] = EnvRootEntry(AmbiguousEntry(entryList));
        return;
    }
}

private resolveGeneralEnvRootEntries(entries, generalImports) {
    var generalEntries = HashMap[String, Vector[Tuple[Module, EnvEntry]]]();

    for (name, module in items(generalImports)) {
        for (entryName, entry in items(module.publicEnv.entries))
            push(generalEntries[entryName], (module, entry));
    }

    for (entryName, entryList in items(generalEntries))
        resolveGeneralEnvRootEntry(entries, entryName, entryList);

    for (name, module in items(generalImports)) {
        ref rootEntries = envRootEntries(envRoot(module.publicEnv));
        for (entryName, entry in items(rootEntries)) {
            if (null?(lookup(entries, entryName))) {
                entries[entryName] = entry;
            }
        }
    }
}

private resolveSpecificEnvRootEntries(entries, specificImports) {
    for (entryName, entry in items(specificImports))
        entries[entryName] = EnvRootEntry(*entry);
}

envRootEntries(envRoot: EnvRoot) = ref maybe(envRoot.entries,
    entries ref=> ref entries,
    () ref=> {
        envRoot.entries = Maybe(HashMap[String, EnvRootEntry]());
        ref entries = just(envRoot.entries);
        resolveGeneralEnvRootEntries(entries, envRoot.generalImports);
        resolveSpecificEnvRootEntries(entries, envRoot.specificImports);
        return ref entries;
    },
);

record Env = referenceType(
    parent: EnvParent,
    entries: HashMap[String, EnvEntry],
);

overload Env(parent:EnvParent) =
    Env(parent, HashMap[String, EnvEntry]());

newEnv(parent:EnvParent) = Env(parent);
[E | VariantMember?(EnvParent, E)]
overload newEnv(parent:E) = Env(EnvParent(parent));
overload newEnv() = Env(EnvParent(allocateRef(EnvRoot)));



//
// envRoot, envAdd, publicLookup, privateLookup
//

envRoot(env:EnvRoot) EnvRoot = env;
overload envRoot(env:Env) EnvRoot = envRoot(*env.parent);
overload envRoot(env:Module) EnvRoot {
    assert(false);
    return envRoot(env.privateEnv);
}
overload envRoot(env:EnvParent) EnvRoot = envRoot(*env);

envAdd(env, name, forward entry) {
    if (contains?(env.entries, name.str))
        genericError("name redefinition: ", name.str);
    env.entries[name.str] = EnvEntry(entry);
}

private envRootReset(envRoot) {
    if (just?(envRoot.entries))
        envRoot.entries = nothing(HashMap[String, EnvRootEntry]);
}

envRootAddGeneral(envRoot:EnvRoot, module:Module) {
    if (not null?(lookup(envRoot.generalImports, module.name)))
        genericError("redundant import of module ", module.name);
    envRoot.generalImports[module.name] = module;
    envRootReset(envRoot);
}

[I | VariantMember?(SpecificImport, I)]
envRootAddSpecific(envRoot:EnvRoot, asName:String, entry:I) {
    envRootAddSpecific(envRoot, asName, SpecificImport(entry));
}
overload envRootAddSpecific(envRoot:EnvRoot, asName:String, entry:SpecificImport) {
    var specImport = lookup(envRoot.specificImports, asName);
    if (null?(specImport))
        envRoot.specificImports[asName] = entry;
    else
        genericError("import of ", entry, " as ", asName, " conflicts with import of ", specImport^, " as ", asName);

    envRootReset(envRoot);
}

private _ifEnvLookup(_ifParentLookup, fromEnv:Maybe[Env], env:Env, name:String, foundFn, notFoundFn) {
    if (just?(fromEnv) and env == just(fromEnv))
        return forward notFoundFn();

    var ptr = lookup(env.entries, name);
    if (null?(ptr)) {
        var newFromEnv = maybe(fromEnv,
            env ref=> fromEnv,
            ()  ref=> if (variantIs?(env.parent, EnvRoot)) Maybe(env) else nothing(Env)
        );
        return forward _ifParentLookup(newFromEnv, env.parent, name, foundFn, notFoundFn);
    }
    return forward foundFn(ptr^);
}

private procedure _ifPublicLookup;

overload _ifPublicLookup(fromEnv:Maybe[Env], env:Env, name:String, foundFn, notFoundFn)
    = forward _ifEnvLookup(_ifPublicLookup, fromEnv, env, name, foundFn, notFoundFn);

[T] throwButPropagate(static T, ex) {
    if (true)
        throw ex;
    else
        return ref typeToLValue(T);
}

private envRootEntryLookup(fromEnv:Maybe[Env], name:String, entry:EnvEntry, foundFn, notFoundFn)
    = forward foundFn(entry);
[F | RValue?(typeToRValue(F)(typeToRValue(EnvEntry)))]
overload envRootEntryLookup(fromEnv:Maybe[Env], name:String, entry:AmbiguousEntry, foundFn:F, notFoundFn)
    = throwButPropagate(Type(foundFn(typeToRValue(EnvEntry))), AmbiguousEntryError(name, entry));
[F | LValue?(typeToRValue(F)(typeToRValue(EnvEntry)))]
overload envRootEntryLookup(fromEnv:Maybe[Env], name:String, entry:AmbiguousEntry, foundFn:F, notFoundFn)
    = ref throwButPropagate(Type(foundFn(typeToRValue(EnvEntry))), AmbiguousEntryError(name, entry));

overload _ifPublicLookup(fromEnv:Maybe[Env], envRoot:EnvRoot, name:String, foundFn, notFoundFn) {
    ref entries = envRootEntries(envRoot);
    var ptr = lookup(entries, name);
    if (null?(ptr))
        return forward notFoundFn();
    else
        return forward envRootEntryLookup(fromEnv, name, *ptr^, foundFn, notFoundFn);
}

overload _ifPublicLookup(fromEnv:Maybe[Env], module:Module, name:String, foundFn, notFoundFn)
    = forward _ifPublicLookup(fromEnv, module.publicEnv, name, foundFn, notFoundFn);

overload _ifPublicLookup(fromEnv:Maybe[Env], envParent:EnvParent, name:String, foundFn, notFoundFn)
    = forward _ifPublicLookup(fromEnv, *envParent, name, foundFn, notFoundFn);

ifPublicLookup(env, name:String, foundFn, notFoundFn)
    = forward _ifPublicLookup(nothing(Env), env, name, foundFn, notFoundFn);

publicLookup(env, name) = ref ifPublicLookup(env, name,
    entry ref=> ref entry,
    () ref=> ref throwButPropagate(EnvEntry, GenericError(printString("name not found: ", name))),
);
overload publicLookup(env, name:ast.Identifier)
    = forward publicLookup(env, name.str);
overload publicLookup(env, name:StringConstant)
    = forward publicLookup(env, String(name));

publicLookup?(env, name) = ifPublicLookup(env, name, entry => true, () => false);


private _ifPrivateLookup(fromEnv:Maybe[Env], env:Env, name:String, foundFn, notFoundFn)
    = forward _ifEnvLookup(_ifPrivateLookup, fromEnv, env, name, foundFn, notFoundFn);

overload _ifPrivateLookup(fromEnv, env:EnvRoot, name, foundFn, notFoundFn)
    = forward _ifPublicLookup(fromEnv, env, name, foundFn, notFoundFn);

overload _ifPrivateLookup(fromEnv, module:Module, name, foundFn, notFoundFn)
    = forward _ifPublicLookup(fromEnv, module.privateEnv, name,
        foundFn,
        () => forward _ifPublicLookup(fromEnv, module.publicEnv, name, foundFn, notFoundFn),
    );

overload _ifPrivateLookup(fromEnv, env:EnvParent, name, foundFn, notFoundFn)
    = forward _ifPrivateLookup(fromEnv, *env, name, foundFn, notFoundFn);

ifPrivateLookup(env, name:String, foundFn, notFoundFn)
    = forward _ifPrivateLookup(nothing(Env), env, name, foundFn, notFoundFn);

privateLookup(env, name) = ref ifPrivateLookup(env, name,
    entry ref=> ref entry,
    () ref=> ref throwButPropagate(EnvEntry, GenericError(printString("name not found: ", name))),
);
overload privateLookup(env, name:ast.Identifier)
    = forward privateLookup(env, name.str);
overload privateLookup(env, name:StringConstant)
    = forward privateLookup(env, String(name));

privateLookup?(env, name) = ifPrivateLookup(env, name, entry => true, () => false);


//
// ValueList
//

record ValueList(
    data: SharedPointer[Vector[Value]],
);

overload ValueList() = ValueList(new Vector[Value]());

[A | Sequence?(A) and (SequenceElementType(A) == Value)]
overload ValueList(forward a:A) = ValueList(new Vector[Value](a));

[...A | equalValues?(Value, ...A)]
callbyname overload ValueList(...elements:A) =
    ValueList(new Vector[Value](...elements));

[T | VariantMember?(Value, T)]
overload ValueList(forward x:T) = ValueList(Value(x));

overload size(x:ValueList) = size(x.data^);
overload index(x:ValueList, i) = ref index(x.data^, i);
overload begin(x:ValueList) = begin(x.data^);
overload end(x:ValueList) = end(x.data^);
overload iterator(x:ValueList) = iterator(x.data^);
overload push(x:ValueList, forward ...a) { push(x.data^, ...a); }
overload hash(x:ValueList) = hash(x.data^);
overload equals?(a:ValueList, b:ValueList) = equals?(a.data^, b.data^);
overload printTo(stream, x:ValueList) {
    printTo(stream, x.data^);
}



//
// Value
//

variant Value = BoolValue
              | IntValue
              | StringValue
              | TupleValue
              | LambdaValue
              | ModuleValue

              | SymbolHead
              | Symbol
              | Primitive
              ;

overload Value() = Value(BoolValue());

record BoolValue(value: Bool);
record IntValue(value: Int);
record StringValue(value: String);
record TupleValue(elements: ValueList);
record LambdaValue(value: ast.LambdaExpr, env: EnvParent);

[E | VariantMember?(EnvParent, E)]
overload LambdaValue(value: ast.LambdaExpr, env: E) = LambdaValue(value, EnvParent(env));


//
// ModuleValue
//

record ModuleValue (
    module: Maybe[Module],
    submodules: HashMap[String, ModuleValue],
);

overload equals?(m1: ModuleValue, m2: ModuleValue)
    = m1.module == m2.module;

overload ModuleValue(module:Module)
    = ModuleValue(Maybe(module), HashMap[String,ModuleValue]());
overload ModuleValue(module:Maybe[Module])
    = ModuleValue(module, HashMap[String,ModuleValue]());

qualifiedImportConflictError(path, name) {
    var pathString = join('.', path);
    genericError(
        "qualified import of module ", pathString, ".", name,
        " conflicts with definition ", name,
        " in module ", pathString
    );
}

private checkQualifiedImportConflicts(moduleValue, module) {
    for (name, m in items(moduleValue.submodules))
        if (publicLookup?(module, name))
            qualifiedImportConflictError(module.name.path, name);
}

addModuleValueSubmodule(moduleRef:ModuleValue, path:Vector[String], name:String, module:Maybe[Module]) {
    maybe(moduleRef.module, refModule => {
        if (publicLookup?(refModule, name))
            qualifiedImportConflictError(path, name);
    });
    ref submoduleRef = moduleRef.submodules[name];
    if (nothing?(submoduleRef.module)) {
        submoduleRef.module = module;
        maybe(module, justModule => {
            checkQualifiedImportConflicts(submoduleRef, justModule);
        });
    } else if (just?(module))
        genericError("conflicting qualified imports for module ", join('.', path), ".", name);
    return ref submoduleRef;
}

overload addModuleValueSubmodule(moduleRef:ModuleValue, path:Vector[String], name:String, module:Module)
    = ref addModuleValueSubmodule(moduleRef, path, name, Maybe(module));
overload addModuleValueSubmodule(moduleRef:ModuleValue, path:Vector[String], name:String)
    = ref addModuleValueSubmodule(moduleRef, path, name, nothing(Module));

private addRootModuleValue(envRoot, moduleName) {
    var moduleRefEntry = lookup(envRoot.specificImports, moduleName.path[0]);
    if (null?(moduleRefEntry)) {
        envRootAddSpecific(envRoot, moduleName.path[0], ModuleValue());
        moduleRefEntry = &envRoot.specificImports[moduleName.path[0]];
    } else if (not nestedVariantIs?(moduleRefEntry^, ModuleValue)) {
        genericError(
            "qualified import of module ", moduleName,
            " conflicts with import of definition ", moduleRefEntry^,
            " as ", moduleName.path[0],
        );
    }
    return ref nestedVariantAs(moduleRefEntry^, ModuleValue);
}

addQualifiedModuleValue(envRoot: EnvRoot, module:Module, moduleName:ModuleName) {
    assert(size(moduleName.path) > 0);

    if (size(moduleName.path) == 1) {
        ref moduleValue = addRootModuleValue(envRoot, moduleName);
        if (just?(moduleValue.module))
            genericError(
                "qualified import of module ", moduleName.path[0],
                " conflicts with import of module ", just(moduleValue.module).name,
                " as ", moduleName.path[0],
            );
        moduleValue.module = Maybe(module);
        checkQualifiedImportConflicts(moduleValue, module);
    } else {
        var moduleRef = &addRootModuleValue(envRoot, moduleName);

        var componentPath = Vector[String](moduleName.path[0]);

        var last = size(moduleName.path) - 1;
        for (component in sliced(moduleName.path, 1, last)) {
            moduleRef = &addModuleValueSubmodule(moduleRef^, componentPath, component);
            push(componentPath, component);
        }
        var basename = moduleName.path[last];
        addModuleValueSubmodule(moduleRef^, componentPath, basename, module);
    }
}


//
// print Value
//

overload printTo(stream, x:Value) {
    printTo(stream, *x);
}
overload printTo(stream, x:BoolValue) {
    printTo(stream, x.value);
}
overload printTo(stream, x:IntValue) {
    printTo(stream, x.value);
}
overload printTo(stream, x:StringValue) {
    printReprTo(stream, x.value);
}
overload printTo(stream, x:TupleValue) {
    printTo(stream, "(");
    var commaOut = CommaListPrinter(&stream);
    for (element in x.elements)
        newItem(commaOut, element);
    printTo(stream, ")");
}
overload printTo(stream, x:LambdaValue) {
    printTo(stream, "<lambda>");
}
overload printTo(stream, x:SymbolHead) {
    printTo(stream, x.ast.name.str);
}
overload printTo(stream, x:Symbol) {
    printTo(stream, x.head.ast.name.str);
    if (not empty?(x.params))
        printTo(stream, x.params);
}
overload printTo(stream, x:ModuleValue) {
    maybe(x.module,
        module ref=> { printTo(stream, "module ", module.name); },
        ()     ref=> { printTo(stream, "<incomplete module name>"); },
    );
}
overload printTo(stream, x:Primitive) {
    printTo(stream, primitiveName(x.kind));
}

[T | VariantMember?(Value, T)]
overload printReprTo(stream, x:T) {
    printTo(stream, x);
}

overload printReprTo(stream, x:Value) {
    printReprTo(stream, *x);
}



//
// Pattern
//

variant Pattern = Value
                | PatternCell
                | SymbolPattern
                ;
         
record PatternCell = referenceType(
    value: Maybe[Value]
);

record SymbolPattern = referenceType(
    head: SymbolHead,
    params: ListPattern,
);

overload Pattern() = Pattern(Value());



//
// ListPattern
//

record ListPattern = referenceType(
    patterns: Vector[Pattern],
    variadicPattern: ListPatternCell,
);

record ListPatternCell = referenceType(
    values: Maybe[ValueList],
);



//
// RTVariable, RTListVariable
//

enum RTVariableKind {
    RT_VARIABLE_CONST,
    RT_VARIABLE_REF,
    RT_VARIABLE_RVALUE,
}

record RTVariable = referenceType(
    kind: RTVariableKind,
    type: Value,
    lowLevelLocal: ir.Local,
);

overload RTVariable(kind:RTVariableKind, type:Value) =
    RTVariable(kind, type, ir.Local());

record RTListVariable = referenceType(
    kinds: Vector[RTVariableKind],
    types: ValueList,
    lowLevelLocals: Vector[ir.Local],
);

overload RTListVariable(forward kinds:Vector[RTVariableKind], forward types:ValueList) =
    RTListVariable(kinds, types, Vector[ir.Local]());



//
// RTResult, RTResultList
//

enum RTResultKind {
    RT_RESULT_CONST,
    RT_RESULT_REF,
    RT_RESULT_RVALUE,
    RT_RESULT_VALUE,
}

record RTResult (
    kind: RTResultKind,
    type: Value,
);

alias RTResultList = Vector[RTResult];

rtResultKindString(x:RTResultKind) {
    switch (x) {
    case RT_RESULT_CONST: return "const";
    case RT_RESULT_REF: return "ref";
    case RT_RESULT_RVALUE: return "rvalue";
    case RT_RESULT_VALUE: return "value";
    default : assert(false); return "";
    }
}

overload printReprTo(stream, x:RTResult) {
    printTo(stream, rtResultKindString(x.kind));
    printTo(stream, " ");
    printReprTo(stream, x.type);
}



//
// Specialization, InvokeTable
//

variant NamedReturn = RTVariable
                    | RTListVariable
                    ;

record Specialization = referenceType(
    matchedOverload: Overload,
    callable: Value,
    args: RTResultList,
    internalArgs: Vector[RTVariable],
    internalVariadicArg: Maybe[RTListVariable],
    bodyEnv: Env,
    namedReturns: Vector[NamedReturn],
    returns: RTResultList,
    lowLevelProc: Maybe[ir.Procedure],
);

overload Specialization(matchedOverload:Overload, callable:Value) {
    var s = allocateRef(Specialization);
    s.matchedOverload = matchedOverload;
    s.callable = callable;
    return move(s);
}

record InvokeEntry(
    specialization: Maybe[Specialization],
);

alias InvokeTable = HashMap[RTResultList, InvokeEntry];



//
// safeInitialize
//

[T]
safeInitialize(x:TriState[T], initializer) {
    return triStateInitialize(x, initializer, () => {
        genericError("recursion during initialization");
    });
}



//
// program stack
//

private var _programStack = Stack[Program]();

withProgram(program:Program, f) =
    forward ...withStackValue(_programStack, program, f);

getProgram() = ref stackTop(_programStack);
getProgramMainModule() = ref mainModule(getProgram());
getHooks() = ref getProgram().hooks;



//
// location stack
//

record LocationEntry (
    tokens: SourceTokens,
    where: SizeT,
);

private var _locationStack = Stack[LocationEntry]();

withLocation(node, f) {
    var x = ast.getLocation(node);
    if (null?(x.tokens) or empty?(x.tokenRange))
        return forward ...f();
    var entry = LocationEntry(x.tokens, x.mainToken);
    return forward ...withStackValue(_locationStack, entry, f);
}



//
// compilation stack
//

variant CompilationEntry = StaticCallEntry
                         | CallAnalysisEntry
                         | CallCompilationEntry
                         ;

record StaticCallEntry (
    callable: Value,
    args: ValueList,
);

record CallAnalysisEntry (
    callable: Value,
    args: RTResultList,
);

record CallCompilationEntry (
    callable: Value,
    args: RTResultList,
);

overload printTo(stream, x:StaticCallEntry) {
    printTo(stream, "eval: ");
    printReprTo(stream, x.callable);
    printTo(stream, "(");
    printReprElementsTo(stream, x.args);
    printTo(stream, ")");
}

overload printTo(stream, x:CallAnalysisEntry) {
    printTo(stream, "analyze: ");
    printReprTo(stream, x.callable);
    printTo(stream, "(");
    printReprElementsTo(stream, x.args);
    printTo(stream, ")");
}

overload printTo(stream, x:CallCompilationEntry) {
    printTo(stream, "compile: ");
    printReprTo(stream, x.callable);
    printTo(stream, "(");
    printReprElementsTo(stream, x.args);
    printTo(stream, ")");
}

private var _compilationStack = Stack[CompilationEntry]();

withCompilationEntry(forward entry:CompilationEntry, f) =
    forward ...withStackValue(_compilationStack, entry, f);

withStaticCall(callable:Value, args:ValueList, f) {
    var entry = CompilationEntry(StaticCallEntry(callable, args));
    return forward ...withCompilationEntry(move(entry), f);
}

withCallAnalysis(callable:Value, args:RTResultList, f) {
    var entry = CompilationEntry(CallAnalysisEntry(callable, args));
    return forward ...withCompilationEntry(move(entry), f);
}

withCallCompilation(callable:Value, args:RTResultList, f) {
    var entry = CompilationEntry(CallCompilationEntry(callable, args));
    return forward ...withCompilationEntry(move(entry), f);
}



//
// semantic errors
//

[T]
private SemanticError?(static T) = false;

private semanticError(...fields) = recordWithPredicate(
    SemanticError?,
    locationStack: Stack[LocationEntry],
    compilationStack: Stack[CompilationEntry],
    ...fields
);

[T, ...A | Record?(T)
           and SemanticError?(T)
           and (Tuple[...RecordFieldTypes(T)] ==
                Tuple[Stack[LocationEntry], Stack[CompilationEntry], ...A])]
callbyname overload T(...args:A) =
    T(_locationStack, _compilationStack, ...args);

variant SemanticError;

instance ClayError = SemanticError;

[T | VariantMember?(SemanticError, T)]
overload Exception(forward e:T) =
    Exception(SemanticError(e));

overload displayError(e:SemanticError) {
    displayError(*e);
}

displaySemanticError(locationStack, compilationStack, ...message) {
    if (stackEmpty?(locationStack)) {
        println(...message);
        return;
    }
    var location = stackTop(locationStack);
    var tokens = location.tokens;
    var offset = tokens.vector[location.where].range.begin;
    errorWithLocation(tokens.file, offset, ...message);
    println("context stack: ");
    for (entry in compilationStack)
        println("    ", entry);
}



//
// semantic error instances
//

record GenericError = semanticError(
    msg: String,
);

instance SemanticError = GenericError;

overload displayError(e:GenericError) {
    displaySemanticError(e.locationStack, e.compilationStack, e.msg);
}


record RecursiveAnalysisError = semanticError(
);

instance SemanticError = RecursiveAnalysisError;

overload displayError(e:RecursiveAnalysisError) {
    var msg = "recursion during type propagation";
    displaySemanticError(e.locationStack, e.compilationStack, msg);
}


record AmbiguousEntryError = semanticError(
    name: String,
    entry: AmbiguousEntry,
);

instance SemanticError = AmbiguousEntryError;

overload displayError(e:AmbiguousEntryError) {
    var msg = printString(
        "name ", e.name, " ambiguously imported from multiple modules; use \"import module.(name);\" form to disambiguate"
    );
    for (m in e.entry.modules)
        printTo(msg, "\n  ", m.0.name);
    displaySemanticError(e.locationStack, e.compilationStack, msg);
}


//
// error reporting helpers
//

genericError(...msg) {
    throw GenericError(printString(...msg));
}

recursiveAnalysisError() {
    throw RecursiveAnalysisError();
}

private valuesStr(n) =
    if (n == 1) "value" else "values";

arityError(expected, received) {
    genericError(
        "expected ", expected, " ", valuesStr(expected),
        ", but received ", received
    );
}

arityError2(minExpected, received) {
    genericError(
        "expected atleast ", minExpected, " ", valuesStr(minExpected),
        ", but received ", received
    );
}

ensureArity(expected, received) {
    if (expected != received)
        arityError(expected, received);
}

ensureArity2(minExpected, received) {
    if (minExpected > received)
        arityError2(minExpected, received);
}

typeError(expected, received) {
    genericError("expected type ", expected, ", but received ", received);
}

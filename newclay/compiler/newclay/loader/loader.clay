
import io.paths.*;
import maybe.*;
import newclay.common.*;
import newclay.lexer.*;
import newclay.ast as ast;
import newclay.ir as ir;
import newclay.ast.transforms.*;
import newclay.parser.*;
import newclay.core.*;
import newclay.resolver.*;

private loadPrimitivesModule(program:Program) {
    var module = Module(program, primitivesModuleName);
    primitivesModule(program) = module;
    module.privateEnv = newEnv();
    module.publicEnv = newEnv();
    for (i in range(EnumMemberCount(PrimitiveKind))) {
        var kind = PrimitiveKind(i);
        var name = ast.Identifier(primitiveName(kind));
        var entry = Primitive(kind);
        envAdd(module.publicEnv, name, Value(entry));
    }
    for (i in range(EnumMemberCount(AliasPrimitiveKind))) {
        var kind = AliasPrimitiveKind(i);
        var name = ast.Identifier(primitiveName(kind));
        var entry = AliasPrimitive(kind);
        envAdd(module.publicEnv, name, Value(entry));
    }
    return module;
}

private addImportItem(module, envRoot, importItem) {
    withLocation(importItem, () ref=> {
        var moduleName = ModuleName(importItem.name);
        var importedModule = programModule(module.program, moduleName);
        match(importItem.spec,
            ast.GeneralImportSpec,   genSpec ref=> {
                envRootAddGeneral(envRoot, importedModule);
            },
            ast.SpecificImportSpecs, specSpecs ref=> {
                for (spec in specSpecs) {
                    var asName = just(spec.asName, spec.name).str;
                    envRootAddSpecific(
                        envRoot,
                        asName,
                        EnvImport(spec.visibility, importedModule, spec.name.str)
                    );
                }
            },
            ast.QualifiedImportSpec, qualSpec ref=> {
                addQualifiedModuleValue(envRoot, importedModule, moduleName);
            },
            ast.AliasedImportSpec, aliasSpec ref=> {
                envRootAddSpecific(envRoot, aliasSpec.asName.str, ModuleValue(importedModule));
            },
        );
    });
}

private addSymbolItem(module, env, symbolItem) {
    withLocation(symbolItem, () ref=> {
        var name = symbolItem.name.name;
        if (not null?(lookup(module.publicEnv.entries, name.str))
            or not null?(lookup(module.privateEnv.entries, name.str)))
            genericError("symbol redefinition: ", name.str);

        var head = SymbolHead(symbolItem, module);

        push(module.program.allSymbolHeads, head);

        if (symbolHeadHasParams?(head)) {
            envAdd(env, name, Value(head));
        } else {
            var sym = Symbol(head, ValueList(), module);
            head.symbols[ValueList()] = sym;
            envAdd(env, name, Value(sym));
        }
    });
}

private addExternalItem(module, env, externalItem) {
    // XXX support external definitions as entry points
    withLocation(externalItem.body, () ref=> {
        if (not variantIs?(externalItem.body, ast.UndefinedCodeBody))
            genericError("external function definitions are not yet supported");
    });
    withLocation(externalItem, () ref=> {
        envAdd(env, externalItem.name,
            RTExternal(externalItem, module, externalItem.name.str, nothing(RTExternalAttributes))
        );
    });
}

private addGlobalItem(module, env, globalItem) {
    withLocation(globalItem, () ref=> {
        switch (globalItem.kind) {
        case ast.BINDING_RVALUE:
            genericError("global variables cannot be rvalues");
            break;
        case ast.BINDING_FORWARD:
            genericError("global variables cannot be forward");
            break;
        case ast.BINDING_ALIAS:
            addGlobalAlias(module, env, globalItem);
            break;
        case ast.BINDING_REF:
            // XXX
            genericError("global ref bindings not yet supported");
            break;
        case ast.BINDING_CONST:
            // XXX
            genericError("global const bindings not yet supported");
            break;
        default:
            addGlobalVariable(module, env, globalItem);
            break;
        }
    });
}

private addGlobalAlias(module, env, globalItem) {
    envAddFlatAlias(env, env, globalItem.bindings);
}

private addGlobalVariable(module, env, globalItem) {
    match(globalItem.bindings,
        ast.SingleBindingList, bindings ref=> {
            for (binding in bindings)
                envAdd(env, binding.name.name,
                    RTGlobal(binding, module, binding.name.name.str, RT_VALUE_REF)
                );
        },
        ast.MultipleBindingList, bindings ref=> {
            for (binding in bindings.names.items)
                envAdd(env, binding.name,
                    RTGlobal(bindings, module, binding.name.str, RT_VALUE_REF)
                );
            maybe(bindings.names.variadicItem, listGlobal ref=> {
                envAdd(env, listGlobal.name, RTListGlobal(bindings, module, listGlobal.name.str));
            });
        },
    );
}

callbyname ifPublic(item, publicValue, privateValue)
    = if (item.visibility == ast.VIS_PUBLIC) publicValue else privateValue;

private initializeGlobals(module) {
    module.publicEnv  = newEnv();
    module.privateEnv = newEnv();
    ref publicRoot  = envRoot(module.publicEnv);
    ref privateRoot = envRoot(module.privateEnv);

    envRootAddGeneral(privateRoot, programModule(module.program, preludeModuleName));

    for (item in module.ast.topLevelItems) {
        match(item,
            ast.Import, importItem ref=> {
                addImportItem(
                    module,
                    ifPublic(importItem, publicRoot, privateRoot),
                    importItem
                );
            },
            ast.Symbol, symbolItem ref=> {
                addSymbolItem(
                    module,
                    ifPublic(symbolItem, module.publicEnv, module.privateEnv),
                    symbolItem
                );
            },
            ast.ExternalProcedure, externalItem ref=> {
                addExternalItem(
                    module,
                    ifPublic(externalItem, module.publicEnv, module.privateEnv),
                    externalItem
                );
            },
            ast.GlobalBinding, globalItem ref=> {
                addGlobalItem(
                    module,
                    ifPublic(globalItem, module.publicEnv, module.privateEnv),
                    globalItem
                );
            },
            MatchDefault, x ref=> { },
        );
    }
}

private initializeAttributesAndOverloads(module) {
    for (item in module.ast.topLevelItems) {
        match(item,
            ast.Attribute, x ref=> {
                var y = Attribute(x, module);
                push(module.program.allAttributes, y);
            },
            ast.StaticOverload, x ref=> {
                var y = StaticOverload(x, module);
                push(module.program.allStaticOverloads, y);
            },
            ast.Overload, x ref=> {
                var y = Overload(x, module);
                push(module.program.allOverloads, y);
            },
            ast.GlobalLiteralCodeBody, x ref=> {
                push(module.program.allLiteralCodeBodies,
                    GlobalLiteralCode(x.codeBody.kind, ir.parseLiteralCodeBody(module, x.codeBody.code))
                );
            },
            MatchDefault, x ref=> { },
        );
    }
}

private initializeModule(module) {
    module.ast = transformAST(module.originalAST);
    initializeGlobals(module);
    initializeAttributesAndOverloads(module);
}

private moduleSourcePath(root, name) = joinPath(root, joinPath(name.path));

private findModuleSource(searchPath, name) {
    var path = String();
    var basename = name.path[size(name.path) - 1];
    for (root in searchPath) {
        path = moduleSourcePath(root, name);
        for (tryName in [
            path + ".clay",
            joinPath(path, basename + ".clay"),
        ])
            if (pathExists?(tryName))
                return move(tryName);
    }

    throw GenericError(printString(
        "could not find source file for module ", moduleNameString(name)
    ));
}

private loadModule(program, name) {
    ref module = program.modules[name];
    module = Module(program, name);
    module.originalAST = parse(tokenize(readSource(findModuleSource(program.moduleSearchPath, name))));
    initializeModule(module);
}

programModule(program:Program, name:ModuleName) {
    var ptr = lookup(program.modules, name);
    if (null?(ptr)) {
        loadModule(program, name);
        return ref program.modules[name];
    } else
        return ref ptr^;
}

private loadMainModule(program, mainFilename) {
    var module = Module(program, mainModuleName);
    mainModule(program) = module;
    module.originalAST = parse(tokenize(readSource(mainFilename)));
    initializeModule(module);
    return module;
}

private resolveProgram(program:Program) {
    for (name, module in items(program.modules))
        resolveAST(EnvParent(module), module.ast);
}

loadProgram(mainFilename:String, searchPath:Vector[String]) {
    var program = allocateRef(Program);
    program.moduleSearchPath = searchPath;
    loadPrimitivesModule(program);
    loadMainModule(program, mainFilename);
    resolveProgram(program);
    return program;
}

overload loadProgram(mainFilename) {
    var mainFilenameStr = String(mainFilename);
    return loadProgram(mainFilenameStr, Vector[String](pathDirname(mainFilenameStr)));
}

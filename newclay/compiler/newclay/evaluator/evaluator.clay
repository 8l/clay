
import maybe.*;
import newclay.ast as ast;
import newclay.ir as ir;
import newclay.analysis.*;
import newclay.core.*;
import newclay.hooks.*;
import newclay.diagnostics.(computeLocation);
import numbers.floats.*;
import printer.formatter.*;



//
// parseStringLiteral, parseCharLiteral
//

parseStringLiteral(s) {
    var result = String();
    assert(front(s) == '\"', "no quote opening string literal");
    assert(back(s) == '\"', "no quote closing string literal");
    var i = 1;
    while (i+1 < size(s)) {
        var c = s[i];
        if (c == '\\') {
            c, i = ...parseEscapeChar(s, i+1);
            push(result, c);
        }
        else {
            push(result, c);
            i += 1;
        }
    }
    return move(result);
}

parseCharLiteral(s) {
    assert(front(s) == '\'', "no apostrophe opening char literal");
    assert(back(s) == '\'', "no apostrophe closing char literal");
    assert(size(s) >= 3, "char literal too small");
    if (s[1] == '\\') {
        var c, i = ...parseEscapeChar(s, 2);
        assert(i == size(s) - 1, "junk characters after escape code character");
        return c;
    } else {
        assert(size(s) == 3, "junk characters after simple character");
        return s[1];
    }
}

private parseEscapeChar(s, i) {
    switch (s[i]) {
    case 'n': return '\n', i+1;
    case 'r': return '\r', i+1;
    case 't': return '\t', i+1;
    case 'a': return '\a', i+1;
    case 'b': return '\b', i+1;
    case 'f': return '\f', i+1;
    case 'v': return '\v', i+1;
    case '\\': return '\\', i+1;
    case '\'': return '\'', i+1;
    case '\"': return '\"', i+1;
    case 'x' : return parseHexChar(s[i+1], s[i+2]), i+3;
    }
    return parseOctalChar(s[i], s[i+1], s[i+2]), i+3;
}

private parseHexChar(a, b) {
    return Char(parseHexDigit(a)*16 + parseHexDigit(b));
}

private parseHexDigit(x) {
    if ((x >= '0') and (x <= '9'))
        return Int(x) - Int('0');
    if ((x >= 'a') and (x <= 'z'))
        return Int(x) - Int('a') + 10;
    if ((x >= 'A') and (x <= 'Z'))
        return Int(x) - Int('A') + 10;
    assert(false, "invalid hex digit: ", x);
    return -1;
}

private parseOctalChar(a, b, c) {
    return Char(
        parseOctalDigit(a)*8*8 + parseOctalDigit(b)*8 + parseOctalDigit(c)
    );
}

private parseOctalDigit(x) {
    if ((x >= '0') and (x <= '7'))
        return Int(x) - Int('0');
    assert(false, "invalid octal digit: ", x);
    return -1;
}


//
// parseIntLiteral, parseFloatLiteral
//

private parseNumberLiteral(s) {
}

parseIntLiteral(s) {
    var number = String();
    var suffix = String();

    for (i, c in enumerated(s)) {
        if (c == 'i' or c == 'u') {
            suffix = sliceFrom(s, i);
            break;
        }
        if (c != '_')
            push(number, c);
    }
    assert(size(number) > 0, "integer literal is empty");
    assert(size(suffix) <= 3, "integer suffix is too large");

    switch (suffix) {
    case "":
        return IntValue(StaticIntValue(Int64(number)));
    case "u64":
        return IntValue(UInt64(number));
    case "i64":
        return IntValue(Int64(number));
    case "u":
    case "u32":
        return IntValue(UInt32(number));
    case "i":
    case "i32":
        return IntValue(Int32(number));
    case "u16":
        return IntValue(UInt16(number));
    case "i16":
        return IntValue(Int16(number));
    case "u8":
        return IntValue(UInt8(number));
    case "i8":
        return IntValue(Int8(number));
    default:
        assert(false, "invalid integer literal suffix");
        return IntValue();
    }
}

parseHexFloatLiteral(number, suffix) {
    var neg? = false;
    var negExponent? = false;
    var mantissa = 0_u64;
    var mantissaBits = 0;
    var usedMantissaBits = 0;
    var exponentI = SizeT(0);
    var point = 0;
    var point? = false;

    for (i, c in enumerated(number)) {
        if (c == '-') {
            assert(i == 0, "junk before negative sign in hex float literal");
            neg? = true;
        } else if (c == '+') {
            assert(i == 0, "junk before positive sign in hex float literal");
            neg? = false;
        } else if (c == '.') {
            assert(not point?, "multiple decimal points in hex float literal");
            point? = true;
        } else if (c == 'p' or c == 'P') {
            assert(exponentI == 0, "multiple P markers in hex float literal");
            exponentI = i;
            break;
        } else if (c == 'x') {
            assert(mantissa == 0, "junk before hex designator in hex float literal");
            continue;
        } else {
            if (point?)
                point += 4;
            if (c == '0' and mantissa == 0)
                continue;
            mantissaBits += 4;
            if (c != '0')
                usedMantissaBits = mantissaBits;
            if (mantissaBits <= 64)
                mantissa = bitwiseOr(shiftLeft(mantissa, 4), parseHexDigit(c));
        }
    }

    assert(number[exponentI] == 'p' or number[exponentI] == 'P', "no exponent in hex float literal");

    var exponent = Int(sliceFrom(number, exponentI+1));
    var value = 0.0;

    if (mantissa == 0) {
        value = floatFromParts(neg?, 0, 0u64);
    } else {
        var mantissaLog = log2(mantissa);
        var mantissaExponent = mantissaLog
            + (if (mantissaBits > 64) mantissaBits - 64 else 0)
            - point
            + exponent
            + exponentBias(Double);

        mantissa = shiftLeft(mantissa, 60 - mantissaLog);
        var mantissaShift = 8 + max(0, 1-mantissaExponent);

        if (mantissaShift > 61) {
            mantissaExponent = 0;
            mantissa = 0_u64;
        } else if (mantissaExponent >= 2047) {
            mantissaExponent = 2047;
            mantissa = 0_u64;
        } else {
            var roundBit  = bitwiseAnd(mantissa, shiftLeft(1_u64, mantissaShift-1));
            var roundMask = bitwiseAnd(mantissa, shiftLeft(1_u64, mantissaShift-1) - 1);
            var evenBit   = bitwiseAnd(mantissa, shiftLeft(1_u64, mantissaShift));
            observe(hex(roundBit), hex(roundMask), hex(evenBit));

            mantissa = shiftRight(mantissa, mantissaShift);

            if (roundBit != 0 and (roundMask != 0 or usedMantissaBits > 60 or evenBit != 0))
                mantissa += 1_u64;

            if (mantissaExponent < 0)
                mantissaExponent = 0;
        }

        observe(number, neg?, mantissaExponent, rightAlignedWith(13, '0', hex(mantissa)));
        value = floatFromParts(neg?, mantissaExponent, mantissa);
    }

    switch (suffix) {
    case "":
    case "f64":
        return FloatValue(value);
    case "f":
    case "f32":
        return FloatValue(Float(value));
    default:
        assert(false, "invalid float suffix");
        return FloatValue();
    }
}

parseFloatLiteral(s) {
    var number = String();
    var suffix = String();
    var s2 = slicedFrom(s, 0);

    assert(size(s) > 0, "empty float literal");

    var sz = size(s);
    var hex? = false;

    if (s[sz - 1] == 'f') {
        suffix = sliceFrom(s, sz - 1);
        s2 = slicedUpto(s, sz - 1);
    } else if (sz > 3 and s[sz - 3] == 'f' and (
           (s[sz - 2] == '3' and s[sz - 1] == '2')
        or (s[sz - 2] == '6' and s[sz - 1] == '4')
    )) {
        suffix = sliceFrom(s, sz - 3);
        s2 = slicedUpto(s, sz - 3);
    }

    for (c in s2) {
        if (c != '_') {
            if (c == 'x')
                hex? = true;
            push(number, c);
        }
    }

    if (hex?)
        return parseHexFloatLiteral(number, suffix);
    else
        switch (suffix) {
        case "":
        case "f64":
            return FloatValue(Double(number));
        case "f":
        case "f32":
            return FloatValue(Float(number));
        default:
            assert(false, "invalid float suffix");
            return FloatValue();
        }
}


//
// bindArgs, bindPatternArgs
//

verifyBindingArity(formalArgs, args) {
    if (nothing?(formalArgs.variadicItem))
        ensureArity(size(formalArgs.items), size(args));
    else
        ensureArity2(size(formalArgs.items), size(args));
}

private lambdaArgName(arg) {
    if (just?(arg.kind))
        genericError("reference kind not allowed on lambda evaluated in static context");
    if (just?(arg.type))
        genericError("type not allowed on lambda evaluated in static context");
    return arg.name;
}

private addLambdaArg(env, formalArg:ast.BoundArgument, entry) {
    envAdd(env, lambdaArgName(formalArg), entry);
}
overload addLambdaArg(env, formalArg:ast.StaticArgument, entry) {
    genericError("static argument pattern not allowed on lambda evaluated in static context");
}

bindArgs(env, formalArgs, args) {
    verifyBindingArity(formalArgs, args);
    var env2 = newEnv(env);
    var nFixed = size(formalArgs.items);
    for (formalArg, arg in zipped(formalArgs.items, slicedUpto(args, nFixed)))
        addLambdaArg(env2, *formalArg, arg);
    maybe(formalArgs.variadicItem,
        formalArg ref=> {
            addLambdaArg(env2, *formalArg, ValueList(slicedFrom(args, nFixed)));
        },
        () => {}
    );
    return env2;
}

bindPatternArgs(env, formalArgs, args) {
    verifyBindingArity(formalArgs, args);
    var env2 = newEnv(env);
    var nFixed = size(formalArgs.items);
    for (name, arg in zipped(formalArgs.items, slicedUpto(args, nFixed)))
        envAdd(env2, name, PatternCell(Maybe(arg)));
    maybe(formalArgs.variadicItem,
        name ref=> {
            ref rest = slicedFrom(args, nFixed);
            envAdd(env2, name, ListPatternCell(Maybe(ValueList(rest))));
        },
        () => {}
    );
    return env2;
}



//
// symbols
//

makeSymbol(x:SymbolHead, params:ValueList) {
    var ptr = lookup(x.symbols, params);
    if (not null?(ptr)) {
        return ptr^;
    }
    ref y = x.symbols[params];
    var env = bindPatternArgs(x.module, x.ast.name.params, params);
    y = Symbol(x, params, env);
    return y;
}

symbolBody(x:Symbol) {
    return safeInitialize(x.body,
        () ref=> maybe(x.head.ast.expr,
            expr ref=> evaluateExpr(expr, newEnv(x.env)),
            () ref=> Value(TupleValue()),
        )
    );
}

symbolAttributes(x:Symbol) {
    return safeInitialize(x.attributes,
        () ref=> Value(TupleValue(evaluateAttributes(Value(x))))
    );
}


//
// evaluateMain
//

evaluateMain() {
    var mainRef = ast.Expr(ast.NameRef(ast.Identifier("Main")));
    var mainCall = ast.Expr(ast.Call(mainRef, ast.ExprList()));
    return evaluateMultiExpr(mainCall, newEnv(getProgramMainModule()));
}



//
// evaluateBool, evaluateBoolCall
//

evaluateBool(expr, env) {
    return withLocation(expr, () ref=> {
        var result = evaluateExpr(expr, env);
        if (not variantIs?(result, BoolValue))
            genericError("expecting a Bool value");
        return variantAs(result, BoolValue).value;
    });
}

evaluateBoolCall(callable:Value, args:ValueList) {
    // FIXME: what about error location?
    var results = evaluateStrictCall(callable, args);
    ensureArity(1, size(results));
    if (not variantIs?(results[0], BoolValue))
        genericError("expecting a Bool value");
    return variantAs(results[0], BoolValue).value;
}



//
// evaluateExpr
//

procedure evaluateExpr;

overload evaluateExpr(x:ast.Expr, env:Env) Value {
    return withLocation(x, () ref=> evaluateExpr(*x, env));
}
overload evaluateExpr(x:ast.BoolLiteral, env) {
    return Value(BoolValue(x.value));
}
overload evaluateExpr(x:ast.IntLiteral, env) {
    return Value(parseIntLiteral(x.value));
}
overload evaluateExpr(x:ast.StringLiteral, env) {
    return Value(StringValue(parseStringLiteral(x.value)));
}
overload evaluateExpr(x:ast.CharLiteral, env) {
    return Value(CharValue(parseCharLiteral(x.value)));
}
overload evaluateExpr(x:ast.FloatLiteral, env) {
    genericError("floating-point values not supported in static context");
    return Value();
}
overload evaluateExpr(x:ast.NameRef, env) {
    return envEntryToValue(nameRefLookup(env, x));
}
overload evaluateExpr(x:ast.SimpleNameRef, env) {
    return envEntryToValue(privateLookup(env, x.name));
}
overload evaluateExpr(x:ast.PatternNameRef, env) {
    return derefPattern(env, x.name);
}
overload evaluateExpr(x:ast.TupleExpr, env) {
    return Value(TupleValue(evaluateExprList(x.args, env)));
}
overload evaluateExpr(x:ast.ArrayExpr, env) {
    genericError("array literals not supported in static context");
    return Value();
}
overload evaluateExpr(x:ast.Call, env) {
    var callable = evaluateExpr(x.expr, env);
    return evaluateCallWithOneResult(callable, env, x);
}
overload evaluateExpr(x:ast.Indexing, env) {
    var indexable = evaluateExpr(x.expr, env);
    var args = evaluateExprList(x.args, env);
    return evaluateIndexing(indexable, args);
}
overload evaluateExpr(x:ast.StaticIndexing, env) {
    var indexable = evaluateExpr(x.expr, env);
    var index = Value(parseIntLiteral(x.index.value));
    return evaluateIndexing(indexable, ValueList(index));
}
overload evaluateExpr(x:ast.FieldRef, env) {
    var indexable = evaluateExpr(x.expr, env);
    var field = Value(StringValue(x.field.str));
    return evaluateFieldRef(indexable, field);
}
overload evaluateExpr(x:ast.NotExpr, env) {
    return Value(BoolValue(not evaluateBool(x.expr, env)));
}
overload evaluateExpr(x:ast.AndExpr, env) {
    if (evaluateBool(x.expr1, env))
        return evaluateExpr(x.expr2, env);
    else
        return Value(BoolValue(false));
}
overload evaluateExpr(x:ast.OrExpr, env) {
    if (evaluateBool(x.expr1, env))
        return Value(BoolValue(true));
    else
        return evaluateExpr(x.expr2, env);
}
overload evaluateExpr(x:ast.LambdaExpr, env) {
    return Value(LambdaValue(x, env));
}
overload evaluateExpr(x:ast.IfExpr, env) {
    if (evaluateBool(x.condition, env))
        return evaluateExpr(x.thenPart, env);
    else
        return evaluateExpr(x.elsePart, env);
}
overload evaluateExpr(x:ast.PairExpr, env) {
    var leftValue = Value(StringValue(x.name.str));
    var rightValue = evaluateExpr(x.expr, env);
    return Value(TupleValue(ValueList(leftValue, rightValue)));
}
overload evaluateExpr(x:ast.StaticExpr, env) {
    return evaluateExpr(x.expr, env);
}
overload evaluateExpr(x:ast.UnaryOpExpr, env) {
    var operands = ValueList(evaluateExpr(x.expr, env));
    switch (x.operator) {
    case ast.OP_PLUS:
        return evaluatePrimitiveWithOneResult(PRIM_StaticPlus, operands);
    case ast.OP_MINUS:
        return evaluatePrimitiveWithOneResult(PRIM_StaticMinus, operands);
    default:
        genericError("operator not allowed in static context");
        return Value();
    }
}

overload evaluateExpr(x:ast.BinaryOpExpr, env) {
    var operands = ValueList(evaluateExpr(x.expr1, env), evaluateExpr(x.expr2, env));

    switch (x.operator) {
    case ast.OP_MULTIPLY:
        return evaluatePrimitiveWithOneResult(PRIM_StaticMultiply, operands);
    case ast.OP_DIVIDE:
        return evaluatePrimitiveWithOneResult(PRIM_StaticDivide, operands);
    case ast.OP_REMAINDER:
        return evaluatePrimitiveWithOneResult(PRIM_StaticRemainder, operands);
    case ast.OP_ADD:
        return evaluatePrimitiveWithOneResult(PRIM_StaticAdd, operands);
    case ast.OP_SUBTRACT:
        return evaluatePrimitiveWithOneResult(PRIM_StaticSubtract, operands);
    case ast.OP_LESSER?:
        return evaluatePrimitiveWithOneResult(PRIM_StaticLesser?, operands);
    case ast.OP_LESSER_EQUALS?:
        return evaluatePrimitiveWithOneResult(PRIM_StaticLesserEquals?, operands);
    case ast.OP_GREATER?:
        return evaluatePrimitiveWithOneResult(PRIM_StaticGreater?, operands);
    case ast.OP_GREATER_EQUALS?:
        return evaluatePrimitiveWithOneResult(PRIM_StaticGreaterEquals?, operands);
    case ast.OP_EQUALS?:
        return evaluatePrimitiveWithOneResult(PRIM_StaticEquals?, operands);
    case ast.OP_NOT_EQUALS?:
        return evaluatePrimitiveWithOneResult(PRIM_StaticNotEquals?, operands);
    default:
        genericError("operator not allowed in static context");
        return Value();
    }
}


//
// envEntryToValue
//

procedure envEntryToValue;

overload envEntryToValue(x:EnvEntry) {
    return envEntryToValue(*x);
}
overload envEntryToValue(x:Value) {
    return x;
}
overload envEntryToValue(x:ValueList) {
    genericError("multi-value used in single value context");
    return Value();
}
overload envEntryToValue(x:PatternCell) {
    genericError("pattern variable cannot be used in non-pattern expression");
    return Value();
}
overload envEntryToValue(x:ListPatternCell) {
    genericError("pattern variable cannot be used in non-pattern expression");
    return Value();
}
overload envEntryToValue(x:Alias) {
    return evaluateExpr(x.value, x.env);
}
overload envEntryToValue(x:ListAlias) {
    genericError("multi-value used in single value context");
    return Value();
}
overload envEntryToValue(x:RTExternal) {
    genericError("external functions not available in static context");
    return Value();
}
overload envEntryToValue(x:RTGlobal) {
    genericError("non-alias global variables not available in static context");
    return Value();
}
overload envEntryToValue(x:RTListGlobal) {
    genericError("non-alias global variables not available in static context");
    return Value();
}
overload envEntryToValue(x:RTValue) {
    return rtTypeToValue(x.type);
}
overload envEntryToValue(x:RTValueList) {
    genericError("multi-value used in single value context");
    return Value();
}
overload envEntryToValue(x:RTLabel) {
    genericError("labels not available in static context");
    return Value();
}



//
// rtTypeToValue
//

rtTypeToValue(type) {
    if (not staticType?(type))
        genericError("run-time variable cannot be used at compile time");
    return staticTypeValue(type);
}



//
// evaluateExprList
//

evaluateExprList(exprList, env) {
    var results = ValueList();
    for (item in exprList)
        push(results, evaluateExprListItem(item, env));
    return move(results);
}

evaluateMultiExprList(exprList, env) {
    if (size(exprList) == 1 and variantIs?(exprList[0], ast.Expr))
        return evaluateMultiExpr(*exprList[0], env);
    return evaluateExprList(exprList, env);
}

procedure evaluateExprListItem;

overload evaluateExprListItem(x:ast.ExprListItem, env) {
    return evaluateExprListItem(*x, env);
}
overload evaluateExprListItem(x:ast.Expr, env) {
    return ValueList(evaluateExpr(x, env));
}
overload evaluateExprListItem(x:ast.UnpackExpr, env) {
    return evaluateMultiExpr(x.expr, env);
}



//
// evaluateMultiExpr
//

procedure evaluateMultiExpr;

overload evaluateMultiExpr(x, env:Env) {
    genericError("single valued expression used in multi-value context");
    return ValueList();
}
overload evaluateMultiExpr(x:ast.Expr, env:Env) ValueList {
    return withLocation(x, () ref=> evaluateMultiExpr(*x, env));
}
overload evaluateMultiExpr(x:ast.NameRef, env) {
    return envEntryToValueList(nameRefLookup(env, x));
}
overload evaluateMultiExpr(x:ast.SimpleNameRef, env) {
    return envEntryToValueList(privateLookup(env, x.name));
}
overload evaluateMultiExpr(x:ast.PatternNameRef, env) {
    return derefListPattern(env, x.name);
}
overload evaluateMultiExpr(x:ast.Call, env) {
    var callable = evaluateExpr(x.expr, env);
    return evaluateCall(callable, env, x);
}



//
// envEntryToValueList
//

procedure envEntryToValueList;

overload envEntryToValueList(x) {
    genericError("single value used in multi-valued context");
    return ValueList();
}
overload envEntryToValueList(x:EnvEntry) {
    return envEntryToValueList(*x);
}
overload envEntryToValueList(x:ValueList) {
    return x;
}
overload envEntryToValueList(x:PatternCell) {
    genericError("pattern variable cannot be used in non-pattern expression");
    return ValueList();
}
overload envEntryToValueList(x:ListPatternCell) {
    genericError("pattern variable cannot be used in non-pattern expression");
    return ValueList();
}
overload envEntryToValueList(x:Alias) {
    genericError("single value used in multi-valued context");
    return ValueList();
}
overload envEntryToValueList(x:ListAlias) {
    return evaluateMultiExprList(x.value, x.env);
}
overload envEntryToValueList(x:RTExternal) {
    genericError("external functions not available in static context");
    return ValueList();
}
overload envEntryToValue(x:RTGlobal) {
    genericError("non-alias global variables not available in static context");
    return Value();
}
overload envEntryToValue(x:RTListGlobal) {
    genericError("non-alias global variables not available in static context");
    return Value();
}
overload envEntryToValueList(x:RTValue) {
    genericError("single value used in multi-valued context");
    return ValueList();
}
overload envEntryToValueList(x:RTValueList) {
    return ValueList(mapped(v => rtTypeToValue(v.type), x.vars));
}
overload envEntryToValueList(x:RTLabel) {
    genericError("labels not available in static context");
    return ValueList();
}



//
// evaluateCallWithOneResult, evaluateCall
//

evaluateCallWithOneResult(x, env, callAst) {
    var results = evaluateCall(x, env, callAst);
    ensureArity(1, size(results));
    return move(results[0]);
}

evaluateCall(x, env, callAst) = evaluateStrictCall(x, evaluateExprList(callAst.args, env));
overload evaluateCall(x:Value, env, callAst) = evaluateCall(*x, env, callAst);
overload evaluateCall(x:AliasPrimitive, env, callAst) = evaluateAliasPrimitive(x.kind, env, callAst);

procedure evaluateStrictCall;

overload evaluateStrictCall(x, args) {
    genericError("invalid callee: ", x);
    return ValueList();
}
overload evaluateStrictCall(x:Value, args) {
    return evaluateStrictCall(*x, args);
}
overload evaluateStrictCall(x:LambdaValue, args) {
    var env2 = bindArgs(x.env, x.value.args, args);
    return evaluateLambdaCodeBody(*x.value.body, env2);
}
overload evaluateStrictCall(x:Symbol, args) {
    return memoizeStaticCall(x.evalTable, x.head.module.program, Value(x), args);
}
overload evaluateStrictCall(x:SymbolHead, args) {
    return memoizeStaticCall(x.evalTable, x.module.program, Value(x), args);
}
overload evaluateStrictCall(x:Primitive, args) {
    return evaluatePrimitive(x.kind, args);
}
overload evaluateStrictCall(x:AliasPrimitive, args) {
    genericError(primitiveName(x), " not supported in this context");
    return ValueList();
}

evaluateStrictCallWithOneResult(x, env) {
    var results = evaluateStrictCall(x, env);
    ensureArity(1, size(results));
    return move(results[0]);
}

evaluateLambdaCodeBody(x:ast.ExprList, env) = evaluateExprList(x, env);
overload evaluateLambdaCodeBody(x:ast.Block, env) {
    genericError("lambda block statement not allowed in static context");
    return ValueList();
}


//
// evaluateIndexing
//

procedure evaluateIndexing;

overload evaluateIndexing(x, args) {
    genericError("invalid static indexee: ", x);
    return Value();
}
overload evaluateIndexing(x:Value, args) {
    return evaluateIndexing(*x, args);
}
overload evaluateIndexing(x:SymbolHead, args) {
    verifyBindingArity(x.ast.name.params, args);
    return Value(makeSymbol(x, args));
}
overload evaluateIndexing(x:StringValue, args) {
    if (size(args) != 1)
        genericError("string must be indexed with a single Int");
    return evaluatePrimitiveWithOneResult(PRIM_StaticStringChar, ValueList(Value(x), args[0]));
}
overload evaluateIndexing(x:TupleValue, args) {
    if (size(args) != 1)
        genericError("tuple must be indexed with a single Int");
    return evaluatePrimitiveWithOneResult(PRIM_StaticTupleElement, ValueList(Value(x), args[0]));
}


//
// evaluateFieldRef
//

procedure evaluateFieldRef;

overload evaluateFieldRef(x, field) {
    genericError("invalid field reference: ", x);
    return Value();
}
overload evaluateFieldRef(x:Value, field) {
    return evaluateFieldRef(*x, field);
}
overload evaluateFieldRef(x:ModuleValue, field) {
    return evaluatePrimitiveWithOneResult(PRIM_StaticModuleMember, ValueList(Value(x), field));
}



//
// evaluatePattern
//

procedure evaluatePattern;

overload evaluatePattern(x:ast.Pattern, env) {
    return withLocation(x, () ref=> evaluatePattern(*x, env));
}
overload evaluatePattern(x:ast.BoolLiteral, env) {
    return Pattern(evaluateExpr(x, env));
}
overload evaluatePattern(x:ast.IntLiteral, env) {
    return Pattern(evaluateExpr(x, env));
}
overload evaluatePattern(x:ast.StringLiteral, env) {
    return Pattern(evaluateExpr(x, env));
}
overload evaluatePattern(x:ast.CharLiteral, env) {
    return Pattern(evaluateExpr(x, env));
}
overload evaluatePattern(x:ast.NameRef, env) {
    return Pattern(evaluateExpr(x, env));
}
overload evaluatePattern(x:ast.PatternNameRef, env) {
    return Pattern(lookupPattern(env, x.name));
}
overload evaluatePattern(x:ast.IndexingPattern, env) {
    var base = evaluateExpr(x.base, env);
    var args = evaluateListPattern(x.args, env);
    return evaluateIndexingPattern(base, args);
}



//
// evaluateIndexingPattern
//

procedure evaluateIndexingPattern;

overload evaluateIndexingPattern(x, args) {
    genericError("invalid pattern expression");
    return Pattern();
}
overload evaluateIndexingPattern(x:Value, args) {
    return evaluateIndexingPattern(*x, args);
}
overload evaluateIndexingPattern(x:SymbolHead, args) {
    return Pattern(SymbolPattern(x, args));
}



//
// evaluateListPattern
//

evaluateListPattern(x:ast.ListPattern, env) ListPattern {
    return ListPattern(
        map(y => evaluatePattern(y, env), x.items),
        maybe(x.variadicItem,
            y => evaluateListPatternCell(y, env),
            () => ListPatternCell(Maybe(ValueList())),
        ),
    );
}



//
// evaluateListPatternCell
//

procedure evaluateListPatternCell;

overload evaluateListPatternCell(x, env) {
    genericError("single value pattern used in multi-pattern context");
    return ListPatternCell();
}
overload evaluateListPatternCell(x:ast.Pattern, env) {
    return withLocation(x, () ref=> evaluateListPatternCell(*x, env));
}
overload evaluateListPatternCell(x:ast.PatternNameRef, env) {
    return lookupListPattern(env, x.name);
}



//
// unify
//

procedure unify;
overload unify(p:Pattern, x:Value) {
    return unify(*p, x);
}
overload unify(p:Value, x:Value) {
    return p == x;
}
overload unify(p:PatternCell, x:Value) {
    return maybe(p.value,
        y ref=> (y == x),
        () ref=> {
            p.value = Maybe(x);
            return true;
        }
    );
}
overload unify(p:SymbolPattern, x:Value) {
    if (not variantIs?(x, Symbol))
        return false;
    var y = variantAs(x, Symbol);
    if (p.head != y.head)
        return false;
    return unifyList(p.params, y.params);
}



//
// unifyList
//

procedure unifyList;

[A | SizedSequence?(A) and SequenceElementType(A) == Value]
overload unifyList(listPattern:ListPattern, values:A) {
    if (size(listPattern.patterns) > size(values))
        return false;
    for (pattern, value in zipped(listPattern.patterns, values))
        if (not unify(pattern, value))
            return false;
    var restValues = slicedFrom(values, size(listPattern.patterns));
    return unifyList(listPattern.variadicPattern, restValues);
}

[A | SizedSequence?(A) and SequenceElementType(A) == Value]
overload unifyList(listCell:ListPatternCell, values:A) {
    return maybe(listCell.values,
        v ref=> {
            if (size(v) != size(values))
                return false;
            for (a, b in zipped(v, values))
                if (a != b)
                    return false;
            return true;
        },
        () ref=> {
            listCell.values = Maybe(ValueList(values));
            return true;
        },
    );
}



//
// lookupPattern, derefPattern
//

procedure lookupPattern;

overload lookupPattern(env, name:ast.Identifier) {
    return withLocation(name, () ref=> {
        return lookupPattern(privateLookup(env, name));
    });
}
overload lookupPattern(x) {
    genericError("non pattern variable used as pattern variable");
    return PatternCell();
}
overload lookupPattern(x:EnvEntry) {
    return lookupPattern(*x);
}
overload lookupPattern(x:PatternCell) {
    return x;
}
overload lookupPattern(x:ListPatternCell) {
    genericError("multi-value pattern used in single value context");
    return PatternCell();
}

procedure derefPattern;

overload derefPattern(env, name:ast.Identifier) {
    return withLocation(name, () ref=> {
        return derefPattern(lookupPattern(env, name));
    });
}
overload derefPattern(x:PatternCell) {
    return maybe(x.value,
        y => y,
        () => {
            genericError("unbound pattern variable");
            return Value();
        },
    );
}



//
// lookupListPattern, derefListPattern
//

procedure lookupListPattern;

overload lookupListPattern(env, name:ast.Identifier) {
    return withLocation(name, () ref=> {
        return lookupListPattern(privateLookup(env, name));
    });
}
overload lookupListPattern(x) {
    genericError("non pattern variable used as pattern variable");
    return ListPatternCell();
}
overload lookupListPattern(x:EnvEntry) {
    return lookupListPattern(*x);
}
overload lookupListPattern(x:PatternCell) {
    genericError("single value pattern used in multi-pattern context");
    return ListPatternCell();
}
overload lookupListPattern(x:ListPatternCell) {
    return x;
}

procedure derefListPattern;

overload derefListPattern(env, name:ast.Identifier) {
    return withLocation(name, () ref=> {
        return derefListPattern(lookupListPattern(env, name));
    });
}
overload derefListPattern(x:ListPatternCell) {
    return maybe(x.values,
        y => y,
        () => {
            genericError("unbound pattern variable");
            return ValueList();
        },
    );
}



//
// walkPattern, walkMultiPattern, walkListPattern, etc.
//

procedure walkPattern;
overload walkPattern(x:ast.Pattern, visitor) {
    withLocation(x, () ref=> {
        visitor(x, false);
        walkPattern(*x, visitor);
    });
}
overload walkPattern(x:ast.BoolLiteral, visitor) {}
overload walkPattern(x:ast.CharLiteral, visitor) {}
overload walkPattern(x:ast.IntLiteral, visitor) {}
overload walkPattern(x:ast.StringLiteral, visitor) {}
overload walkPattern(x:ast.NameRef, visitor) {}
overload walkPattern(x:ast.PatternNameRef, visitor) {}
overload walkPattern(x:ast.IndexingPattern, visitor) {
    walkListPattern(x.args, visitor);
}

procedure walkMultiPattern;
overload walkMultiPattern(x, visitor) {
    genericError("not a multi-valued pattern");
}
overload walkMultiPattern(x:ast.Pattern, visitor) {
    withLocation(x, () ref=> {
        visitor(x, true);
        walkMultiPattern(*x, visitor);
    });
}
overload walkMultiPattern(x:ast.PatternNameRef, visitor) {
}

walkListPattern(x:ast.ListPattern, visitor) {
    for (y in x.items)
        walkPattern(y, visitor);
    maybe(x.variadicItem,
        y ref=> { walkMultiPattern(y, visitor); },
        () => {},
    );
}



//
// walkArgumentPatterns, walkArgumentPattern, walkVariadicArgumentPattern

walkArgumentPatterns(x:ast.VariadicList[ast.Argument], visitor) {
    for (y in x.items)
        walkArgumentPattern(*y, visitor);
    maybe(x.variadicItem,
        x ref=> { walkVariadicArgumentPattern(*x, visitor); },
        () => {},
    );
}

walkArgumentPattern(x:ast.BoundArgument, visitor) {
    maybe(x.type, p ref=> { walkPattern(p, visitor); }, () => {});
}
overload walkArgumentPattern(x:ast.StaticArgument, visitor) {
    walkPattern(x.pattern, visitor);
}

walkVariadicArgumentPattern(x:ast.BoundArgument, visitor) {
    maybe(x.type, p ref=> { walkMultiPattern(p, visitor); }, () => {});
}
overload walkVariadicArgumentPattern(x:ast.StaticArgument, visitor) {
    walkMultiPattern(x.pattern, visitor);
}



//
// PatternVarCollector
//

record PatternVarCollector(
    names: HashMap[String, Bool],
    vars: Vector[ast.Identifier],
    variadicVars: Vector[ast.Identifier],
);

overload call(x:PatternVarCollector, pattern, variadic?) {
    if (not variantIs?(pattern, ast.PatternNameRef))
        return;
    ref name = variantAs(pattern, ast.PatternNameRef).name;
    if (contains?(x.names, name.str)) {
        if (x.names[name.str] != variadic?) {
            genericError(
                "pattern variable cannot be used in both ",
                "single-value and multi-value context"
            );
        }
    }
    else {
        x.names[name.str] = variadic?;
        if (variadic?)
            push(x.variadicVars, name);
        else
            push(x.vars, name);
    }
}



//
// topLevelItemPatternVars
//

procedure TopLevelItemWithPatternVars?(x) = false;
overload TopLevelItemWithPatternVars?(static Attribute) = true;
overload TopLevelItemWithPatternVars?(static StaticOverload) = true;
overload TopLevelItemWithPatternVars?(static Overload) = true;

private procedure applyCollector;
overload applyCollector(x:Attribute, collector) {
    walkPattern(x.ast.target, collector);
}
overload applyCollector(x:StaticOverload, collector) {
    walkPattern(x.ast.target, collector);
    walkListPattern(x.ast.definition.args, collector);
}
overload applyCollector(x:Overload, collector) {
    walkPattern(x.ast.target, collector);
    walkArgumentPatterns(x.ast.definition.args, collector);
}

[T | TopLevelItemWithPatternVars?(T)]
topLevelItemPatternVars(x:T) {
    return maybe(x.patternVars,
        y ref=> y,
        () ref=> {
            var c = PatternVarCollector();
            applyCollector(x, c);
            var pvars = PatternVars(move(c.vars), move(c.variadicVars));
            x.patternVars = Maybe(pvars);
            return pvars;
        },
    );
}



//
// initializeMatch, verifyPatternVars, testPredicate
//

[E | VariantMember?(EnvParent, E)]
initializeMatch(baseEnv:E, pvars:PatternVars) {
    var env = newEnv(baseEnv);
    for (name in pvars.vars)
        envAdd(env, name, PatternCell(nothing(Value)));
    for (name in pvars.variadicVars)
        envAdd(env, name, ListPatternCell(nothing(ValueList)));
    return env;
}

verifyPatternVars(pvars:PatternVars, env:Env) {
    for (name in pvars.vars)
        derefPattern(env, name);
    for (name in pvars.variadicVars)
        derefListPattern(env, name);
}

testPredicate(predicate:Maybe[ast.Expr], env) {
    try {
        return maybe(predicate,
            pred => evaluateBool(pred, env),
            () => true,
        );
    } catch (e:StaticError) {
        return false;
    }
}



//
// matchAttribute, matchStaticOverload
//

matchAttribute(x:Attribute, target:Value) Maybe[Env] {
    var pvars = topLevelItemPatternVars(x);
    var env = initializeMatch(x.module, pvars);
    var targetPattern = evaluatePattern(x.ast.target, env);
    if (not unify(targetPattern, target))
        return nothing(Env);
    verifyPatternVars(pvars, env);
    if (not testPredicate(x.ast.predicate, env))
        return nothing(Env);
    return Maybe(env);
}

matchStaticOverload(x:StaticOverload, callable:Value, args:ValueList) Maybe[Env] {
    var pvars = topLevelItemPatternVars(x);
    var env = initializeMatch(x.module, pvars);
    var callablePattern = evaluatePattern(x.ast.target, env);
    if (not unify(callablePattern, callable))
        return nothing(Env);
    var argsPattern = evaluateListPattern(x.ast.definition.args, env);
    if (not unifyList(argsPattern, args))
        return nothing(Env);
    verifyPatternVars(pvars, env);
    if (not testPredicate(x.ast.definition.predicate, env))
        return nothing(Env);
    return Maybe(env);
}



//
// evaluateAttributes
//

evaluateAttributes(target) {
    var values = ValueList();
    for (x in getProgram().allAttributes) {
        var result = matchAttribute(x, target);
        if (not nothing?(result))
            push(values, evaluateExpr(x.ast.body, just(result)));
    }
    return move(values);
}


//
// runtimeCallable?
//

runtimeCallable?(x:Value) {
    return match(x,
        SymbolHead, y => symbolHeadTypeTemplate?(y),
        Symbol, y => (symbolType?(y) or symbolProcedure?(y)),
        MatchDefault, y => false,
    );
}

staticCallable?(x:Value) {
    return match(x,
        SymbolHead, y => true,
        Symbol, y => true,
        MatchDefault, y => false,
    );
}


//
// memoizeStaticCall, evaluateStaticCall
//

memoizeStaticCall(evalTable, program, callable, args) {
    var memoizedResultp = lookup(evalTable, args);
    if (null?(memoizedResultp)) {
        // observe(#evaluating, callable, args);
        ref result = evalTable[args];
        result = evaluateStaticCall(program, callable, args);
        return result;
    } else {
        // observe(#memoized, callable, args);
        return memoizedResultp^;
    }
}

evaluateStaticCall(program, callable, args) {
    return withStaticCall(callable, args, () ref=> {
        for (x in reversed(program.allStaticOverloads)) {
            var result = matchStaticOverload(x, callable, args);
            if (just?(result))
                return evaluateExprList(x.ast.definition.body, just(result));
        }
        genericError("no matching operation");
        return ValueList();
    });
}



//
// evaluatePrimitive
//

private oneValue(args) {
    ensureArity(1, size(args));
    return args[0];
}

private twoValues(args) {
    ensureArity(2, size(args));
    return args[0], args[1];
}

private threeValues(args) {
    ensureArity(3, size(args));
    return args[0], args[1], args[2];
}

private oneInt(args) {
    var a = oneValue(args);
    if (not variantIs?(a, IntValue))
        genericError("expecting one integer");
    return variantAs(a, IntValue);
}

private twoInts(args) {
    var a, b = ...twoValues(args);
    if (not variantIs?(a, IntValue) or not variantIs?(b, IntValue))
        genericError("expecting two integers");
    return variantAs(a, IntValue), variantAs(b, IntValue);
}

private oneString(args) {
    var a = oneValue(args);
    if (not variantIs?(a, StringValue))
        genericError("expecting one string");
    return variantAs(a, StringValue).value;
}

private stringInt(args) {
    var a, b = ...twoValues(args);
    if (not variantIs?(a, StringValue)
        or not variantIs?(b, IntValue))
    {
        genericError("expecting a string and an Int");
    }
    return ...(
        variantAs(a, StringValue).value,
        variantAs(b, IntValue),
    );
}

private stringIntInt(args) {
    var a, b, c = ...threeValues(args);
    if (not variantIs?(a, StringValue)
        or not variantIs?(b, IntValue)
        or not variantIs?(c, IntValue))
    {
        genericError("expecting a string, and two Ints");
    }
    return ...(
        variantAs(a, StringValue).value,
        variantAs(b, IntValue),
        variantAs(c, IntValue),
    );
}

private nStrings(args) {
    var result = Vector[String]();
    for (i, x in enumerated(args)) {
        if (not variantIs?(x, StringValue))
            genericError("expecting a string, but received: ", x);
        push(result, variantAs(x, StringValue).value);
    }
    return move(result);
}

private nChars(args) {
    var result = Vector[Char]();
    for (i, x in enumerated(args)) {
        if (not variantIs?(x, CharValue))
            genericError("expecting a char, but received: ", x);
        push(result, variantAs(x, CharValue).value);
    }
    return move(result);
}

private oneTuple(args) {
    var a = oneValue(args);
    if (not variantIs?(a, TupleValue))
        genericError("expecting a tuple, but received: ", a);
    return variantAs(a, TupleValue).elements;
}

private tupleInt(args) {
    var a, b = ...twoValues(args);
    if (not variantIs?(a, TupleValue) or not variantIs?(b, IntValue))
        genericError("expecting a tuple and a int");
    return variantAs(a, TupleValue).elements, variantAs(b, IntValue);
}

private oneSymbol(args) {
    var a = oneValue(args);
    if (not variantIs?(a, Symbol))
        genericError("expecting a symbol, but received: ", a);
    return variantAs(a, Symbol);
}

private moduleString(args) {
    var moduleValue, stringValue = ...twoValues(args);
    if (not variantIs?(moduleValue, ModuleValue) or not variantIs?(stringValue, StringValue)) {
        genericError("expecting a module and an identifier string");
    }
    return variantAs(moduleValue, ModuleValue), variantAs(stringValue, StringValue);
}

evaluatePrimitiveWithOneResult(kind, args) {
    var results = evaluatePrimitive(kind, move(args));
    ensureArity(1, size(results));
    return move(results[0]);
}

private evaluateStaticCompareEquality(a:Value, b:Value, op) = ValueList(BoolValue(op(a, b)));

private evaluateStaticCompare(a, b, op) {
    genericError("static comparison is only applicable to Bool,Bool; Char,Char; Int,Int; String,String; or Tuple,Tuple arguments");
    return ValueList();
}

[T | inValues?(T, BoolValue, CharValue, StringValue)]
overload evaluateStaticCompare(a:T, b:T, op) = ValueList(BoolValue(op(a.value, b.value)));
overload evaluateStaticCompare(a:IntValue, b:IntValue, op) = ValueList(BoolValue(op(a, b)));

overload evaluateStaticCompare(a:TupleValue, b:TupleValue, op) {
    for (av, bv in zipped(a.elements, b.elements)) {
        if (av != bv)
            return evaluateStaticCompare(*av, *bv, op);
    }
    return ValueList(BoolValue(op(size(a.elements), size(b.elements))));
}

evaluatePrimitive(kind, args) {
    switch (kind) {
    case PRIM_StaticInt? : {
        var a = oneValue(args);
        return ValueList(BoolValue(variantIs?(a, IntValue)));
    }
    case PRIM_StaticPlus : {
        var a = oneInt(args);
        return ValueList(+a);
    }
    case PRIM_StaticMinus : {
        var a = oneInt(args);
        return ValueList(-a);
    }
    case PRIM_StaticAdd : {
        var a, b = ...twoInts(args);
        return ValueList(a + b);
    }
    case PRIM_StaticSubtract : {
        var a, b = ...twoInts(args);
        return ValueList(a - b);
    }
    case PRIM_StaticMultiply : {
        var a, b = ...twoInts(args);
        return ValueList(a * b);
    }
    case PRIM_StaticDivide : {
        var a, b = ...twoInts(args);
        return ValueList(a / b);
    }
    case PRIM_StaticRemainder : {
        var a, b = ...twoInts(args);
        return ValueList(a % b);
    }
    case PRIM_StaticIntegers : {
        var a = Int64(oneInt(args));
        if (a < 0)
            genericError("expecting a non-negative integer");
        return ValueList(mapped(i => Value(IntValue(StaticIntValue(i))), range(a)));
    }

    case PRIM_StaticBool? : {
        var a = oneValue(args);
        return ValueList(BoolValue(variantIs?(a, BoolValue)));
    }
    case PRIM_StaticEquals? : {
        var a, b = ...twoValues(args);
        return evaluateStaticCompareEquality(a, b, equals?);
    }
    case PRIM_StaticNotEquals? : {
        var a, b = ...twoValues(args);
        return evaluateStaticCompareEquality(a, b, notEquals?);
    }
    case PRIM_StaticLesser? : {
        var a, b = ...twoValues(args);
        return evaluateStaticCompare(*a, *b, lesser?);
    }
    case PRIM_StaticLesserEquals? : {
        var a, b = ...twoValues(args);
        return evaluateStaticCompare(*a, *b, lesserEquals?);
    }
    case PRIM_StaticGreater? : {
        var a, b = ...twoValues(args);
        return evaluateStaticCompare(*a, *b, greater?);
    }
    case PRIM_StaticGreaterEquals? : {
        var a, b = ...twoValues(args);
        return evaluateStaticCompare(*a, *b, greaterEquals?);
    }

    case PRIM_StaticString? : {
        var a = oneValue(args);
        return ValueList(BoolValue(variantIs?(a, StringValue)));
    }
    case PRIM_StaticStringSize : {
        var s = oneString(args);
        return ValueList(IntValue(StaticIntValue(Int64(size(s)))));
    }
    case PRIM_StaticStringSlice : {
        var s, i_, j_ = ...stringIntInt(args);
        var i = Int64(i_);
        var j = Int64(j_);
        if ((i < 0) or (i > size(s)))
            genericError("invalid starting index: ", i);
        if ((j < i) or (j > size(s)))
            genericError("invalid ending index: ", j);
        return ValueList(StringValue(slice(s, i, j)));
    }
    case PRIM_StaticStringConcat : {
        var a = nStrings(args);
        var result = String();
        for (s in a)
            push(result, move(s));
        return ValueList(StringValue(move(result)));
    }
    case PRIM_StaticStringChar : {
        var s, i_ = ...stringInt(args);
        var i = Int64(i_);

        if ((i < 0) or (i >= size(s)))
            genericError("invalid index into string: ", i);
        return ValueList(CharValue(s[i]));
    }
    case PRIM_StaticStringChars : {
        var s = oneString(args);
        return ValueList(mapped(c => Value(CharValue(c)), s));
    }
    case PRIM_StaticStringFromChars : {
        var s = nChars(args);
        return ValueList(StringValue(s));
    }

    case PRIM_StaticTuple? : {
        var a = oneValue(args);
        return ValueList(BoolValue(variantIs?(a, TupleValue)));
    }
    case PRIM_StaticTuple : {
        return ValueList(TupleValue(args));
    }
    case PRIM_StaticTupleSize : {
        var a = oneTuple(args);
        return ValueList(IntValue(Int(size(a))));
    }
    case PRIM_StaticTupleElement : {
        var a, i_ = ...tupleInt(args);
        var i = Int64(i_);
        if ((i < 0) or (i > size(a)))
            genericError("tuple index out of range");
        return ValueList(a[i]);
    }
    case PRIM_StaticTupleElements : {
        return oneTuple(args);
    }

    case PRIM_StaticSymbol? : {
        var a = oneValue(args);
        return ValueList(BoolValue(variantIs?(a, Symbol)));
    }
    case PRIM_StaticSymbolBody : {
        var a = oneSymbol(args);
        return ValueList(symbolBody(a));
    }
    case PRIM_StaticSymbolAttributes : {
        var a = oneSymbol(args);
        return ValueList(symbolAttributes(a));
    }
    case PRIM_StaticModuleMember : {
        var module, member = ...moduleString(args);
        return ValueList(envEntryToValue(moduleValueLookup(module, member.value)));
    }

    case PRIM_StaticError : {
        throw StaticError(args);
    }

    case PRIM_StaticAssert : {
        if (empty?(args))
            genericError("StaticAssert requires at least one argument");
        if (args[0] == Value(BoolValue(false))) {
            if (size(args) == 1)
                throw StaticError(ValueList(StringValue(String("assertion failure"))));
            else
                throw StaticError(ValueList(slicedFrom(args, 1)));
        }
        return ValueList(TupleValue());
    }

    default : {
        assert(false, "invalid primitive");
        return ValueList();
    }
    }
}

private argVariables(env, argsAst) {
    var body = Vector[ir.Node]();
    var complete? = false;
    var s = SpecializationContext(&body, &complete?);
    return analyzeExprListNode(s, env, argsAst);
}

private refType(v) {
    var refSymbolHead = Value();
    switch (v.kind) {
    case RT_VALUE_REF:
        refSymbolHead = getHooks().Ref;
        break;
    case RT_VALUE_CONST:
        refSymbolHead = getHooks().Const;
        break;
    case RT_VALUE_RVALUE:
        refSymbolHead = getHooks().Rvalue;
        break;
    }

    return Value(makeSymbol(variantAs(refSymbolHead, SymbolHead), ValueList(v.type)));
}

private expressionStringAst(env, argsAst:ast.ExprList) {
    if (size(argsAst) == 1)
        return match(argsAst[0],
            ast.UnpackExpr, x ref=> expressionStringAst2(env, argsAst, x.expr),
            ast.Expr, x ref=> expressionStringAst2(env, argsAst, x),
        );
    return argsAst;
}

private expressionStringAst2(env, argsAst:ast.ExprList, expr:ast.Expr)
    = match(expr,
        ast.NameRef, x ref=> expressionStringAst3(argsAst, nameRefLookup(env, x)),
        ast.SimpleNameRef, x ref=> expressionStringAst3(argsAst, privateLookup(env, x.name)),
        MatchDefault, x ref=> argsAst,
    );

private expressionStringAst3(argsAst, entry:EnvEntry)
    = match(entry,
        Alias, x ref=> ast.ExprList(ast.ExprListItem(x.value)),
        ListAlias, x ref=> x.value,
        MatchDefault, x ref=> argsAst,
    );

evaluateAliasPrimitive(kind, env, callAst) {
    switch (kind) {

    case APRIM_Returns? : {
        return ValueList(BoolValue(just?(argVariables(env, callAst.args))));
    }

    case APRIM_Type : {
        return maybe(argVariables(env, callAst.args),
            vars ref=> ValueList(map(v => v.type, vars)),
            ()   ref=> ValueList(),
        );
    }

    case APRIM_RefType : {
        return maybe(argVariables(env, callAst.args),
            vars ref=> ValueList(map(v => refType(v), vars)),
            ()   ref=> ValueList(),
        );
    }

    case APRIM_ExpressionString : {
        var exprAst = expressionStringAst(env, callAst.args);
        if (empty?(exprAst))
            return ValueList(StringValue(String("")));
        var sourceTokens = ast.getLocation(exprAst[0]).tokens;
        var firstToken = ast.getLocation(exprAst[0]).tokenRange.begin;
        var lastToken = ast.getLocation(exprAst[size(exprAst) - 1]).tokenRange.end;
        var beginChar, endChar = sourceTokens.vector[firstToken].range.begin,
                                 sourceTokens.vector[lastToken].range.end - 1;
        return ValueList(StringValue(slice(sourceTokens.file.data, beginChar, endChar)));
    }

    case APRIM_ExpressionLocation : {
        var exprAst = expressionStringAst(env, callAst.args);
        var locAst = if (empty?(exprAst))
                ast.ExprListItem(ast.Expr(callAst))
            else
                exprAst[0];

        var location = ast.getLocation(locAst);
        ref path = location.tokens.file.path;
        var data = location.tokens.file.data;
        var tokenOffset = location.tokenRange.begin;
        var offset = location.tokens.vector[tokenOffset].range.begin;
        var line, column, tabs = ...computeLocation(data, offset);

        return ValueList(
            Value(StringValue(path)),
            Value(IntValue(StaticIntValue(Int64(line+1)))),
            Value(IntValue(StaticIntValue(Int64(column + 7*tabs))))
        );
    }

    default : {
        assert(false, "invalid primitive");
        return ValueList();
    }
    }
}

import __primitives__.*;


//
// MapValues
//

symbol MapValues;
overload #MapValues('f, 'first, ...'rest) = 'f('first), ...MapValues('f, ...'rest);
overload #MapValues('f) = ;



//
// clay types as symbols
//

symbol Bool;
symbol Int;
symbol Array['T, 'n];
symbol Tuple[...'T];

symbol String;


symbol TupleWithTag?;
overload #TupleWithTag?('x, 'tag) =
    StaticTuple?('x)
    and StaticLesser?(0, StaticTupleSize('x))
    and StaticEquals?(StaticTupleElement('x, 0), 'tag);



//
// record types
//

symbol RecordTag;

symbol RecordType;
overload #RecordType(...'fields) =
    (RecordTag, FieldNames(...'fields), FieldTypes(...'fields));

symbol FieldNames;
overload #FieldNames(...'fields) =
    (...MapValues(f => StaticTupleElement(f, 0), ...'fields));

symbol FieldTypes;
overload #FieldTypes(...'fields) =
    (...MapValues(f => StaticTupleElement(f, 1), ...'fields));

symbol Record?;
overload #Record?('t) =
    StaticSymbol?('t)
    and TupleWithTag?(StaticSymbolBody('t), RecordTag);

symbol RecordFieldCount;
overload #RecordFieldCount('t) | Record?('t) =
    StaticTupleSize(StaticTupleElement(StaticSymbolBody('t), 1));

symbol RecordFieldNames;
overload #RecordFieldNames('t) | Record?('t) =
    StaticTupleElement(StaticSymbolBody('t), 1);

symbol RecordFieldTypes;
overload #RecordFieldTypes('t) | Record?('t) =
    StaticTupleElement(StaticSymbolBody('t), 2);



//
// variant types
//

symbol VariantTag;

symbol VariantType;
overload #VariantType(...'members) = (VariantTag, (...'members));

symbol Variant?;
overload #Variant?('t) = 
    StaticSymbol?('t)
    and TupleWithTag?(StaticSymbolBody('t), VariantTag);

symbol VariantMembers;
overload #VariantMembers('t) | Variant?('t) = (
    ...StaticTupleElements(StaticTupleElement(StaticSymbolBody('t), 1)),
    ...StaticTupleElements(StaticSymbolAttributes('t)),
);



//
// testing
//

symbol GenericError = RecordType(
    ("msg", String),
);

symbol Exception = VariantType(GenericError);

symbol AssertionError = RecordType(
    ("condition", String),
    ("msg", String),
);

attribute Exception = AssertionError;

symbol Main;
overload #Main() = (
    Variant?(String),
    Variant?(Exception),
    VariantMembers(Exception)
);
